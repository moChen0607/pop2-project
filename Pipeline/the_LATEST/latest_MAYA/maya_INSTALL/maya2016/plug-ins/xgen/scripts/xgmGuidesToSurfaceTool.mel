// Copyright (C) 1997-2013 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

/**
 * @file xgmGuidesToSurfaceTool.mel
 * @brief Contains the XgmGuideToSurfaceTool MEL procedure.
 *
 * <b>CONFIDENTIAL INFORMATION: This software is the confidential and
 * proprietary information of Walt Disney Animation Studios ("WDAS").
 * This software may not be used, disclosed, reproduced or distributed
 * for any purpose without prior written authorization and license
 * from WDAS. Reproduction of any section of this software must include
 * this legend and all copyright notices.
 * Copyright Disney Enterprises, Inc. All rights reserved.</b>
 *
 * @author Lawrence Chai
 * @author Ying Liu
 *
 * @version Created 01/30/07
 */


/**
 * User interface for converting guides to a nurbsSurface and back.
 */
global proc xgmGuidesToSurfaceTool(string $parent)
{
    // Just use a column layout as the controls are fairly simple.
    columnLayout -parent $parent -adj on;
    {
        rowLayout -nc 2 -cw2 180 180 -cl2 "center" "center";
        button -l (uiRes("m_xgmGuidesToSurfaceTool.kConvertGuidesToSurface")) -w 180 
            -ann (uiRes("m_xgmGuidesToSurfaceTool.kConvertGuidesToSurfaceAnn"))
            -c xgmGuidesToSurface;
        button -l (uiRes("m_xgmGuidesToSurfaceTool.kConvertSurfaceToGuides")) -w 180 
            -ann (uiRes("m_xgmGuidesToSurfaceTool.kConertSurfaceToGuidesAnn"))
            -c xgmSurfaceToGuides;
        setParent ..;
    }
}


global proc xgmGuidesToSurface()
{
    // Create curves and delete guides
    string $curves[] = xgmCreateCurvesFromGuides(2, 0);
    if ( size($curves) < 2 ) {
	warning (uiRes("m_xgmGuidesToSurfaceTool.kNeedToSelect2OrMoreGuides"));
	return;
    }

    // Create surface from curves
    select -r $curves;
    string $surface[] = `loft -d 3`;
    string $gr = group($surface[0]);
    xgmGroomParent $gr;
    rename $gr xgGuidesToSurface;

    delete $curves;
}


global proc xgmSurfaceToGuides()
{
    string $desc = python( "xgui.currentDescription()" );
    if ($desc == "") {
        warning((uiRes("m_xgmGuidesToSurfaceTool.kNoDescriptionSelected")));
        return;
    }

    string $surface[] = `ls -sl -dag -type nurbsSurface`;
    if (!size($surface)) {
        warning((uiRes("m_xgmGuidesToSurfaceTool.kNoNurbssurfaceIsSelected")));
        return;
    }

    string $surfaceInfo = createNode("surfaceInfo");
    connectAttr -f ($surface[0] + ".worldSpace") ($surfaceInfo + ".inputSurface");
    float $knots[] = getAttr($surfaceInfo + ".knotsU");
    delete $surfaceInfo;

    float $prevKnot = -1.0;
    int $index = 0;
    string $curves[];
    for ($knot in $knots) {
        if ($knot != $prevKnot) {
            string $dup[] = `duplicateCurve -ch 0 ($surface[0] + ".u[" + $knot +"]")`;
            $curves[$index] = $dup[0];
            $index++;
            $prevKnot = $knot;
        }
    }
    delete `firstParentOf($surface[0])`;

    select $curves;
    string $command = "xgmCurveToGuide -d ";
    $command += $desc;
    $command += " -tsp 4.0 -tsa 0.0 -deleteCurve";
    eval($command);
}
