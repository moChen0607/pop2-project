/**
 * @file igCleanup.mel
 * @brief Contains the iGroom cleanup procedure for unloading the plugin.
 *
 * <b>CONFIDENTIAL INFORMATION: This software is the confidential and
 * proprietary information of Walt Disney Animation Studios ("WDAS").
 * This software may not be used, disclosed, reproduced or distributed
 * for any purpose without prior written authorization and license
 * from WDAS. Reproduction of any section of this software must include
 * this legend and all copyright notices.
 * Copyright Disney Enterprises, Inc. All rights reserved.</b>
 *
 * @author Thomas V Thompson II
 *
 * @version tthompso  05/14/07  Initial Version
 */


/**
 * Cleanup anything iGroom added to the environment in preparation for the
 * plugin to be unloaded.
 */
global proc igCleanup()
{
    // Get a list of all scriptjobs
    string $ascriptjobs[] = `scriptJob -lj`;
    for ( $sj in $ascriptjobs )
    {
        // Find iGroom related scriptjobs
        if ( `match "ig" $sj` != "" )
        {
            string $aTok[] = {};
            tokenize $sj " " $aTok;
            string $strjob = `substitute ":" $aTok[0] ""`;
            int $njob = $strjob;
            // Force-kill iGroom related script jobs
            catch( `scriptJob -force -k $njob` );
        }
    }

    // Remove our shelf buttons
    if (`shelfButton -ex -q "iGroomUIButton"`)
        deleteUI -control "iGroomUIButton";
    global string $igBrushList[];
    for ( $item in $igBrushList ) {
        string $bname = "igBrush" + $item + "ShelfButton";
        if (`shelfButton -ex -q $bname`)
            deleteUI -control $bname;
    }

    // Delete the UI if still up
    if (`window -exists iGroomWindow`) {
        deleteUI -window iGroomWindow;
    }
}
