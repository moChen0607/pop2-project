// Copyright (C) 1997-2013 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

/**
 * @file xgmStraightenGuideTool.mel
 * @brief Contains the MEL for the guide straightening tool.
 *
 * <b>CONFIDENTIAL INFORMATION: This software is the confidential and
 * proprietary information of Walt Disney Animation Studios ("WDAS").
 * This software may not be used, disclosed, reproduced or distributed
 * for any purpose without prior written authorization and license
 * from WDAS. Reproduction of any section of this software must include
 * this legend and all copyright notices.
 * Copyright Disney Enterprises, Inc. All rights reserved.</b>
 *
 * @author Patrick Witting
 * @author Ying Liu
 *
 * @version Created 02/02/05
 */

global proc xgmStraightenGuideTool( string $parent )
{
    columnLayout -parent $parent;
    floatSliderGrp 
        -ann ( (uiRes("m_xgmStraightenGuideTool.kStraightnessAnn")) )
        -label (uiRes("m_xgmStraightenGuideTool.kStraightness"))
        -cw3 80 80 240
        -field 1
        -min 0 
        -max 2.0
        -fieldMinValue -100000.0
        -fieldMaxValue 100000.0
        -pre 3
        -value 1.0 
        xgmStraightenGuideToolStraightness;

    gridLayout -numberOfColumns 2 -cellWidthHeight 200 25;

    button -label (uiRes("m_xgmStraightenGuideTool.kStraightenSelectedGuides")) -w 190 
        -ann (uiRes("m_xgmStraightenGuideTool.kStraightenSelectedGuidesAnn"))
        -command "xgmStraightenGuideToolStraighten";

    checkBox -label (uiRes("m_xgmStraightenGuideTool.kPreserveLength")) -v 1
        -ann (uiRes("m_xgmStraightenGuideTool.kPreserveLengthAnn"))
        xgmStraightenGuideToolPreserve;

    setParent ..;
}


/**
 *  This function invokes the straightening algorithm on the selected guides.
*/
global proc xgmStraightenGuideToolStraighten()
{
    string $selOrig[] = `ls -sl`;

    string $gList[] = xgmSelectedGuides( "", 0 );
    if ( !`size $gList` ) {
        warning (uiRes("m_xgmStraightenGuideTool.kPleaseSelectAtLeastOneGuideToStraghten"));
        return;
    }

    float $Straightness = `floatSliderGrp -q -v xgmStraightenGuideToolStraightness`;
    int $Preserve = `checkBox -q -v xgmStraightenGuideToolPreserve`;

    // process the whole guide - including base
    for ( $guide in $gList ) {
        float $points[] = xgmGuidePositions( $guide, 0, 9999 );
        float $points0[] = xgmCopyFloatArray( $points );
        float $data[] = `xgmGuideGeom -guide $guide -numVertices`;
        int $ncvs = $data[0];

        xgmMayaCurvesStraightenLine( $points, $ncvs, $Straightness, $Preserve );

        // now make a relative move to new position
        int $i;
        for ( $i=0; $i<$ncvs; $i++ ) {
            float $x[3];
            for ( $k=0; $k<3; $k++ ) {
                $x[$k] = $points[$i*3 + $k] - $points0[$i*3 + $k];
            }
            move -r $x[0] $x[1] $x[2] ( $guide + ".vtx[" + $i + "]" );
        }
    }

    select -r $selOrig;
}
