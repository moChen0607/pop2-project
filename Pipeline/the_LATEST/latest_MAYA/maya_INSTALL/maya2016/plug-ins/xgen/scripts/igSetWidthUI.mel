// Copyright (C) 1997-2013 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

/** Set the width of the fur in the active igmDescription. */
global proc igSetWidthUI()
{
    if (`window -exists igSetWidthWindow`)
        deleteUI -window  igSetWidthWindow;

    string $value = "0.1";
    string $desc = igCurrentDescription();
    if ( `attributeQuery -ex -node $desc "setWidth"` ) {
        $value = `getAttr ($desc+".setWidth")`;
    }
    
    window -sizeable false -minimizeButton false -maximizeButton false
	    -menuBar false -width 600 -height 100
	    -title (uiRes("m_igSetWidthUI.kIgroomSetWidth")) igSetWidthWindow;

    columnLayout -adj on -rs 10;
    {
        // Create a text field for the filename.
        textFieldGrp -label (uiRes("m_igSetWidthUI.kWidth")) -tx $value -cal 1 right -adj 2
            -ann (uiRes("m_igSetWidthUI.kWidthAnnot"))
            igSetWidthField;

        rowLayout -numberOfColumns 3 -columnWidth3 130 130 130
            -columnAlign3 "center" "center" "center"
            -adjustableColumn3 1;
        {
            // Control over the dialog
            text -label "";
            button -label (uiRes("m_igSetWidthUI.kApply")) -w 130
                -ann (uiRes("m_igSetWidthUI.kApplyAnnot"))
                -c "igSetWidth";
            button -label (uiRes("m_igSetWidthUI.kCancel")) -w 130 
                -ann (uiRes("m_igSetWidthUI.kCancelAnnot"))
                -c "deleteUI igSetWidthWindow";
            setParent ..;
        }

        setParent ..;
    }

    showWindow igSetWidthWindow;

    // Dont let the window pop-up the wrong size
    window -e -width 600 igSetWidthWindow;
    window -e -height 70 igSetWidthWindow;
}


global proc igSetWidth()
{
    global string $igNullDescriptionTag;

    string $desc = igCurrentDescription();
    if ( $desc != $igNullDescriptionTag && $desc != "" ) {
        waitCursor -state on;
        string $width = `textFieldGrp -q -tx igSetWidthField`;

        // Remember what was entered via attributes on the description
        if ( !`attributeQuery -ex -node $desc "setWidth"` ) {
            addAttr -ln "setWidth" -sn "setWidth" -dt "string" $desc;
        }
        setAttr -type "string" ($desc+".setWidth") $width;

        iGroom -sw $width -d $desc;
        waitCursor -state off;
    }
}
