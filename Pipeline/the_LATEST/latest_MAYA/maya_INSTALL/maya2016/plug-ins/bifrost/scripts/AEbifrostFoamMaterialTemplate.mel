// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	Procedure Name:
//	AEbifrostFoamMaterialTemplate
//
//	Description Name;
//	Creates the attribute editor controls for the bifrostFoamMaterial node
//

// ----------------------------------------------------------------------------
// ----------------------------------------------------------------------------
// Methods for managing the enable/disable state of controls
// ----------------------------------------------------------------------------
// ----------------------------------------------------------------------------
global proc AEbifrostFoamMaterialCheckDiffuseColorRemap(string $nodeName)
{
	string $diffuseColorRemapChannel = `getAttr ($nodeName + ".diffuseColorRemapChannel")`;
	int $diffuseColorRemapEnable = ($diffuseColorRemapChannel != "");

	editorTemplate -dimControl $nodeName "diffuseColor" $diffuseColorRemapEnable;
	AEbifrostvisRemapEnable $nodeName "diffuseColorRemap" "diffuseColorValueRemap" $diffuseColorRemapEnable;
}

global proc AEbifrostFoamMaterialCheckScatterWeightRemap(string $nodeName)
{
	string $scatterWeightRemapChannel = `getAttr ($nodeName + ".scatterWeightRemapChannel")`;
	int $scatterWeightRemapEnable = ($scatterWeightRemapChannel != "");

	editorTemplate -dimControl $nodeName "scatterWeight" $scatterWeightRemapEnable;
	AEbifrostvisRemapEnable $nodeName "" "scatterWeightRemap" $scatterWeightRemapEnable;
}

global proc AEbifrostFoamMaterialCheckReflectionWeightRemap(string $nodeName)
{
	string $reflectionWeightRemapChannel = `getAttr ($nodeName + ".reflectionWeightRemapChannel")`;
	int $reflectionWeightRemapEnable = ($reflectionWeightRemapChannel != "");

	editorTemplate -dimControl $nodeName "reflectionWeight" $reflectionWeightRemapEnable;
	AEbifrostvisRemapEnable $nodeName "" "reflectionWeightRemap" $reflectionWeightRemapEnable;
}

global proc AEbifrostFoamMaterialCheckTransparencyWeightRemap(string $nodeName)
{
	string $transparencyWeightRemapChannel = `getAttr ($nodeName + ".transparencyWeightRemapChannel")`;
	int $transparencyWeightRemapEnable = ($transparencyWeightRemapChannel != "");

	editorTemplate -dimControl $nodeName "transparencyWeight" $transparencyWeightRemapEnable;
	AEbifrostvisRemapEnable $nodeName "" "transparencyWeightRemap" $transparencyWeightRemapEnable;
}

global proc AEbifrostFoamMaterialCheckScatterUseDiffuseColor(string $nodeName)
{
	int $scatterUseDiffuseColor = `getAttr ($nodeName + ".scatterUseDiffuseColor")`;
	editorTemplate -dimControl $nodeName "scatterColor" $scatterUseDiffuseColor;
}

global proc AEbifrostFoamMaterialCheckReflectionUseDiffuseColor(string $nodeName)
{
	int $reflectionUseDiffuseColor = `getAttr ($nodeName + ".reflectionUseDiffuseColor")`;
	editorTemplate -dimControl $nodeName "reflectionColor" $reflectionUseDiffuseColor;
}

// ----------------------------------------------------------------------------
// ----------------------------------------------------------------------------
// Main template definition
// ----------------------------------------------------------------------------
// ---------------------------------------------------------------------------
global proc AEbifrostFoamMaterialTemplate(string $nodeName)
{
	AEswatchDisplay $nodeName;
	AEbifrostvisCommonTemplate;

	editorTemplate -beginScrollLayout;

		// Shader Type drop-down list
		editorTemplate -callCustom AEshaderTypeNew AEshaderTypeReplace "message";

		// Diffuse controls
		editorTemplate -beginLayout (getPluginResource("bifrostvisplugin", "kFoamDiffuse")) -collapse 0;
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kColor")) -addControl "diffuseColor";
			editorTemplate -callCustom "AEbifrostvisChannelListNew" "AEbifrostvisChannelListReplace"
				"diffuseColorRemapChannel" "kColorRemapChannel" "kParticle" "AEbifrostFoamMaterialCheckDiffuseColorRemap";
			editorTemplate -callCustom "AEbifrostvisRemapNew" "AEbifrostvisRemapReplace"
				"diffuseColorRemapInputMin" "diffuseColorRemapInputMax" "diffuseColorRemap" "diffuseColorValueRemap"
				"kFoamDiffuseColorRemap" "kMin" "kMax" "AEbifrostFoamMaterialCheckDiffuseColorRemap";
		editorTemplate -endLayout;

		// Scatter controls
		editorTemplate -beginLayout (getPluginResource("bifrostvisplugin", "kFoamScatter")) -collapse 0;
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kFoamUseDiffuseColor"))
				-addControl "scatterUseDiffuseColor" "AEbifrostFoamMaterialCheckScatterUseDiffuseColor";
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kColor")) -addControl "scatterColor";
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kWeight")) -addControl "scatterWeight";
			editorTemplate -callCustom "AEbifrostvisChannelListNew" "AEbifrostvisChannelListReplace"
				"scatterWeightRemapChannel" "kWeightRemapChannel" "kParticle" "AEbifrostFoamMaterialCheckScatterWeightRemap";
			editorTemplate -callCustom "AEbifrostvisRemapNew" "AEbifrostvisRemapReplace"
				"scatterWeightRemapInputMin" "scatterWeightRemapInputMax" "" "scatterWeightRemap"
				"kFoamScatterWeightRemap" "kMin" "kMax" "AEbifrostFoamMaterialCheckScatterWeightRemap";
		editorTemplate -endLayout;

		// Reflection controls
		editorTemplate -beginLayout (getPluginResource("bifrostvisplugin", "kFoamReflection")) -collapse 0;
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kFoamUseDiffuseColor"))
				-addControl "reflectionUseDiffuseColor" "AEbifrostFoamMaterialCheckReflectionUseDiffuseColor";
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kColor")) -addControl "reflectionColor";
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kWeight")) -addControl "reflectionWeight";
			editorTemplate -callCustom "AEbifrostvisChannelListNew" "AEbifrostvisChannelListReplace"
				"reflectionWeightRemapChannel" "kWeightRemapChannel" "kParticle" "AEbifrostFoamMaterialCheckReflectionWeightRemap";
			editorTemplate -callCustom "AEbifrostvisRemapNew" "AEbifrostvisRemapReplace"
				"reflectionWeightRemapInputMin" "reflectionWeightRemapInputMax" "" "reflectionWeightRemap"
				"kFoamReflectionWeightRemap" "kMin" "kMax" "AEbifrostFoamMaterialCheckReflectionWeightRemap";
		editorTemplate -endLayout;

		// Transparency controls
		editorTemplate -beginLayout (getPluginResource("bifrostvisplugin", "kFoamTransparency")) -collapse 0;
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kColor")) -addControl "transparencyColor";
			editorTemplate -label (getPluginResource("bifrostvisplugin", "kWeight")) -addControl "transparencyWeight";
			editorTemplate -callCustom "AEbifrostvisChannelListNew" "AEbifrostvisChannelListReplace"
				"transparencyWeightRemapChannel" "kWeightRemapChannel" "kParticle" "AEbifrostFoamMaterialCheckTransparencyWeightRemap";
			editorTemplate -callCustom "AEbifrostvisRemapNew" "AEbifrostvisRemapReplace"
				"transparencyWeightRemapInputMin" "transparencyWeightRemapInputMax" "" "transparencyWeightRemap"
				"kFoamTransparencyWeightRemap" "kMin" "kMax" "AEbifrostFoamMaterialCheckTransparencyWeightRemap";
		editorTemplate -endLayout;

		// Add parent class controls
		AEdependNodeTemplate $nodeName;

		// Add any missing controls
		editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;

	// Suppress meaningless attributes
}
