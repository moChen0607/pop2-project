// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//  Procedure Name:
//	AEcacheBlendTemplate
//
//  Description Name;
//	Creates the attribute editor controls for the cacheBlend Node
//
//  Input Value:
//	nodeName 
//
//  Output Value:
//	None
// 

proc string
getCacheTrackSliderLabel(string $input)
{
	string $name = $input;
//	if (nodeType($input) == "cacheFile") {
//		$name = `getAttr ($input+".cacheName")`;
//	}
	return $name;
}

global proc AEcacheBlendNew(string $cacheName)
{
	string $buffer[];
	tokenize($cacheName, ".", $buffer);
	string $nodeName = $buffer[0];

	setUITemplate -pst attributeEditorTemplate;
	rowColumnLayout -numberOfColumns 2
		-columnWidth 1 350
		-columnWidth 2 25
		-columnAlign 1 "right"
		-columnAlign 2 "left"
		($buffer[1]+"Column");

	int $i, $ct;
	string $inputs[] = `listConnections ($nodeName+".inCache")`;
    string $visited[];
	$ct = 0;
	for ($i=0; $i < size($inputs); $i++)
	{
		string $label = getCacheTrackSliderLabel($inputs[$i]);
        // If the object has already been visited, just continue.
		int $v = 0;
        string $vis;
        for ($vis in $visited)
		{
			if ($vis == $label)
			{
				$v = 1;
			}
		}
		if ($v == 1)
			continue;
		$visited[size($visited)] = $label;

		string $name = ("input"+$ct);
		attrFieldSliderGrp -label $label -min 0.0 -max 1.0 -fmn 0.0 -fmx 20.0 -s 0.1 $name;
		symbolButton  -image "navButtonConnected.png" ($name+"Cache");
		$ct++;
	}
	
	setParent ..;
	setUITemplate -ppt;

	AEcacheBlendReplace( $cacheName );
}


global proc AEcacheBlendReplace(string $cacheName)
{

	string $buffer[];
	tokenize($cacheName, ".", $buffer);
	string $nodeName = $buffer[0];

	setUITemplate -pst attributeEditorTemplate;
  
	setParent ($buffer[1]+"Column");
	$layoutName = `setParent -q`;
	int $oldCount = `rowColumnLayout -q -nch $layoutName`;

	int $i, $ct;
	string $inputs[] = `listConnections ($nodeName+".inCache")`;
    string $visited[];
	$ct = 0;
	for ($i=0; $i < size($inputs); $i++)
	{
		string $label = getCacheTrackSliderLabel($inputs[$i]);

        // If the object has already been visited, just continue.
		int $v = 0;
        string $vis;
        for ($vis in $visited)
		{
			if ($vis == $label)
			{
				$v = 1;
			}
		}
		if ($v == 1)
			continue;
		$visited[size($visited)] = $label;

		string $name = ("input"+$ct);
		string $attr = ($nodeName+".cd["+$ct+"].w");
		if (`attrFieldSliderGrp -ex $name`)
		{
			attrFieldSliderGrp -e -label $label -at $attr $name;
			button -e -c ("showEditor "+$inputs[$i]) ($name+"Cache");
		}
		else
		{
			attrFieldSliderGrp -min 0.0 -max 1.0 -fmn 0.0 -fmx 20.0 -s 0.1
				-label $label -at $attr $name;
			symbolButton  -image "navButtonConnected.png" ($name+"Cache");
			button -e -c ("showEditor "+$inputs[$i]) ($name+"Cache");
		}
		$ct++;
	}

	for ($i=$ct; $i < $oldCount; $i++)
	{
		$sliderName = ("input"+$i);
		if ( `attrFieldSliderGrp -ex $sliderName` )
			deleteUI $sliderName;
		if ( `symbolButton -ex ($sliderName+"Cache")` )
			deleteUI ($sliderName+"Cache");
	}

	setParent ..;
	setUITemplate -ppt;
}

global proc AEcacheBlendTemplate ( string $nodeName )
{
	editorTemplate -beginScrollLayout;

	editorTemplate -beginLayout (uiRes("m_AEcacheBlendTemplate.kInputWeights")) -collapse false;
	editorTemplate -callCustom AEcacheBlendNew AEcacheBlendReplace weight;
	editorTemplate -endLayout;

	AEdependNodeTemplate $nodeName;
	editorTemplate -suppress "disableAll";
	editorTemplate -suppress "cacheData";
	editorTemplate -suppress "track";
	editorTemplate -suppress "trackState";	
	editorTemplate -suppress "start";
	editorTemplate -suppress "end";
	editorTemplate -suppress "inRange";
	editorTemplate -suppress "cacheWeights";
	editorTemplate -suppress "perPtWeights";	
	
	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}
