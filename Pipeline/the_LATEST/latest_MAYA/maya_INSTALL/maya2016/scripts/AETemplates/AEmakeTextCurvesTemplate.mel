// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	Procedure Name:
//	AEmakeTextCurvesTemplate
//
//	Description Name;
//	Creates the attribute editor controls for the makeTextCurves node
//
//	Input Value:
//	nodeName
//
//	Output Value:
//	None
//

global proc AEmakeTextCurvesTemplate( string $nodeName )
{
	
	editorTemplate -beginScrollLayout;
	
		editorTemplate -beginLayout (uiRes("m_AEmakeTextCurvesTemplate.kTextCurvesHistory")) -collapse 0;
			editorTemplate -addControl "text";
			editorTemplate -callCustom "AEmakeTextCurvesNew"
						"AEmakeTextCurvesReplace"
						"font";
		editorTemplate -endLayout;

	// include/call base class/node attributes
	AEabstractBaseCreateTemplate $nodeName;
	
	editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;
}

global proc AEmakeTextCurvesNew( string $inputFont )
{
	setUITemplate -pst attributeEditorTemplate;

	string $buffer[];
	tokenize($inputFont, ".", $buffer);
	string $attFont = $buffer[1];

	columnLayout ($attFont + "Column");

	// create the fields

	rowLayout -cat 3 left 0 -nc 3 fontRowTotalLayout;
	string $parent = `setParent -q`;
		// the font text field will not be editable by the user - the
		// string will come from the popup menu
		//
		text -label (uiRes("m_AEmakeTextCurvesTemplate.kFont"));
		textField -ed false fontField;
			symbolButton -image "pickMenuIcon.png" fontMenuIconBtn;
	setParent ..;
	
	popupMenu -b 1 -p fontMenuIconBtn
		-pmc ("createFontPopup " + "-scalable " + " resetFontAttr " + $inputFont + " " + $parent)
		fontPopupMenu;

	setParent ..;

	setUITemplate -ppt;

	// set up initial values of fields
	AEmakeTextCurvesReplace( $inputFont );
}

global proc AEmakeTextCurvesReplace( string $inputFont )
{
	// initialize field with font string
	string $fontString = `getAttr $inputFont`;
    string $tokens[];
    tokenize $fontString "|" $tokens;
	textField -e -text $tokens[0] fontField;

	string $parent = `setParent -q`;
	popupMenu -e
		-pmc ("createFontPopup " + "-scalable " + " resetFontAttr " + $inputFont + " " + $parent)
		fontPopupMenu;
}

global proc resetFontAttr(string $inputFont, string $parent, string $fontName)
{
	// set font field with new text string
	setParent $parent;
	optionVar -stringValue textCurvesFont $fontName;
    string $tokens[];
    tokenize $fontName "|" $tokens;
    textField -e -text $tokens[0] fontField;

	// set attribute with new font
	setAttr -type "string" $inputFont $fontName;
}
