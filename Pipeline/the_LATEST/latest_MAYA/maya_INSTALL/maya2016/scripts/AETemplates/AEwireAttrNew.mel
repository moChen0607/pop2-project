// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:	July 11, 1997
//
//  Procedure Name:
//	AEwireAttrNew
//
//  Description Name;
//	Creates the multi attribute controls for the wire Node
//
//  Input Value:
//	nodeName.attrName
//
//  Output Value:
//	None
//
// 



global proc AEwireAttrNew ( string $wireMulti )
{
	string $buffer[];
	tokenize($wireMulti, ".", $buffer);
	string $nodeName = $buffer[0];
	string $att	 = $buffer[1];
	

	setUITemplate -pst attributeEditorTemplate;
  
	columnLayout ($att+"Column");
	$attr=$nodeName+".dw";
	string $wireNode[] =`listConnections -p false -d false $attr`;
	for ($i=0; $i < size($wireNode); $i++)
	{
		setUITemplate -pst attributeEditorTemplate;
		$sliderLabel = $wireNode[$i];
		$sliderName = $att+$i;
		attrFieldSliderGrp -min 0.0 -max 5.0 -fmn 0.0 -fmx 20.0 
			-s 0.1 -label $sliderLabel $sliderName;
		setUITemplate -ppt;
	}
	setParent ..;
	setUITemplate -ppt;
	AEwireAttrReplace $wireMulti;
}


global proc AEwireAttrReplace ( string $wireMulti )
{
	string $buffer[];
	tokenize($wireMulti, ".", $buffer);
	string $nodeName = $buffer[0];
	string $att	 = $buffer[1];

	setUITemplate -pst attributeEditorTemplate;

  
	setParent ($att+"Column");
	$layoutName=`setParent -q`;
	int $oldCount=`columnLayout -q -nch $layoutName`;
	int $i;
	$attr=$nodeName+".deformedWire";
	string $wire[] =`listConnections -p false -d false $attr`;
	string $plugs[] = `listAttr -m -st deformedWire $nodeName`;
	if (size($wire)!=size($plugs))
	{
		for ($i=0; $i < size($wire); $i++)
		{
			$attr=$nodeName+"."+$att+"["+$i+"]";
			$sliderName=$att+$i;
			$sliderLabel = $wire[$i];
			
			if ( `attrFieldSliderGrp -ex $sliderName` )
			{
				attrFieldSliderGrp -e -label $sliderLabel -at $attr $sliderName;
			} else {
				setUITemplate -pst attributeEditorTemplate;
				attrFieldSliderGrp -min 0.0 -max 5.0 -fmn 0.0 -fmx 20.0 -s 0.1
					-label $sliderLabel -at $attr $sliderName;
				setUITemplate -ppt;
			}
		}
	}
	else
	{
		string  $extract;
		for ($i=0; $i < size($wire); $i++)
		{
			$extract = substring($plugs[$i],13,size($plugs[$i])); 
			$attr=$nodeName+"."+$att+$extract;
			$sliderName=$att+$i;
			$sliderLabel = $wire[$i];
			if ( `attrFieldSliderGrp -ex $sliderName` )
			{
				attrFieldSliderGrp -e -label $sliderLabel -at $attr $sliderName;
			} else {
				setUITemplate -pst attributeEditorTemplate;
				attrFieldSliderGrp -min 0.0 -max 5.0 -fmn 0.0 -fmx 20.0 -s 0.1
					-label $sliderLabel -at $attr $sliderName;
				setUITemplate -ppt;
			}
		}
	}

	for ($i=size($wire); $i < $oldCount; $i++)
	{
		$sliderName=$att+$i;
		if ( `attrFieldSliderGrp -ex $sliderName` )
			deleteUI $sliderName;
	}
	setParent ..;
	setUITemplate -ppt;
}

