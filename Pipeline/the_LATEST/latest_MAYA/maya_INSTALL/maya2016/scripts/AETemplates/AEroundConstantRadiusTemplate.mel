// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//  Procedure Name:
//	AEroundConstantRadiusTemplate
//
//  Description Name;
//	Creates the attribute editor controls for the round node
//
//  Input Value:
//	nodeName 
//
//  Output Value:
//	None
// 

global proc AEedgePairNew( string $plug )
{
	frameLayout -cl true -cll true -label (uiRes("m_AEroundConstantRadiusTemplate.kEdgeInputs")) -lv true "edgeLayout";
	columnLayout;

	// get the number of elements in this multi attribute
	//
	int $numEdgePairs = `getAttr -size $plug`;

	int $i = 0;
	for( $i = 0; $i < $numEdgePairs; $i++ ) {
		frameLayout -cl true -cll true -l ("Edge[" + ($i) + "]") -lv true;

		string $layout = `columnLayout`;

		frameLayout -cl false -cll false -lv false -bv false;
		string $crv = $plug + "[" + $i + "].inputCurveA";
		AEmultiInputNew( "inputCurveA", $crv );
		setParent $layout;

		frameLayout -cl false -cll false -lv false -bv false;
		string $crv = $plug + "[" + $i + "].inputCurveB";
		AEmultiInputNew( "inputCurveB", $crv );
		setParent $layout;

		setParent ..;
		setParent ..;
	}

	setParent ..;
	setParent ..;
}


global proc AEedgePairReplace( string $plug )
{
	deleteUI edgeLayout;
	AEedgePairNew( $plug );
}

global proc AEradiusNew( string $plug )
{
	// get the number of elements in this multi attribute
	//
	columnLayout "radiusLayout";
	int $numRadii = `getAttr -size $plug`;
	string $fmt = (uiRes("m_AEroundConstantRadiusTemplate.kAttrFieldLabel"));
	int $i = 0;
	for( $i = 0; $i < $numRadii; $i++ ) {
		attrFieldSliderGrp -label (`format -s $i $fmt`) 
			-at ($plug + "[" + $i + "]");
	}
}


global proc AEradiusReplace( string $plug )
{
	deleteUI radiusLayout;
	AEradiusNew( $plug );
}


global proc AEroundConstantRadiusTemplate ( string $nodeName )
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout (uiRes("m_AEroundConstantRadiusTemplate.kRoundConstantRadius")) -collapse 0;

			// this will display each radius
			//
			editorTemplate -callCustom "AEradiusNew"
						"AEradiusReplace"
						"radius";

			// this will display the tolerance
			//
			editorTemplate -addControl "tolerance";

			// this will display each edgePair and its curve inputs
			//
			editorTemplate -callCustom "AEedgePairNew"
						"AEedgePairReplace"
						"edge";

	// include/call base class/node attributes
	AEabstractBaseCreateTemplate $nodeName;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;

	// supress attributes so they will not show up in "Extra Atrributes"
	//
	editorTemplate -suppress "inputSurface";
	editorTemplate -suppress "tolerance";
}
