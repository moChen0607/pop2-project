// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//	Creation Date:	June 3, 1997
//
//	Procedure Name:
//	AEnewMessage
//
//	Description Name;
//	Creates a textFieldButtonGrp for a TattributeMessage for
//	the attribute editor
//	If the node is mental ray shader, 
//	creates a different kinds of controls.
//
//	Input Value:
//	plug name, attribute name, and changedCommand
//
//	Output Value:
//	Returns the name of the newly created textFieldButtonGrp
//

global proc string AEnewMessage(string $plugName, string $attrName, string $changedCommand )
{
	
	string $buffer[];
	tokenize($plugName, ".", $buffer);
	string $node = (size($buffer) > 0) ? $buffer[0] : "";
	
	// special control for mental ray node
	if( isClassified($node, "rendernode/mentalray") )
	{
		return AEmentalrayNewMessage($plugName, $attrName, $changedCommand);
	}
	
	
	string $createdControl = `textFieldButtonGrp -l $attrName`;
	string $connections[] = `listConnections $plugName`;
	textFieldButtonGrp -e -tx $connections[0] $createdControl;
	if ($connections[0] != "") {
		textFieldButtonGrp -e
			-eb true
			-bl " > " -bc ("showEditor "+$connections[0]) $createdControl;
	} else {
		textFieldButtonGrp -e -eb false -bc "" $createdControl;
	}

	// connect the control
	string $cmd = "AEupdateMessage \""+$createdControl+"\" \""+$plugName+"\"";
	scriptJob -p $createdControl -rp -ac $plugName $cmd;

	// only make the text field editable if there is a changedCommand
	// to handle it
	if ($changedCommand != "") {
		$cmd = $changedCommand+" \""+$node+"\"";
		textFieldButtonGrp -e
			-ed true
			-cc $cmd
			$createdControl;
	} else {
		textFieldGrp -e -ed false $createdControl;
	}
	return $createdControl;
}
