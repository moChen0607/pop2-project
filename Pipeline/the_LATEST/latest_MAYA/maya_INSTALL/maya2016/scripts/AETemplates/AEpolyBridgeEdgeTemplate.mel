// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

///////////////////////////////////////////////////////////////////////////////
//
//  Description:
//  Attribute editor template layout for polyBridgeEdge nodes
//
///////////////////////////////////////////////////////////////////////////////

proc int needCurveControls ( string $nodeName )
{
	string $connectedCurveAttr = $nodeName + ".inputProfile";
	string $curveTypeAttr = $nodeName + ".curveType";
	
	int $curveType = `getAttr $curveTypeAttr`;
	
	// The curve controls should be valid if something is
	// connected to the inputProfile, ie. either a user
	// curve was used for the bridge or one was created
	// by us, or it is set to use an implicit curve.
	//
	return ( size(`listConnections $connectedCurveAttr`) ||
			 $curveType == 1 ); 
}

global proc AEpolyBridgeEdgeCurveTypeChanged ( string $nodeName )
{
	int $needCurveControls = needCurveControls($nodeName);
	AEpolyBridgeEdgeRampEnable $nodeName $needCurveControls;
}

global proc AEpolyBridgeEdgeRampEnable(string $nodeName, int $val)
{
	editorTemplate -dimControl $nodeName "taper" (1-$val);
	editorTemplate -dimControl $nodeName "twist" (1-$val);
	
	if (`control -ex "taperCurveSp"`)
		attrFieldSliderGrp -e -enable $val "taperCurveSp";
	if (`control -ex "taperCurveScc"`)
		attrFieldSliderGrp -e -enable $val "taperCurveScc";
	if (`control -ex "taperCurveSi"`)
		attrEnumOptionMenuGrp -e -enable $val "taperCurveSi";
	if (`control -ex "taperCurveRamp"`)
		gradientControl -e -enable $val "taperCurveRamp";
}
global proc AEpolyBridgeEdgeNew( string $attrName )
{
}
global proc AEpolyBridgeEdgeReplace( string $attrName )
{
	string $tmp[];
	tokenize($attrName, ".", $tmp);
	string $nodeName = $tmp[0];
	
	int $enable = needCurveControls($nodeName);

	AEpolyBridgeEdgeRampEnable $nodeName $enable;
}

global proc AEpolyBridgeEdgeTemplate(string $nodeName)
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout (uiRes("m_AEpolyBridgeEdgeTemplate.kPolygonBridgeEdgeAttributes")) -collapse 0;

			editorTemplate -addControl "divisions";
			editorTemplate -addControl "curveType" "AEpolyBridgeEdgeCurveTypeChanged";
			editorTemplate -addControl "bridgeOffset";
			editorTemplate -addControl "smoothingAngle";
			
			int $collapse = !needCurveControls($nodeName);

			editorTemplate -beginLayout (uiRes("m_AEpolyBridgeEdgeTemplate.kPolyBridgeEdgeAttributes")) -collapse $collapse;
				editorTemplate -addControl "twist";
				editorTemplate -addControl "taper";
				AEaddRampControl ($nodeName+".taperCurve");
			editorTemplate -endLayout;

			editorTemplate -callCustom
					"AEpolyBridgeEdgeNew"
					AEpolyBridgeEdgeReplace
					AEpolyBridgeEdgeType;
						
		editorTemplate -endLayout;

		AEpolyModifierInclude $nodeName;

		// suppressed attributes
		editorTemplate -suppress "inputComponents";
		editorTemplate -suppress "startEdge1";
		editorTemplate -suppress "startEdge2";
		editorTemplate -suppress "inputProfile";
		editorTemplate -suppress "worldSpace";

		editorTemplate -suppress "startPoint";
		editorTemplate -suppress "startDir";
		editorTemplate -suppress "endPoint";
		editorTemplate -suppress "endDir";
		
	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}
