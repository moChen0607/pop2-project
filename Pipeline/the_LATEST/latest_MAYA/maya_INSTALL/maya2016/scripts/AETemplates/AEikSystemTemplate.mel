// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	Procedure Name:
//	AEikSystemTemplate
//
//	Description Name;
//	Creates the attribute editor controls for the ikSystem node
//
//	Input Value:
//	nodeName
//
//	Output Value:
//	None
//

global proc AEikSystemTemplate( string $nodeName )
{
	
	editorTemplate -beginScrollLayout;
	
	editorTemplate -beginLayout (uiRes("m_AEikSystemTemplate.kIkSystem"))  -collapse false;
		editorTemplate -callCustom "buildSolverPriorityList" 
					"replaceSolverPriorityList";
		editorTemplate -beginNoOptimize;
		editorTemplate -addControl "globalSnap";
		editorTemplate -addControl "globalSolve";
		editorTemplate -label (uiRes("m_AEikSystemTemplate.kLegacyIkFkBlend")) -addControl "preMaya2011IKFKBlend";
		editorTemplate -endNoOptimize;
	editorTemplate -endLayout;

	// include/call base class/node attributes
	AEdependNodeTemplate $nodeName;

	editorTemplate -suppress "ikSolver";

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;

}


global proc buildSolverPriorityList()
{
	textScrollList -nr 8 -ams false
		-sc "selectSolverItem"
		-dkc "unselectSolverItem"
		ikSolverList;
	ikListSolverTypes();
}

global proc replaceSolverPriorityList()
{}


global proc ikListSolverTypes ()
{

    global string $solverList[];
    global int $firstSelectedIndex;
    global int $totalItems;
    int $i;

    $solverList = `ikSystem -q -ls`;

    textScrollList -e -ra ikSolverList;

    $totalItems = size($solverList);
    for ( $i = 0; $i < $totalItems; $i++ ) {
        textScrollList -e -a $solverList[$i] ikSolverList;
    }
    $firstSelectedIndex = -1;
    textScrollList -e -da ikSolverList;

    setParent ..;
}

global proc selectSolverItem ()
{

    global string $solverList[];
    global int $firstSelectedIndex;
    global int $totalItems;

    int $currentList[];
    int $i;
    string $temp[];

    $currentList = `textScrollList -q -sii ikSolverList`;

    $i = $currentList[0] - 1;
    $temp = `textScrollList -q -si ikSolverList`;
    $solverList = `ikSystem -q -ls`;
    if ( $temp[0] != $solverList[$i] ) {
        ikListSolverTypes ();
        return;
    }

    if ( $firstSelectedIndex == -1 ) {
        textScrollList -e -sii $currentList[0] ikSolverList;
        $firstSelectedIndex = $currentList[0] - 1;
    } else {
        $i = $currentList[0] - 1;
        ikSystem -e -ls $firstSelectedIndex $i;
        ikListSolverTypes ();
    }

    setParent ..;

}

global proc unselectSolverItem ()
{

    global int $firstSelectedIndex;


    $firstSelectedIndex = -1;
    textScrollList -e -da ikSolverList;

    setParent ..;
}


