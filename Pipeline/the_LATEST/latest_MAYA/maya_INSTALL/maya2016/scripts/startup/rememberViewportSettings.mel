// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//  Description:
//  If a panel with the given label does not yet exist then this will
//  create the panel and check for an optionVar with the same name.  
//  If the optionVar exists then it will eval the command that is 
//  stored in it, which will initialize the panel to its last stored
//  state.  If the optionVar does not exist then this will create it,
//  initialized to an empty string, so that it will be stored the
//  next time the settings are saved.
//
global proc string rememberViewportSettings( string $viewLabel, string $camera )
{
	string $localPanelLabel = localizedPanelLabel($viewLabel);
	string $panelName = `sceneUIReplacement -getNextPanel "modelPanel" $localPanelLabel`;

	int $resetView = `optionVar -q ResetModelViewsOnNewScene`;

	if ("" == $panelName) 
	{
		global string  	$gMainPane;
		int    $menusOkayInPanels = `optionVar -q allowMenusInPanels`;
		$panelName = `modelPanel -mbv $menusOkayInPanels -parent $gMainPane -label $localPanelLabel `;

		if ( `optionVar -exists $panelName` )
		{
			// Don't bother loading the saved view settings if we're going to reset anyways
			if ( !$resetView )
			{
				string $cmd = ("{int $menusOkayInPanels = " + $menusOkayInPanels + "; ");
				$cmd += ("string $editorName, $panelName = \"" + $panelName + "\"; ");
				$cmd += `optionVar -q $panelName`;
				$cmd += ("; }");
				catch( `eval($cmd)` );
			}
		}
		else 
		{
			optionVar -sv $panelName "";
			$resetView = true;
		}
	} 
	else 
	{
		modelPanel -edit -label $localPanelLabel $panelName;
	}

	if ($resetView)
	{
		resetModelEditor( `findStartUpCamera $camera`, $panelName );
	}

	return $panelName;
}
