// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  May 7 1997
//
//  Description:
//      This script is called after file open/new on startup to make
//		sure that the UI construction went okay, and if not to clean up.
//  
{
	global string $gMainPane;

	if (`paneLayout -ex $gMainPane`) {
		//
		//  Make sure that the main pane was finally managed.
		//
		paneLayout -e -manage true $gMainPane;

	}

	// set the name of the proc to call when you need to exit from
	// the current tool
	//
	contextInfo -esc "escapeCurrentTool";

    // Check which renderer is the preferred renderer.
    //
    evalDeferred -lowestPriority "setupRendererSceneOpenedCallback";
	
	// Set up default decimal places for universal manipulator
	linearPrecision 3;
	
	// Setup default filter for "Echo All" mode of script editor
	commandEcho -filter {"setLastFocusedCommand", "buildScriptEditor", "handleScriptEditorAction", "verifyCommandPopupMenus"};

	source fileDialogFilterTypes;
}

// Define the callback that is needed for the file command to
// turn off anything 'dangerous' on file i/o
//
global proc fileCmdCallback ()
{
	// Prevent nested calls as one file command may trigger another file command through notifications
	global int $gFileCmdCallbackCount = 0;
	if ($gFileCmdCallbackCount++ == 0) {
		global string $gSelect;
		global string $gCurrentSacredTool;
		global string $gPreFileCmdCallbackTool;

		$gCurrentSacredTool = $gSelect;
		$gPreFileCmdCallbackTool = `currentCtx`;
	
		// Ignore resetting the script paint tools.  These tools require setup
		// via UI (dialog box, etc) and just restoring the tool is not enough.
		if (size($gPreFileCmdCallbackTool) &&
			`contextInfo -exists $gPreFileCmdCallbackTool` &&
			`contextInfo -c $gPreFileCmdCallbackTool` == "artUserPaint" )
		{
			$gPreFileCmdCallbackTool = "";
		}
	
		// Go to the safe (select) tool
		escapeCurrentTool;
			
		// Make sure that playback is off
		global int $gPreFileCmdCallbackPlayState;
		if (`exists play`) {
			$gPreFileCmdCallbackPlayState = `play -query -state`;
			if ( $gPreFileCmdCallbackPlayState ) {
				play -state off;
			}
		}
	}
}

// Puts back what fileCmdCallback changes
//
global proc fileCmdRestoreCallback ()
{
	// Prevent nested calls as one file command may trigger another file command through notifications
	global int $gFileCmdCallbackCount;
	if (--$gFileCmdCallbackCount == 0) {
		global string $gPreFileCmdCallbackTool;
		global int $gPreFileCmdCallbackPlayState;
		if (size($gPreFileCmdCallbackTool) && `contextInfo -exists $gPreFileCmdCallbackTool`) {
			setToolTo $gPreFileCmdCallbackTool;
		}
		if (`exists play` && $gPreFileCmdCallbackPlayState) {
			play -state $gPreFileCmdCallbackPlayState;
		}
	}
}

//
// Called by TtimePort's deleteAll method.  If a deleteAll was run, then
// it is not auto save and we can clear the pre file cmd playback state
// so that we do not start a new scene in playback mode
//
global proc clearPreFilePlayState()
{
	global int $gPreFileCmdCallbackPlayState;
	$gPreFileCmdCallbackPlayState = false;
}


// Called by TFileCmd's handleFileNewFlag and handleFileOpenFlag method.  If a file -open
// or file -new was run, then the tool would reset to selectSuperContext

global proc clearPreFileSetToolState()
{
	global string $gPreFileCmdCallbackTool;
	global int $gQuadDrawClearDots;
	$gPreFileCmdCallbackTool = "";
	$gQuadDrawClearDots = true;
}

global proc int quadDrawClearDots()
{
	global int $gQuadDrawClearDots;
	return $gQuadDrawClearDots;
}

global proc setQuadDrawClearDots(int $clearDots)
{
	global int $gQuadDrawClearDots;
	if($gQuadDrawClearDots != $clearDots){
		$gQuadDrawClearDots = $clearDots;
	}
}

// Assign the callback to the file command
file -c "fileCmdCallback" "fileCmdRestoreCallback";
