// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  Aug 15 1996
//
//  Description:
//      This procedure creates the main maya window.
//
{

	global string $gUIpathDiv;

	global string $gMainWindow;
	global string $gMainWindowForm;
	global string $gStatusLineForm;    
	global string $gShelfForm;
	global string $gWorkAreaForm;
	global string $gMainPane;
	global string $gToolboxForm;
	global string $gTimeSliderForm;
	global string $gPlaybackRangeForm;
	global string $gCommandLineForm;
	global string $gHelpLineForm;
	global string $gAttributeEditorForm;
	global string $gToolSettingsForm;
	global string $gChannelsLayersForm;
	global string $gChannelsForm;
	global string $gLayersForm;

	//	These globals track the visibility of the corresponding window
	//	component.  Whenever a toggleUIComponentVisibility("Component") call
	//	is made these values are updated.  Set all these flags to be
	//	initially visible, they will be updated accordingly when each
	//	component is initialized and their startup preferences are
	//	loaded.
	// 
	global int    $gStatusLineVisible      = 1;
	global int    $gShelfVisible           = 1;
	global int    $gToolboxVisible         = 1;
	global int    $gTimeSliderVisible      = 1;
	global int    $gPlaybackRangeVisible   = 1;
	global int    $gCommandLineVisible     = 1;
	global int    $gHelpLineVisible        = 1;
	global int    $gAttributeEditorVisible = 1;
	global int    $gToolSettingsVisible    = 1;
	global int    $gChannelsVisible		   = 1;
	global int    $gLayersVisible		   = 1;

	//  These flags used by toolbar button in the main window on Mac
	global int    $gUIElementsRestored     = 1;
	global int    $gShowAllUIElements      = 0;
	global int    $gHideAllUIElements      = 0;

	//	Default size of window is full screen.
	//
	int $top = 33;
	int $left = 9;
	int $width = 1262;
	int $height = 982;

	if (`about -nt`) {
		// window size/position is non-client size.
		$top =  0;
		$left = 0;
		$width = 1280;
		$height = 1024;
	}
// QT_TODO
//	if (`about -mac`) {
//		// default to a 1024 x 768 display
//		//
//		$top =  0;
//		$left = 0;
//		$width = 1024;
//		// allow space for the title bar and the dock
//		//
//		$height = 768 - 24 - 72;
//	}

	//	Read in the preference values.
	//
	int $useTitleBar = `optionVar -q mainWindowTitleBarVis`;
	int $menubarVis = `optionVar -q mainWindowMenubarVis`;

	//	Create the main Maya window.
	//
	if ( `about -evalVersion` ) {
		$gMainWindow = `window 
			-retain
			-mainWindow
			-menuBarVisible $menubarVis
			-topLeftCorner  $top $left 
			-widthHeight    $width $height
			-titleBar       $useTitleBar
			-title          (uiRes("m_initMainWindow.kMayaPLETitle")) 
			-iconName       (uiRes("m_initMainWindow.kMayaPLEIconName")) 
			"MayaWindow"`;
	} else {
		$gMainWindow = `window 
			-retain
			-mainWindow
			-menuBarVisible $menubarVis
			-topLeftCorner  $top $left 
			-widthHeight    $width $height
			-titleBar       $useTitleBar
			-title          "Maya" 
			-iconName       "Maya" 
			"MayaWindow"`;
	}

	//  if a title bar was specified, annotate it so that it
	//  will always display the current file and selected object.
	//
	if ( $useTitleBar ){
		showSelectionInTitle "MayaWindow";
	}

	// ------------------------------------------------------------
	//	Top level form.
	// ------------------------------------------------------------

	//	The Work area.  This contains the main view panes
	//
	$gWorkAreaForm = `formLayout -nbg 1 -parent $gMainWindow`;

	//	The Status line.
	//
	$statusLine = "MainStatusLineLayout";
	formLayout -parent $gMainWindow $statusLine;
            
	//	The Shelf and Toolbox.
	//
	$shelf = "MainShelfLayout";
	formLayout -parent $gMainWindow $shelf;

    
//	//	The Time slider.
//	//
//	//	NOTE that the name of this form is TimeSliderForm. This is
//	//	referenced from mlControlPanel.mel. If you change it here
//	//	you must change it there. Maya Live needs to insert a 
//	//	panel of controls above the Time Slider and below the work
//	//	area.
//	//
	
	$timeSlider = "MainTimeSliderLayout";
	formLayout -parent $gMainWindow $timeSlider;
	
//
//	//	The Playback range.
//	//
	$playbackRange = "MainPlaybackRangeLayout";
	formLayout -parent $gMainWindow $playbackRange;

	//	The Command line.
	//
	$commandLine = "MainCommandLineLayout";
	formLayout -parent $gMainWindow $commandLine;

	//	The Help line.
	//
	$helpLine = "MainHelpLineLayout";
	formLayout -parent $gMainWindow $helpLine;

	$gMainPane = `paneLayout -nbg 0 -parent $gWorkAreaForm viewPanes`; 

	//	The Toolbox.
	//
	$toolbox = "MainToolboxLayout";
	formLayout -parent $gMainWindow $toolbox;

	//	The Attribute Editor.
	//
	$attributeEditor = "MainAttributeEditorLayout";
	formLayout -parent $gMainWindow $attributeEditor;

	//	The Tool Settings.
	//
	
	$toolSettings = "MainToolSettingsLayout";
	formLayout -parent $gMainWindow $toolSettings; 

	//	The combined Channel Box / Layer Editor area.
	//
	$channelsLayers = "MainChannelsLayersLayout";
	formLayout -parent $gMainWindow $channelsLayers;

	//	The Channel Box area.
	//
	$channels = "MainChannelBoxLayout";
	formLayout -parent $gMainWindow $channels;
	
	//	The Layer Editor area.
	// 
	$layers = "MainLayerEditorLayout";
	formLayout -parent $gMainWindow $layers;
    
    
	source "UIComponents.mel";

	// ------------------------------------------------------------
	//	Now create the UI component areas.
	// ------------------------------------------------------------
	//
	//	Use the createUIComponentToolBar() or createUIComponentDockControl() procedures for each component.
	//
            
    $gStatusLineForm = createUIComponentToolBar(
		"Status Line", localizedUIComponentLabel("Status Line"), $statusLine, "top", {"top", "bottom"});
 
      
	$gShelfForm = createUIComponentToolBar(
		"Shelf", localizedUIComponentLabel("Shelf"), $shelf, "top", {"top", "bottom"});

	$gHelpLineForm = createUIComponentToolBar(
		"Help Line", localizedUIComponentLabel("Help Line"), $helpLine,  "bottom", {"top", "bottom"});

	$gCommandLineForm = createUIComponentToolBar(
		"Command Line", localizedUIComponentLabel("Command Line"), $commandLine, "bottom", {"top", "bottom"});

	$gPlaybackRangeForm = createUIComponentToolBar(
		"Range Slider", localizedUIComponentLabel("Range Slider"), $playbackRange, "bottom", {"top", "bottom"});

	$gTimeSliderForm = createUIComponentToolBar(
		"Time Slider", localizedUIComponentLabel("Time Slider"), $timeSlider, "bottom", {"top", "bottom"});

	$gToolboxForm = createUIComponentToolBar(
		"Tool Box", localizedUIComponentLabel("Tool Box"), $toolbox, "left", {"left", "right"});

	$gChannelsLayersForm = createUIComponentDockControl(
		"Channel Box / Layer Editor", localizedUIComponentLabel("Channel Box / Layer Editor"), $channelsLayers, "right", {"right", "left"});

	$gChannelsForm = createUIComponentDockControl(
		"Channel Box", localizedUIComponentLabel("Channel Box"), $channels, "right", {"right", "left"});

	$gLayersForm = createUIComponentDockControl(
		"Layer Editor", localizedUIComponentLabel("Layer Editor"), $layers, "right", {"right", "left"});


	$gAttributeEditorForm = createUIComponentDockControl(
		"Attribute Editor", localizedUIComponentLabel("Attribute Editor"), $attributeEditor, "right", {"right", "left"});

	$gToolSettingsForm = createUIComponentDockControl(
		"Tool Settings", localizedUIComponentLabel("Tool Settings"), $toolSettings, "left", {"right", "left"});
        
	// ------------------------------------------------------------

	//	Create a default cluster for any toolbox or shelf tools.
	//
	if (!`toolCollection -exists toolCluster`) {
		toolCollection -global true toolCluster;
	}
        
	formLayout -edit
		-attachForm     $gMainPane          "top"    0
		-attachForm     $gMainPane          "left"   0
		-attachForm     $gMainPane          "bottom" 0
		-attachForm     $gMainPane          "right"  0
		$gWorkAreaForm;	      
}
