// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//
//<doc>
//<name callPython>
//
//<synopsis>
//		callPython (string $module, string $function, string $args[])
//
//<returns>
//		void
//
//<description>
//		Call a python function from MEL with string arguments.
//      Does not return a value.  Use the python() function if you want
//      to evaluate an expression.
//
//<flags>
//		string $module Python module
//		string $function function name
//		string $args[] list of string arguments to pass
//
//<examples>
//	callPython "sys" "stderr.write" {"hello\n"}
//</doc>
global proc callPython (string $module, string $function, string $args[])
{
    string $cmd = "";
    if ("" != $module) {
        $cmd = ("import " + $module + " as __mod\ntry:\n\t__mod.");
    }
    $cmd = ($cmd + $function + "(");
    int $i;
    int $n = size($args);
    for($i = 0; $i < $n; $i++) {
        string $encodedArg = `encodeString $args[$i]`;
        $cmd = ($cmd + "\"" + $encodedArg + "\"");
        if (($i + 1) < $n) {
            $cmd = ($cmd + ",");
        }
    }
    $cmd = ($cmd + ")\n");
    if ("" != $module) {
        $cmd = ($cmd + "finally:\n\tdel __mod\n");
    }
    // do it
    python($cmd);
}
