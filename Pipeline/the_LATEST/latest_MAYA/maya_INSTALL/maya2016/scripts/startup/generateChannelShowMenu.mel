// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc setChannelBoxShowType(string $type)
{
	global string $gChannelBoxName;
	optionVar -stringValue cbShowType $type;
	channelBox -e -update $gChannelBoxName;
}

global proc setChannelBoxAtTop(int $val)
{
	global string $gChannelBoxName;
	string $cmd = ("channelBox -e -containerAtTop "+$val+" "+$gChannelBoxName);
	evalEcho $cmd;
	optionVar -intValue cbContainerAtTop $val;
}

global proc int channelBoxAttrCategories(string $filter)
//
// Custom filter to suppress the attribute menu items that do not
// make sense for the channel box.
//
{
	string $nameString = `itemFilterAttr -q -byName $filter`;
	if (size($nameString) > 0) {
		return 1;
	} else if (`itemFilterAttr -q -hidden $filter`) {
		return 0;
	} else if (`itemFilterAttr -q -keyable $filter`) {
		return 0;
	}
	return 1;
}



global proc generateChannelShowMenu( string $parent )
//
// Generates the Show menu on the channel box menubar.
//
{
	global string $gChannelBoxName;
	setParent -m $parent;

	string $showByType = "all";
	if (`optionVar -exists cbShowType`) {
		$showByType = `optionVar -q cbShowType`;
	}
	
	if( 0 != `menu -q -ni $parent` ) {

		menuItem -e -radioButton ($showByType == "all") byAllItem;
		menuItem -e -radioButton ($showByType == "select") byObjectItem;

	} else {
		//
		// The menu doesn't exist yet - build it
		//
		menuItem -label (uiRes("m_generateChannelShowMenu.kAssets")) -subMenu true;
			radioMenuItemCollection;
			menuItem -label (uiRes("m_generateChannelShowMenu.kAll"))
				-c "setChannelBoxShowType(\"all\")"
				-radioButton ($showByType == "all")
				byAllItem;
			menuItem -label (uiRes("m_generateChannelShowMenu.kSelected"))
				-c "setChannelBoxShowType(\"select\")"
				-radioButton ($showByType == "select")
				byObjectItem;
			
			menuItem -divider true;

			menuItem -label (uiRes("m_generateChannelShowMenu.kShowAtTop"))
				-annotation (uiRes("m_generateChannelShowMenu.kShowAtTopAnnot"))
				-cb `channelBox -q -containerAtTop $gChannelBoxName`
				-c "setChannelBoxAtTop #1";
			setParent -m ..;

			filterUICreateMenuSub($gChannelBoxName,$parent,0,1);
			filterUISetRelatedAttrFiltersProcedure($gChannelBoxName,"channelBoxAttrCategories");

			// The filterUI sets up a new postMenuCommand for the menu, so
			// we need to add this command back to the script.
			//
			string $pmc = `menu -q -pmc $parent`;
			$pmc += ("; generateChannelShowMenu "+$parent);
			menu -e -pmc $pmc $parent;
			
			setParent -m ..;

	}
}
