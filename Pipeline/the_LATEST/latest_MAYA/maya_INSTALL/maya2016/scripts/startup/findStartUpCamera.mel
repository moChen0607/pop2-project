// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date: 
//

proc int equivalent( float $a, float $b ) {
	float $tolerance = 0.001;
	return( ($a > $b) ? ($a - $b <= $tolerance) : ($b - $a <= $tolerance));
}

global proc string findStartUpCamera(string $type) {

	string $foundCamera = "";
	string $cameras[];
	int    $yUp;
	float  $rot[];

	if ("persp" == $type) {
		$cameras = `listCameras -p`;
	} else {
		$cameras = `listCameras -o`;
		$yUp = ("y" == `upAxis -q -ax`);
	}

	if (0 == size($cameras)) {
		//
		//  Couldn't find any of the requested type.  Look for any type
		//
		$cameras = `listCameras`;
		if (0 == size($cameras)) {
			error (uiRes("m_findStartUpCamera.kNoCamerasWereFoundInTheScene"));
		}
	}

	switch ($type) {
	case "persp":
		for ( $camera in $cameras ) {
			if (`camera -q -startupCamera $camera`) {
				$foundCamera = $camera;
				break;
			}
		}
		break;
	case "top":
		for ( $camera in $cameras ) {
			if (`camera -q -startupCamera $camera`) {
				$rot = `camera -q -rot $camera`;
				if ($yUp) {
					if (!equivalent(0,$rot[0]) && equivalent(0,$rot[1]) && equivalent(0,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				} else {
					if (equivalent(0,$rot[0]) && equivalent(0,$rot[1]) && equivalent(0,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				}
			}
		}
		break;
	case "side":
		for ( $camera in $cameras ) {
			if (`camera -q -startupCamera $camera`) {
				$rot = `camera -q -rot $camera`;
				if ($yUp) {
					if (equivalent(0,$rot[0]) && !equivalent(0,$rot[1]) && equivalent(0,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				} else {
					if (equivalent(90,$rot[0]) && equivalent(0,$rot[1]) && equivalent(90,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				}
			}
		}
		break;
	case "front":
		for ( $camera in $cameras ) {
			if (`camera -q -startupCamera $camera`) {
				$rot = `camera -q -rot $camera`;
				if ($yUp) {
					if (equivalent(0,$rot[0]) && equivalent(0,$rot[1]) && equivalent(0,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				} else {
					if (equivalent(90,$rot[0]) && equivalent(0,$rot[1]) && equivalent(0,$rot[2])) {
						$foundCamera = $camera;
						break;
					}
				}
			}
		}
		break;
	default:
		{
			string $fmt = (uiRes("m_findStartUpCamera.kBadStartupCameraTypeSpecified"));
			error `format -s $type $fmt`;
		}
		break;
	}

	if ("" == $foundCamera) {
		{
			string $fmt = (uiRes("m_findStartUpCamera.kCouldNotFindAnAppropriateStartupCamera"));
			warning `format -s $type $fmt`;
		}

		//  
		//  Try to find any othro camera for the ortho case.
		//
		switch ($type) {
		case "persp":
			//  Do nothing for persp, yet.
			break;
		default:
			for( $camera in $cameras ) {
				if (`camera -q -startupCamera $camera` &&
				    `camera -q -orthographic $camera` ) 
				{
					$foundCamera = $camera;
					break;
				}
			}
			break;
		}

		if ("" == $foundCamera) {
			$foundCamera = $cameras[0];
		}
	}

	return $foundCamera;
}
