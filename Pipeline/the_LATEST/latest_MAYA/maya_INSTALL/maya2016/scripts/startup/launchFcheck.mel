// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


global proc launchFcheck ( int $sequence )
//Description
// This procedure launches fcheck to view an image or a sequence
// $sequence = 0 is an image
// $sequence = 1 is a sequence
{
	
	//check the stored optionVar for the path to look in first
	if (!`optionVar -exists "launchFcheckPath"`){
		string $workspace = `workspace -query -directory`;
		optionVar -stringValue "launchFcheckPath" ($workspace + "*.*");
	}
	
	//query the optionVar
	string $filePath = `optionVar -query launchFcheckPath`;
	
	//let the user specify the image or sequence to view
	string $fileName = `fileDialog -directoryMask $filePath`;
	if ($fileName != ""){
		launchFcheckCmd $fileName $sequence;
		
		//change the optionVar to the latest image so we look in the 
		//right place next time
		int $numPathComponents;//number of pieces to the file path
		string $filePathBuffer[];//break the file path into pieces
		$numPathComponents = `tokenize $fileName "/" $filePathBuffer`;
		string $newFilePath;
		
		//reassemble the path to the selected file without the fileName
		int $i;
		if (`substring $fileName 1 1` == "/") $newFilePath += "/";
		for ($i = 0; $i < ($numPathComponents - 1); $i++){
			$newFilePath += $filePathBuffer[$i];
			$newFilePath += "/";
		}
		
		//finish the path with a wildcard
		$newFilePath += "*.*";

		//set the optionVar
		optionVar -stringValue "launchFcheckPath" $newFilePath;
	}
}
