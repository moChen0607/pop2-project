// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  30 April 1998
//
//
//  Description:
//      A function to hide/show the selected CVs on a nurbs surface. 
//


global proc selectiveCvsDisplay( int $hide )
//
//	$hide == 1 : hide the cvs that are not selected on the surface by turning 
//	on the selective cv display attribute for the surface.
//
//	$hide == 0 : show all the cvs on the selected surface by turning off the
//	selective cv display attribute for the surface.
//
{
	// get the list of objects selected with CV components.
	//
	global int $gSelectCVsBit;
	string $surfaceList[] = `filterExpand -ex false -sm $gSelectCVsBit`;
	int $numSurfaces = size($surfaceList);
	string $warning = (uiRes("m_selectiveCvsDisplay.kNoCVsOnANURBSSurfaceSelected"));
	if ( $numSurfaces == 0 )
	{
		warning($warning);
		return;
	}

	// from the items with CVs selected, get the name of the object only
	// (ignoring the component) - this will also filter the list to 
	// ignore duplicate items
	//
	$surfaceList = `ls -sl -o $surfaceList`;

	// now select the objects that had CVs selected and filter that list
	// for nurbs surfaces only
	//
	global int $gSelectNurbsSurfacesBit;
	$surfaceList = `filterExpand -ex false -sm $gSelectNurbsSurfacesBit $surfaceList`;
	$numSurfaces = size($surfaceList);
	if ( $numSurfaces == 0 )
	{
		warning($warning);
		return;
	}

	// turn on/off the selective CV display for the nurbs surfaces
	//
	int $i;
	int $isAlreadySelective;
	for ( $i = 0; $i < $numSurfaces; $i++ )
	{
		// if setting the attribute on the surface to true/false but the 
		// attribute is already true/false, then no need to do a setAttr again
		//
		$isAlreadySelective = eval("getAttr " + $surfaceList[$i] + ".scvd");
		if ( $hide == $isAlreadySelective ) 
		{
			if ( $hide == 1 ) 
			  warning (uiRes("m_selectiveCvsDisplay.kSelectiveCVDisplayIsAlreadyOnForThisSurface"));
			else 
			  warning (uiRes("m_selectiveCvsDisplay.kSelectiveCVDisplayIsAlreadyOffForThisSurface"));

			continue;
		}

		eval("setAttr " + $surfaceList[$i] + ".scvd " + $hide);
	}
}
