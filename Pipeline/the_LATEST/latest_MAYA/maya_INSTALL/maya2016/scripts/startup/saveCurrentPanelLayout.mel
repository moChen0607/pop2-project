// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc string saveCurrentPanelLayout() {

	global string $gMainWindow;
	global string $gMainPane;
	global string $gPanelEditorWnd;
	string $text = "New Layout";
	string $layoutName = "";
	string $config, $configArray[];

	string $OK = (uiRes("m_saveCurrentPanelLayout.kOk"));
	string $cancel = (uiRes("m_saveCurrentPanelLayout.kCancel"));

	string $result = `promptDialog 
		-title (uiRes("m_saveCurrentPanelLayout.kSavePanelArrangement"))
		-message (uiRes("m_saveCurrentPanelLayout.kEnterName"))  
		-text $text
		-button $OK
		-button $cancel
		-defaultButton $OK
		-cancelButton $cancel
		-dismissString $cancel
		-parent $gMainWindow`;

	//
	// If the result was "OK", then proceed
	//
	if ( $result == $OK ) {

		$layoutName = `promptDialog -query`;

		//	First check if the name is unique.
		//
		$configArray = `getPanel -allConfigs`;
		for ($config in $configArray) {
			if ($layoutName == `panelConfiguration -query -label $config`) {

				string $str = (uiRes("m_saveCurrentPanelLayout.kConfirmDlgMsg")); 
				string $msg = `format -s $layoutName $str`;
				
				confirmDialog 
					-title (uiRes("m_saveCurrentPanelLayout.kWarning")) 
					-message $msg
					-button (uiRes("m_saveCurrentPanelLayout.kClose")) 
					-parent $gMainWindow;
				return "";
			}
		}

		string $newConfigName = `panelConfiguration -sc false`;
		panelConfiguration -edit -label $layoutName $newConfigName;
		updatePanelLayoutFromCurrent $layoutName;

		//  special case for top,front,side,persp
		//	    -make sure they always reset their state.
		//
		int    $i,$nArr;
		string $labels[];
	
		$labels = `panelConfiguration -q -labelStrings $newConfigName`;
		
		$nArr = size($labels);
		for ($i = 0; $i < $nArr; $i++) {
			if (localizedPanelLabel("Persp View") == $labels[$i] ||
				localizedPanelLabel("Top View") == $labels[$i] ||
				localizedPanelLabel("Side View") == $labels[$i] ||
				localizedPanelLabel("Front View") == $labels[$i])
			{
				panelConfiguration -e -rfs ($i+1) true $newConfigName;
			}
		}
	}

	return $layoutName;
}

