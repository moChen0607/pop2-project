// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  7 July 1999
//
//  Description:
//      Performs snap 2 point to 2 point objects.
//

//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	//	Add var to store what's being moved - the object, its parent
	//  or its grandparent.
	//
	if ($forceFactorySettings || !`optionVar -exists snap2PtTo2PtParent`) {
		optionVar -intValue snap2PtTo2PtParent 0;
	}
	if ($forceFactorySettings || !`optionVar -exists snap2PtTo2PtType`) {
		optionVar -intValue snap2PtTo2PtType 0;
	}
}

//
//  Procedure Name:
//      performSnap2PtTo2PtSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//	forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc performSnap2PtTo2PtSetup(string $parent, 
	int $forceFactorySettings)
{
	int $boolToOption[] = { 1, 2, 3 };

	// Retrieve the option settings
	//
	setOptionVars ($forceFactorySettings);
	setParent $parent;

	//	Radio button to select what gets moved
	//
	int $whichParent = `optionVar -q snap2PtTo2PtParent`;
	radioButtonGrp -e -select $boolToOption[$whichParent] parentRadioGrp;

	//	Radio button to select the type of snap
	//
	int $snapType = `optionVar -q snap2PtTo2PtType`;
	radioButtonGrp -e -select $snapType snapTypeRadioGrp;
}

//
//  Procedure Name:
//      performSnap2PtTo2PtCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc performSnap2PtTo2PtCallback(string $parent, int $doIt)
{
	int $optionToBool[] = { 0, 0, 1, 2 };
    setParent $parent;

	// Set the optionVar's from the control values, and then perform command
	//
	int $i = `radioButtonGrp -q -select parentRadioGrp`;
	optionVar -intValue snap2PtTo2PtParent $optionToBool[$i];

	int $j = `radioButtonGrp -q -select snapTypeRadioGrp`;
	optionVar -intValue snap2PtTo2PtType $j;

	if ($doIt) {
		performSnap2PtTo2Pt 0; 
		addToRecentCommandQueue "performSnap2PtTo2Pt 0" "Snap2PtTo2Pt";
	}
}

//
//  Procedure Name:
//      performSnap2PtTo2PtOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc performSnap2PtTo2PtOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "performSnap2PtTo2Pt";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	//	The value returned is the name of the layout to be used as
	//	the parent for the option box UI.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box - see STEP 8.
	//	==============================================================
	
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	//	Activate the default UI template so that the layout of this 
	//	option box is consistent with the layout of the rest of the 
	//	application.
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	STEP 4: Create option box contents.
	//	===================================
	//	
	//	This, of course, will vary from option box to option box.	
	
	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	//	RECOMMENDATION:  Place the UI in a 'scrollable' layout.  A 
	//	scrollable layout ensures that if the option box window is ever
	//	resized such that it's entire contents is not visible then the 
	//	scroll bars provided by the scrollable layout will allow the user
	//	to access the hidden UI.  Two layouts currently supporting 
	//	scrollable behaviour are the 'scrollLayout' and the 'tabLayout'.
	//
//	scrollLayout;
	//
	//	or...
	//
	tabLayout -tabsVisible 0 -scrollable 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;
	
	//	RECOMMENDATION:  Use the 'Grp' commands where possible because
	//	they obey the formatting specified in the default template.
	//	This will result in a more consistent look throughout the
	//	application.
	//	

	radioButtonGrp -numberOfRadioButtons 3
		-cw 2 90 
		-label (uiRes("m_performSnap2PtTo2Pt.kMove")) 
		-label1 (uiRes("m_performSnap2PtTo2Pt.kObject")) 
		-label2 (uiRes("m_performSnap2PtTo2Pt.kParent")) 
		-label3 (uiRes("m_performSnap2PtTo2Pt.kGrandparent")) 
		parentRadioGrp;

	radioButtonGrp -numberOfRadioButtons 3
		-cw 2 90
		-label (uiRes("m_performSnap2PtTo2Pt.kSnapType")) 
		-label1 (uiRes("m_performSnap2PtTo2Pt.kLeft")) 
		-label2 (uiRes("m_performSnap2PtTo2Pt.kMiddle")) 
		-label3 (uiRes("m_performSnap2PtTo2Pt.kRight")) 
		snapTypeRadioGrp;

	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Step 5: Deactivate the default UI template.
	//	===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	//	Provide more descriptive labels for the buttons.  This is not 
	//	necessary, but in some cases, for example, a button labelled 
	//	'Create' may be more meaningful to the user than one labelled
	//	'Apply'.
	//
	//	Disable those buttons that are not applicable to the option box.
	//
	//	Attach actions to those buttons that are applicable to the option
	//	box.  Note that the 'Close' button has a default action attached 
	//	to it that will hide the window.  If a a custom action is
	//	attached to the 'Close' button then be sure to call the 'hide the
	//	option box' procedure within the custom action so that the option
	//	box is hidden properly.

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performSnap2PtTo2Pt.kSnap")) 
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle (uiRes("m_performSnap2PtTo2Pt.kSnapPointsTitle"));

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "Align2Pointsto2Points" );

	//	Step 9: Set the current values of the option box.
	//	=================================================
	//
	eval (($setup + " " + $parent + " " + 0));	
	
	//	Step 10: Show the option box.
	//	=============================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      performParentHelp
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string performParentHelp()
{
	return 
	"  Command: Snap 2 Points to 2 Points.\n" +
	"Selection: 4 points of any type of point or component.";
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
proc string assembleCmd()
{
	string  $cmd = "snap2PointsTo2Points";

	setOptionVars(false);

	int		$whichParent      = `optionVar -q snap2PtTo2PtParent`;

	int		$snapType = `optionVar -q snap2PtTo2PtType`;
	
	// Execute the command with the option settings
	//

	$cmd = $cmd+"("+$whichParent+", "+$snapType+")";
	return $cmd;
}

//
//  Procedure Name:
//      performParent
//
//  Description:
//		Perform the performParent command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the optionBoxExample1 command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
global proc string performSnap2PtTo2Pt(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Get the command.
			//
			$cmd = `assembleCmd`;

			//	Execute the command with the option settings.
			//
			eval($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			performSnap2PtTo2PtOptions;
			break;

		//	Return the command string.
		//
		case 2:
			//	Get the command.
			//
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

