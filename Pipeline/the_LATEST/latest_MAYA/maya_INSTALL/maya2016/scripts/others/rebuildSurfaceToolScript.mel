// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      rebuildSurfaceToolScript
//
//  Description:
//	create wrapper around the rebuildSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc rebuildSurfaceToolSetup( int $forceFactorySettings, string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex rebuildSurfaceEuc` ) {
		optionVar -iv rebuildSurfaceEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex rebuildSurfaceLac` ) {
		optionVar -iv rebuildSurfaceLac 1;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q rebuildSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q rebuildSurfaceLac` $goToTool;
	}
}

global proc rebuildSurfaceValues ( string $toolName ) 
{
	performRebuildSurface 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc rebuildSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createRebuildSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		rebuildSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "rebuildSurface.png"
		  -title (uiRes("m_rebuildSurfaceToolScript.kRebuildSurfaceDirectionTool"))
		  -bcn "rebuildSurface"

		  -exitUponCompletion `optionVar -q rebuildSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("rebuildSurfaceToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_rebuildSurfaceToolScript.kSelectSurfaceorIsoparm")) )
		  -setSelectionPrompt ((uiRes("m_rebuildSurfaceToolScript.kSelectMoreSurfacesOrIsoparms")) )
		  -setDoneSelectionPrompt ((uiRes("m_rebuildSurfaceToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q rebuildSurfaceLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		  -isoparm true
		$tool;
	}
}

global proc string rebuildSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolRebuildSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "rebuildSurfaceContext";
			createRebuildSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performRebuildSurface", $setToTool, $tool );
}
