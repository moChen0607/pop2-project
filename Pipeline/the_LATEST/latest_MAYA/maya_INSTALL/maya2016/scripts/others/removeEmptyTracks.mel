// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc
removeEmptyTracks (string $clipEditor)
{
	// If there is a highlighted character just use that.
	string $connection = `clipEditor -q -highlightConnection $clipEditor`;
	string $connObjs[] = `selectionConnection -q -object $connection`;
	
	if( size( $connObjs ) == 0 ) {
		// Otherwise tidy all the characters visible in the editor.
		$connection = `clipEditor -q -mainListConnection $clipEditor`;
		$connObjs = `selectionConnection -q -object $connection`;
	}
	
	if( size( $connObjs ) == 0 ) {
		warning( (uiRes("m_removeEmptyTracks.kNoObjectsToRemoveTracks")) );
		return;
	}
	
	string $charsToTidy[];
	string $objsToTidy[];
	for ($obj in $connObjs) {
		if (nodeType($obj) == "character") {
			$charsToTidy[size($charsToTidy)] = $obj;
		} else {
			$objsToTidy[size($objsToTidy)] = $obj;
		}
	}

	// Augment the list of characters to include all descendent characters.
	//
	int $charIndex = 0;
	while ( $charIndex < size( $charsToTidy ) ) {
		string $curCharacter = $charsToTidy[ $charIndex ];
		string $allNodes[] = `character -q -nodesOnly $curCharacter`;
		for ($member in $allNodes) {
			// add subcharacters to the list of characters
			//
			if (nodeType($member) == "character") {
				$charsToTidy[size($charsToTidy)] = $member;
			}
		}
        $charIndex++;
	}

	// clean up the tracks for characters
	//
	for ( $char in $charsToTidy ) {
		string $scheduler = `character -q -scheduler $char`;
		if( $scheduler != "" ) {
			clipSchedule -removeEmptyTracks $scheduler;
		}
	}

	for ( $obj in $objsToTidy) {
		cacheFileTrack -removeEmptyTracks $obj;
	}
}
