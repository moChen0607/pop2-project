// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//	Creation Date:	June 24, 1997
//
//
//<doc>
//<name refreshAE>
//
//<synopsis>
//	refreshAE
//
//<returns>
//	None
//
//<description>
//	Refreshes the attribute editor. (Only if it's visible.)
//	If the focus node (i.e., the selected node that was last known to be
//	displayed in the AE) still exists, re-build all tabs, and if the current tab
//	no longer exists, force an update of the AE. If the focus node no longer
//	exists, clear the AE.
//
//<flags>
//	None
//
//<examples>
//  refreshAE;
//
//</doc>

global proc refreshAE()
{
	global string $gAttributeEditorWindowName;
	global string $gAETabLayoutName;
	global string $gAEFocusNode;

	if (	(	`window -exists $gAttributeEditorWindowName` 
			&&	`window -q -vis $gAttributeEditorWindowName`) 
		||	(`isAttributeEditorVisible`))
	{
		// update the tabs
		if (`objExists $gAEFocusNode`) {

			// find out what tab is currently being viewed
			//
			string	$tabLabels[] = `tabLayout -q -tl $gAETabLayoutName`;
			int		$tabIndex = `tabLayout -q -sti $gAETabLayoutName`;
			string	$currentTab = $tabLabels[$tabIndex-1];

			AEbuildAllTabs $gAEFocusNode;

			// check if the currentTab still exists
			$tabLabels = `tabLayout -q -tl $gAETabLayoutName`;
			for ($i = 0; $i < size($tabLabels); $i++) {	
				if ($currentTab == $tabLabels[$i]) {
					tabLayout -e -st ("formTab"+$i) $gAETabLayoutName;
					return;
				}
			}

			// if we've gotten this far, currentTab no longer
			// exists, rebuild the AE and select $gAEFocusNode's tab
			//
			updateAE($gAEFocusNode);

		} else {

			// BUG #172131
			// In certain cases (like a curve on surface) $gAEFocusNode
			// does not exist but $gAEFocusNode stripped of '|' does.
			// Only make the formLayout invisible if neither $gAEFocusNode
			// nor $gAEFocusNode stripped of '|' exist.
			string $stripped = `substitute "|" $gAEFocusNode ""`;
			if ( !`objExists $stripped` )
			{
				AEsetDisplayToEmpty();
			}

			if (`window -exists $gAttributeEditorWindowName`) 
			{
				window -e
					-title (localizedUIComponentLabel("Attribute Editor"))
					$gAttributeEditorWindowName;
			}

		}
	}
}
