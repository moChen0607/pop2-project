// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc sequencerPanelBarSection1Callback(string $btnName, string $editor, string $parent)
{
	if (`clipEditor -q -exists $editor`)
	{
		setParent $parent;
		
		if ("CreateBtn" == $btnName)
		{
			sequencerCreateShot; 
			doReload $editor;
		}
		else if ("FrameAllBtn" == $btnName)
		{
			FrameAll;
		}

		setParent ..;
	}
}

proc updateSection1(string $editor)
{
	// Nothing to do 
}


global proc sequencerPanelBarSection2Callback(string $btnName, string $editor, string $parent)
{
	if (`clipEditor -q -exists $editor`)
	{
		setParent $parent;
		
		if ("TrimBeforeBtn" == $btnName)
		{
			shotTrim 0 {};
		}
		else if ("TrimAfterBtn" == $btnName)
		{
			shotTrim 1 {};
		}
		else if ("SplitBtn" == $btnName)
		{
			splitShots 0;
		}


		setParent ..;
	}
}

proc updateSection2(string $editor)
{
	// Nothing to do 
}


global proc sequencerPanelBarSection3Callback(string $btnName, string $editor, string $parent)
{
	if (`clipEditor -q -exists $editor`)
	{
		setParent $parent;
		
		if ("RippleBtn" == $btnName)
		{
			int $ripple = 0;
			if (`optionVar -exists rippleEditModeEnabled`) {
				$ripple = `optionVar -q rippleEditModeEnabled`;
			}
			$ripple = !$ripple;
			optionVar -iv rippleEditModeEnabled $ripple;
		}
		else if ("ExpandBtn" == $btnName)
		{
			shotExpand 2 -1;
		}
		else if ("CompressBtn" == $btnName)
		{
			shotCompress 2 -1;
		}
		else if ("AlignBtn" == $btnName)
		{
			shotExpandAndCompress 2 -1;
		}
		else if ("SMLBtn" == $btnName)
		{
			// Cycle through small/med/large
			int $state = `clipEditor -q -cs $editor`;

			switch($state) {
				case 1:
					$state = 2;
					iconTextButton -e -image "smlMed.png" SMLBtn;
				break;

				case 3:
					$state = 1;
					iconTextButton -e -image "smlSmall.png" SMLBtn;
				break;

				case 2:
				default:
					$state = 3;
					iconTextButton -e -image "smlLarge.png" SMLBtn;
				break;
			}
			clipEditor -e -cs $state $editor;
		}

		setParent ..;
	}
}

proc updateSection3(string $editor)
{
	int $ripple = 0;
	if (`optionVar -exists rippleEditModeEnabled`) {
		$ripple = `optionVar -q rippleEditModeEnabled`;
	}
	iconTextCheckBox -e -value $ripple RippleBtn;
}

global proc sequencerPanelBarSection4Callback(string $btnName, string $editor, string $parent)
{
	if (`clipEditor -q -exists $editor`)
	{
		setParent $parent;
		
		if ("SwapShotLeftBtn" == $btnName)
		{
			swapShotLeft "";
		}
		else if ("SwapShotRightBtn" == $btnName)
		{
			swapShotRight "";
		}

		setParent ..;
	}
}

proc updateSection4(string $editor)
{
	// Nothing to do 
}

global proc updateSequenceEditorPanelBar(string $editor)
{
        if (!`clipEditor -q -exists $editor`)
                return;

        string $parent = `clipEditor -q -panel $editor`;
        if (!`panel -q -exists $parent`)
                return;

        if ("" == `panel -q -control $parent`)
                return;

        setParent $parent;

        updateSection1($editor);
        updateSection2($editor);
        updateSection3($editor);
        updateSection4($editor);

        setParent ..;
}

