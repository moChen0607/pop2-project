// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:	2001
//
//  Procedure Name:
//	setNotesAttribute
//
//  Input Value:
//	Node name, attribute names and type, and new attribute value
//
//  Output Value:
//	None
//

global proc setNotesAttribute( 
	string $nodeName, 
	string $longAttrName, string $shortAttrName, string $attrType,
	string $newAttrValue ) 
{
	if( !`objExists $nodeName` ) {
		return;
	}
	// Description: Sets the given "Notes" attribute on the node 
	// to be the new value.  If the attribute doesn't exist,
	// then create it as a dynamic attribute, and set it.

	// Warning! $newAttrValue is not really used here even though
	// it's passed in.  It's checked a few times, but ultimately
	// `scrollField -q -text AENotesScrollFIeld` is used.
	// If you replace the scrollField query to use $newAttrValue
	// (which is the proper way to do things), then multi-line
	// entry into the notes area behaves strangely and
	// doesn't preserve carriage returns.

	// If the Notes attribute exists, then just update it
	// If the newAttrValue attribute is empty, that means
	// that this attribute is to be deleted.
	//
	if( `attributeQuery -exists -n $nodeName $longAttrName` ) {
		if( !`getAttr -lock ($nodeName + "." + $longAttrName)`) {
		

		if( size($newAttrValue) > 0 ) {
			// Update the Notes only if it's changed
			string $currentNotes = `getAttr ($nodeName + "." + $longAttrName)`;
			if( $currentNotes != $newAttrValue ) {
				$editCmd = ("setAttr " + "-type \"" + $attrType + "\" " 
						+ $nodeName + "." + $longAttrName + 
						" `scrollField -q -text AENotesScrollField`");
				if( catch( eval($editCmd)) ) {
					string $msgFormat = (uiRes("m_setNotesAttribute.kCantSetAttribute"));
					error(`format -s $nodeName -s $longAttrName $msgFormat`);
				}
			}
		} else {
			// Delete this attribute from the node
			// but only if the attribute is a dynamic one
			//
			string $dynamicAttrs[] = `deleteAttr -q $nodeName`;
			int $i;
			int $numAttrs = size($dynamicAttrs);
			for( $i = 0; $i < $numAttrs; $i ++ ) {
				if( $dynamicAttrs[$i] == $longAttrName ) {
					if( catch( deleteAttr ($nodeName + "." + $longAttrName)))
					{
						// Problem deleting attribute,
						// possibly because a script job still refers 
						// to this attribute
						warning (uiRes("m_setNotesAttribute.kCantDelAttr"));
					}
					break;
				}
			}
		}
		}

	} else {

		// If the Notes attribute doesn't exist, then create it and set it
		//
		if( size($newAttrValue) > 0 ) {
			if( catch(`addAttr -dt $attrType -ln $longAttrName 
				-sn $shortAttrName $nodeName`) )
			{
				string $msgFormat = (uiRes("m_setNotesAttribute.kCantCreateDynAttr"));
				
				error(`format -s $nodeName -s $longAttrName -s $nodeName -s $shortAttrName $msgFormat`);
			} else {
	
				// Do same as above and update the Notes attr
				//
				$editCmd = ("setAttr " + "-type \"" + $attrType + "\" " 
							+ $nodeName + "." + $longAttrName + 
						" `scrollField -q -text AENotesScrollField`");
				if( catch( eval($editCmd)) ) {
					string $msgFormat = uiRes("m_setNotesAttribute.kCantSetAttribute");
					error(`format -s $nodeName -s $longAttrName $msgFormat`);
				}
			}
		}
	}
}

