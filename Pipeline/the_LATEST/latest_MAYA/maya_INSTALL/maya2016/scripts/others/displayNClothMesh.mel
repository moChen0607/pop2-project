// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//<doc>
//<name displayNClothMesh>
//<owner "Autodesk">
//
//<synopsis>
//		displayNClothMesh( string $meshType )
//
//<returns>
//		None.	
//
//<description>
//	This sets the selected nCloth objects to display either their input or current position meshes.
//  It is equivalent to calling the menu items "Display Input Mesh" and "Display Current Mesh".
//
//<flags>
//	string $meshType  Set to either "input" or "current".
//
//<examples>
//  displayNClothMesh "input"
//</doc>


global proc displayNClothMesh( string $mType )
{
	// First, find the nCloths directly selected
	//
	string $nCloths[] = getSelectedNObjs( "nCloth" );
	int $badSelection = true;

	int $numCloths = size($nCloths);
	int $index = 0;
	while( $index < $numCloths ){
		string $nNode = $nCloths[$index];
		$index++;
		// Choose which mesh to display - at the moment, we can easily have history between the
		// nCloth node and output mesh, but we expect the input mesh to be directly connected
		// If we support workflows that would encourage history between the input mesh and
		// ncloth, we would need to look for it too.

		string $downstreamNodes[] = `listHistory -f 1 $nNode`;
		string $oMeshes[] = `ls -dag -type mesh $downstreamNodes`;
		string $iMeshes[] = `listConnections -sh true -type mesh ($nNode + ".inputMesh")`;
		string $oMesh;
		string $iMesh;
		if( size($oMeshes) >= 1 ){
			$oMesh = $oMeshes[0];
		} else {
			continue;
		}
		if( size($iMeshes) >= 1 ){
			$iMesh = $iMeshes[0];
		} else {
			continue;
		}
		$badSelection = false;
		if( $mType == "input" ){
			setAttr ($iMesh + ".visibility") true;
			setAttr ($iMesh + ".intermediateObject") false;
			setAttr ($oMesh + ".visibility") false;
		} else {
			setAttr ($iMesh + ".intermediateObject") true;
			setAttr ($oMesh + ".visibility") true;
		}
	}
	if( $badSelection ){
		warning((uiRes("m_displayNClothMesh.kNoValidClothNodes")) );
	}
}
