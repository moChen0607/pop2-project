// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc createPolySoccerBallValues(string $toolName)
{
	string $name = "createPolySoccerBall";
	string $parent = (`toolPropertyWindow -q -location` + "|" + $name);
	setParent $parent;

	float $rad = `createPolySoccerBallCtx -q -radius $toolName`;
	float $slen = `createPolySoccerBallCtx -q -sideLength $toolName`;
	
	int $uvSetting = `createPolySoccerBallCtx -q -createUVs $toolName`;
	
	int $createUV = 0;
	int $normalize = 0;
	int $preserveAR = 0;
	int $collective = 0;
	int $seperate = 0;
	
	int $select = 1;

	if ($uvSetting == 4){
		$createUV = 1;
		$normalize = 1;
		$collective = 1;
		$preserveAR = 1;
		$select = 1;
	} else if ($uvSetting == 3){
		$createUV = 1;
		$normalize = 1;
		$collective = 1;
		$select = 1;
	} else if ($uvSetting == 2){
		$createUV = 1;
		$normalize = 1;
		$seperate = 1;
		$select = 2;
	} else if ($uvSetting == 1){
		$createUV = 1;
	}
	
	int $axis = `createPolySoccerBallCtx -q -axis $toolName`;
	
	string $radiusChangeString = "createPolySoccerBallCtx -e -radius #1 " + $toolName + "; " + "changeSoccerBallRadiusCallBack(\"" + $toolName+ "\", #1 )";
	string $sideLengthChangeString = "createPolySoccerBallCtx -e -sideLength #1 " + $toolName + "; " + "changeSoccerBallEdgeLengthCallBack(\"" + $toolName+ "\", #1 )";

	floatSliderGrp -e
		-v $rad
		-cc ($radiusChangeString)
		-dc ($radiusChangeString)
		createPolySoccerBallRadiusSlider;

	floatSliderGrp -e
		-v $slen
		-cc ($sideLengthChangeString)
		-dc ($sideLengthChangeString)
		createPolySoccerBallSideLengthSlider;
	
	radioButtonGrp -e
		-sl ($axis + 1)
		-cc ("changeSoccerBallAxisSetting(\"" + $toolName+ "\")")
		createPolySoccerBallAxisRadioButton;
	
	checkBoxGrp -e
		-v1 $createUV
		-cc1 ("changeSoccerBallUVSettings(\"" + $toolName+ "\")")
		createPolySoccerBallCreateUVCheckBox;
		
	checkBoxGrp -e
		-v1 $normalize
		-cc1 ("changeSoccerBallUVSettings(\"" + $toolName+ "\")")
		createPolySoccerBallNormalizeCheckBox;
	
	radioButtonGrp -e
		-sl $select
		-cc ("changeSoccerBallUVSettings(\"" + $toolName+ "\")")
		createPolySoccerBallNormalizeTypeRadioButton;
	
	checkBoxGrp -e
		-v1 $preserveAR
		-cc1 ("changeSoccerBallUVSettings(\"" + $toolName+ "\")")
		createPolySoccerBallPreserveAspectRatioCheckBox;

	string $icon = "polySoccerBall.png";
	string $helpTag= "CreatePolySoccerBallTool";
	toolPropertySetCommon $toolName $icon $helpTag;
	toolPropertySelect $name;
	
	changeSoccerBallUVSettings($toolName);
}

global proc changeSoccerBallRadiusCallBack(string $toolName, float $val){
	global float $soccerBallRatio;
	
	float  $length;
	
	$length = $val*$soccerBallRatio;
	
	floatSliderGrp -edit -value $length createPolySoccerBallSideLengthSlider;
	string $cmd = "createPolySoccerBallCtx -e -sideLength " + $length + " " +  $toolName;
	
	eval($cmd);
}

global proc changeSoccerBallEdgeLengthCallBack(string $toolName, float $val){
	global float $soccerBallRatio;
	
	float  $radius;
	
	$radius = $val/$soccerBallRatio;
	
	floatSliderGrp -edit -value $radius createPolySoccerBallRadiusSlider;
	
	string $cmd = "createPolySoccerBallCtx -e -radius " + $radius + " " +  $toolName;
	
	eval($cmd);
}

global proc changeSoccerBallAxisSetting(string $toolName){
	
	int $axisSetting = 0;
	
	if (`radioButtonGrp -exists createPolySoccerBallAxisRadioButton`){
		$axisSetting = (`radioButtonGrp -q -sl createPolySoccerBallAxisRadioButton` - 1);
	}	
	
	if ($axisSetting < 0){
		$axisSetting = 0;
	}
	
	string $cmd = "createPolySoccerBallCtx -e -axis " + $axisSetting + " " + $toolName;
	
	eval($cmd);
}


global proc changeSoccerBallUVSettings(string $toolName)
{
	int $value = 0;
	int $createUVVal;
	int $normalizeVal;
	int $preserveARVal;
	int $collectiveVal;
	int $seperateVal;
	
	if (`checkBoxGrp -exists createPolySoccerBallCreateUVCheckBox`){
		 $createUVVal = `checkBoxGrp -q -v1 createPolySoccerBallCreateUVCheckBox`;
	}
	
	if (`checkBoxGrp -exists createPolySoccerBallNormalizeCheckBox`){
		 $normalizeVal = `checkBoxGrp -q -v1 createPolySoccerBallNormalizeCheckBox`;
	}
	
	if (`checkBoxGrp -exists createPolySoccerBallPreserveAspectRatioCheckBox`){
		 $preserveARVal = `checkBoxGrp -q -v1 createPolySoccerBallPreserveAspectRatioCheckBox`;
	}
	
	if (`radioButtonGrp -exists createPolySoccerBallNormalizeTypeRadioButton`){
		 $collectiveVal = `radioButtonGrp -q -sl createPolySoccerBallNormalizeTypeRadioButton`;
		 $seperateVal = `radioButtonGrp -q -sl createPolySoccerBallNormalizeTypeRadioButton`;
	}
	
	if ($collectiveVal == 1){
		$collectiveVal = 1;
	} else {
		$collectiveVal = 0;
	}
	
	if ($seperateVal == 2){
		$seperateVal = 1;
	} else {
		$seperateVal = 0;
	}
	
	if ($createUVVal){
		checkBoxGrp -edit -enable true createPolySoccerBallNormalizeCheckBox;
		if ($normalizeVal){
			radioButtonGrp -edit -enable true createPolySoccerBallNormalizeTypeRadioButton;
			if($collectiveVal){
				checkBoxGrp -edit -enable true createPolySoccerBallPreserveAspectRatioCheckBox;
			} else {
				checkBoxGrp -edit -enable false createPolySoccerBallPreserveAspectRatioCheckBox;
			}
		}else {
			checkBoxGrp -edit -enable false createPolySoccerBallPreserveAspectRatioCheckBox;
			radioButtonGrp -edit -enable false createPolySoccerBallNormalizeTypeRadioButton;
		}
	}else {
		checkBoxGrp -edit -enable false createPolySoccerBallNormalizeCheckBox;	
		checkBoxGrp -edit -enable false createPolySoccerBallPreserveAspectRatioCheckBox;
		radioButtonGrp -edit -enable false createPolySoccerBallNormalizeTypeRadioButton;
	}	
	
	if($createUVVal && $normalizeVal && $collectiveVal && $preserveARVal){
		$value = 4;
	} else if($createUVVal && $normalizeVal && $collectiveVal){
		$value = 3;
	} else if ($createUVVal && $normalizeVal){
		$value = 2;
	} else if ($createUVVal){
		$value = 1;
	}
		
	string $cmd = ("createPolySoccerBallCtx -edit -cuv " + $value + " " + $toolName);

	eval($cmd);
}