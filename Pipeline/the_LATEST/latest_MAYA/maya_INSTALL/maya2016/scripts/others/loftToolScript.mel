// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      loftToolScript
//
//  Description:
//	create wrapper around the loft command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc loftToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex loftEuc` ) {
		optionVar -iv loftEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex loftLac` ) {
		optionVar -iv loftLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q loftEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q loftLac` $goToTool;
	}
}

global proc loftValues ( string $toolName ) 
{
	performLoft 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc loftProperties()
{
	scriptCtxCommonProperties;
}

proc createLoftContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		loftToolSetup( 0, "" );
		string $selectAdditionalCurves = (uiRes("m_loftToolScript.kSelectAdditionalCurves"));
		scriptCtx -i1 "skin.png"
		  -title (uiRes("m_loftToolScript.kLoftTool"))
		  -bcn "loft"

		  -totalSelectionSets 2

		  -exitUponCompletion `optionVar -q loftEuc`
		  -expandSelectionList true
		  -forceAddSelect true
		  -fcs ("loftToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_loftToolScript.kSelectCurves"))
		  -setSelectionPrompt $selectAdditionalCurves

		  -setAllowExcessCount false
		  -setAutoToggleSelection true
		  -setAutoComplete 1
		  -setSelectionCount 1

		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		  -nurbsSurface true


		  -setNoSelectionPrompt (uiRes("m_loftToolScript.kSelectCurvesToLoft"))
		  -setSelectionPrompt $selectAdditionalCurves

		  -setAllowExcessCount true
		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q loftLac`
		  -setSelectionCount 0

		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		  -nurbsSurface true
		$tool;
	}
}

global proc string loftToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the action
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolLoft` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "loftContext";
			createLoftContext $tool;
		}
	}
	return scriptToolScript( "performLoft", $setToTool, $tool );
}
