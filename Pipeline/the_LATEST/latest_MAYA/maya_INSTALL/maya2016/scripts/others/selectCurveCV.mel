// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//
//<doc>
//<name selectCurveCV>
//
//<synopsis>
//		selectCurveCV ( string $CVsToSelect )
//
//<returns>
//		None
//
//<description>
//      Selects the specified CV on active curve.
//
//<flags>
//      $CVsToSelect: valid strings are "first", "last" and "all".
//
//<examples>
// // Select the first cv.
// selectCurveCV("first");
//
// // Select the last cv.
// selectCurveCV("last");
//
// // Select all cv's.
// selectCurveCV("all");
//</doc>

global proc selectCurveCV( string $CVsToSelect ){
	
	//get selected curves
	string $curves[] = `listRelatives -shapes -type "nurbsCurve"`;

	//test to see if user has selected curve components
	if (`size ($curves)` == 0){
		$curves = `listRelatives -parent -type "nurbsCurve"`;
	}
	
	//error out if there is nothing to convert
	if (`size ($curves)` == 0){
		string $msg = (uiRes("m_selectCurveCV.kInvalidSelection"));
		error ($msg);
	}

	//clear selection
	select -clear;
	
	//create a string array to store all components
	string $newSelection[];

	int $i = 0;
	//for each curve
	for ($curve in $curves){
		if ($CVsToSelect == "first"){
			//select first CV
			$newSelection[$i] = ($curve + ".cv[0]") ;
		} else {
			//select last or all
			
			//find number of CVs in curve
			int $numCVs = `getAttr -size ($curve + ".controlPoints")`;
			
			if ($CVsToSelect == "last"){
				//select last curve CV
				$newSelection[$i] = ($curve + ".cv[" + $numCVs + "]") ;
			} else {
				//select all curve CVs
				$newSelection[$i] = ($curve + ".cv[0:" + $numCVs + "]") ;
			}
		}
		$i++;
	}
	select -replace $newSelection;
}

