// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  4 April 1997
//
//  Procedure Name:
//      performPolyCopyUV
//
//  Description:
//        
//         
//  Input Arguments:
//        $option : Whether to set the options to default values.
//  Return Value:
//        command string iff $option==2
//

//
// Initialize optionVars for polyCopyUV:
// 
proc setOptionVars (
	string		$prefix, 
	int			$forceFactorySettings,
	string		$vars[]
)
{
	string $varName;

	// Must match $vars
	int    $intValues[]= { 1, 0 };

	for ( $i = size($vars) ; $i-- ; )
	{
		$varName = ($prefix + $vars[$i]);
		if ($forceFactorySettings || !`optionVar -exists $varName`)
			optionVar -intValue $varName $intValues[$i];
	}

	// Also add the map names
	$varName = ($prefix + "uvi");
	if ($forceFactorySettings || !`optionVar -exists $varName`)
		optionVar -stringValue $varName "map1";

	$varName = ($prefix + "uvs");
	if ($forceFactorySettings || !`optionVar -exists $varName`)
		optionVar -stringValue $varName "";
}

global proc performPolyCopyUVSetup (string $parent, int $forceFactorySettings)
{
	string $prefix = "polyCopyUV";
	string $intVars[] ={"ibd", "cm"};
	string $varName;
	int    $i;

	setOptionVars($prefix, $forceFactorySettings, $intVars);
	setParent $parent;
	
	$varName = ($prefix + $intVars[0]);
	int $val = `optionVar -query $varName`;
	checkBoxGrp -e -value1 $val $varName;

	$varName = ($prefix + $intVars[1]);
	int $val = `optionVar -query $varName`;
	checkBoxGrp -e -value1 $val $varName;

	$varName = ($prefix + "uvs");
	textFieldGrp -e -tx `optionVar -q $varName` $varName; 

	$varName = ($prefix + "uvi");
	textFieldGrp -e -tx `optionVar -q $varName` $varName; 
}

global proc performPolyCopyUVCallback (string $parent, int $doIt)
{
	string $prefix = "polyCopyUV";
	string $intVars[] ={"ibd", "cm"};
	string $varName;

	setParent $parent;
	
	$varName = ($prefix + $intVars[0]);
	optionVar -intValue $varName `checkBoxGrp -q -v1 $varName`;

	$varName = ($prefix + $intVars[1]);
	optionVar -intValue $varName `checkBoxGrp -q -v1 $varName`;

	$varName = ($prefix + "uvs");
	optionVar -stringValue $varName `textFieldGrp -q -tx $varName`; 

	$varName = ($prefix + "uvi");
	optionVar -stringValue $varName `textFieldGrp -q -tx $varName`; 

	if ($doIt) 
	{
		performPolyCopyUV 0;
		addToRecentCommandQueue "performPolyCopyUV 0" "PolyCopyUV";
	}
}

proc polyCopyUVOptions (string $prefix, string $intVars[])
{
	string $commandName = "performPolyCopyUV";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");
	string $varName;

	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;

	$varName = ($prefix + $intVars[0]);
	checkBoxGrp -l1 (uiRes("m_performPolyCopyUV.kInsertBeforeDeform")) $varName;

	$varName = ($prefix + "uvi");
	textFieldGrp -l (uiRes("m_performPolyCopyUV.kInputUVSetName")) $varName;

    $varName = ($prefix + $intVars[1]);
    checkBoxGrp -l1 (uiRes("m_performPolyCopyUV.kCreateNew")) $varName;

	$textName = ($prefix + "uvs");
	textFieldGrp -l (uiRes("m_performPolyCopyUV.kOutputUVSetName")) $textName;

	waitCursor -state 0;
	setUITemplate -popTemplate;

	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performPolyCopyUV.kCopy"))
	       -command ($callback + " " + $parent + " " + 1) $applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1) $resetBtn;

	setOptionBoxTitle( (uiRes("m_performPolyCopyUV.kOptions")));

	setOptionBoxHelpTag( "CopyUV" );

	eval (($setup + " " + $parent + " " + 0));

	showOptionBox();
}

global proc string performPolyCopyUV (int $option)
{
	string $prefix = "polyCopyUV";
	string $intVars[] ={"ibd", "cm"};
	string $cmd="";

	switch ($option)
	{
		case 0:
		string $varName;
		int $ival;
		setOptionVars($prefix, false, $intVars);
		$cmd = "polyCopyUV";
		for ( $i = size($intVars) ; $i-- ; )
		{
			$varName = ($prefix + $intVars[$i]);
			$ival = `optionVar -query $varName`;
			$cmd = $cmd + " -" + $intVars[$i] + " " + $ival;
		}

		$varName = ($prefix + "uvs");
		string $sval = `optionVar -query $varName`;
		if ($sval != "")
			$cmd = $cmd + " -uvs " + $sval;

		$varName = ($prefix + "uvi");
		string $sval = `optionVar -query $varName`;
		if ($sval != "")
			$cmd = $cmd + " -uvi " + $sval;

		polyPerformAction $cmd "f" 0;
		break;

		case 1:
		polyCopyUVOptions($prefix, $intVars);
		break;

		case 2:
		$cmd="performPolyCopyUV 0";
		break;
	}
	return $cmd;
}
