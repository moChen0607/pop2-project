// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  Mar. 14, 1997
//
//  Description:
//      The revolvePreset() procedure executes a revolve operation on each 
//		selected object.
//

global proc revolvePreset( int $doHistory, int $outputPolys,
						   int $partialCrvRange, float $startSweep,
						   float $endSweep, int $useTol, float $tol,
						   int $degree, int $numSegments, int $useLocalPivot,
						   float $axisX, float $axisY, float $axisZ,
						   float $pivotX, float $pivotY, float $pivotZ )
{
	// Get a list of curves to revolve and execute the revolve cmd on each.
	// Allowable curves: nurbsCurves, isoparms, cos, surf edges
	//
	global int $gSelectNurbsCurvesBit;
	global int $gSelectIsoparmsBit;
	global int $gSelectCurvesOnSurfacesBit;
	global int $gSelectSurfaceEdgeBit;
	global int $gSelectMeshEdge;

	// Piece together the revolve command
	//
	string $cmd;
	$cmd = ( "revolve " + 
			 " -ch " + $doHistory + " -po " + $outputPolys +
			 " -rn " + $partialCrvRange + " -ssw " + $startSweep  +
			 " -esw " + $endSweep  + " -ut " + $useTol + " -tol " + $tol +
			 " -degree " + $degree  + " -s " + $numSegments  +
			 " -ulp " + $useLocalPivot +
			 " -ax " + $axisX + " " + $axisY +  " " + $axisZ );

	if( ! $useLocalPivot ) {
		$cmd = $cmd + " -p " + $pivotX + " " + $pivotY +  " " + $pivotZ;
	}

	string $curveList[] = `filterExpand -ex true -sm $gSelectMeshEdge -sm $gSelectNurbsCurvesBit -sm $gSelectIsoparmsBit -sm $gSelectCurvesOnSurfacesBit -sm $gSelectSurfaceEdgeBit`;

	if( size($curveList) > 0 ) {
		$cmd += " %s;";
		string $revolveResults[] = executeForEachObject( $curveList, $cmd );
		if( (size($revolveResults)) == 0 ) {
			string $msg = (uiRes("m_revolvePreset.kFailedError"));
			error($msg);
		}
		else {
			// Select all the results with one select command.  Note that only
			// resulting surfaces are selected, not dependency nodes.
			//
			string $selectString;
			$selectString = "select ";
			int $i;

			int $numResults = size($revolveResults);
			for( $i = 0; $i < $numResults; $i ++ ) {
				$selectString += $revolveResults[$i];
				$selectString += " ";
			}
			$selectString += ";";
			select -cl;
			eval($selectString);
		}
	}
	else {
		string $msg = (uiRes("m_revolvePreset.kNoValidItems"));
		error($msg);
	}
}

