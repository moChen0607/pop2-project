// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      filletCurveToolScript
//
//  Description:
//	create wrapper around the filletCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc filletCurveToolSetup( int $forceFactorySettings,
								  string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex filletCurveEuc` ) {
		optionVar -iv filletCurveEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex filletCurveLac` ) {
		optionVar -iv filletCurveLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q filletCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q filletCurveLac` $goToTool;
	}
}

global proc filletCurveValues ( string $toolName ) 
{
	performFilletCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc filletCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createFilletCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		filletCurveToolSetup( 0, "" );
		scriptCtx -i1 "fillet.png"
		  -title (uiRes("m_filletCurveToolScript.kFilletcurveTool"))
		  -bcn "filletCurve"

		  -exitUponCompletion `optionVar -q filletCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("filletCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_filletCurveToolScript.kSelectTwoCurves"))
		  -setSelectionPrompt (uiRes("m_filletCurveToolScript.kSelectSecondCurve"))
		  -setDoneSelectionPrompt (uiRes("m_filletCurveToolScript.kComputeFilletCurve"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q filletCurveLac`
		  -showManipulators true

		  -setSelectionCount 2
		  -nurbsCurve true
		  -curveParameterPoint true
		$tool;
	}
}

global proc string filletCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolFilletCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "filletCurveContext";
			createFilletCurveContext $tool;
		}
	}
	return scriptToolScript( "performFilletCurve", $setToTool, $tool );
}
