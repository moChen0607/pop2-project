// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  May, 1997
//
//  Description:
//      dynExecuteFieldCommands executes the field command and
//		ConnectDynamic commands.
//
//  Input Arguments():
//		int $isCreate -- create or add mode
//		string $fieldCmd -- the field command string (except for the selection)
//
//  Return Value:
//      None.
//

//
//  ============== dynExecuteFieldCommands ==============
//
//  SYNOPSIS
//      Execute the field command and connectDynamic command.
//      If executing a "Create"/positional field command, all items in
//      the selection list will be connected to the field, to be
//      influenced by it.
//      If executing an "Add" field command, add the field to all
//      items in the selection list.
//
global proc dynExecuteFieldCommands(int $isCreate, string $fieldCmd)
{
	string $selected[] = `ls -sl`;

    if (!$isCreate && size($selected) == 0)
	{
		warning (uiRes("m_dynExecuteFieldCommands.kNothingSelected"));
		return;
	}

	
	// filter out the names of any selected auxiliaries,
	// because we do not want to connect a field to another field.
	// The node name dynBase takes in fields, emitters, and collisions.
	//
	string $selectedAux[] = `ls -sl -type dynBase`;
	int $ii; 
	for ($ii = 0; $ii < size($selected); $ii++)
	{
		// search for the name of this selected item
		// among the selected fields.  If we find it there,
		// erase this item from the array.
		//
		int $jj;
		for ($jj = 0; $jj < size($selectedAux); $jj++)
		{	
			if ($selected[$ii] == $selectedAux[$jj])
			{
				$selected[$ii] = "";
				break;
			}
		}

		// If this is the output mesh of an nobject, add field to 
		// the nBase instead
		string $nBase = findTypeInHistory($selected[$ii],"nBase",0,1);
		if( size($nBase) ) {
			$selected[$ii] = $nBase;
		}
	}

	// Execute the field command.
	//
	string $fieldNames[] = evalEcho ($fieldCmd);

	// Currently only Drag has a currentTime attribute, which is used only
	// if inheritVelocity is enabled on the drag field.
	if( size($fieldNames) > 0 && objExists( $fieldNames[0] + ".currentTime") ){
		connectAttr time1.outTime ($fieldNames[0]+".currentTime");
	}

	// If creating a positional field, and there are objects selected,
	// connect them to the new field.
	//
	if ($isCreate && size($selected) > 0)
	{
		string $connectNames = "";
		for ($i = 0; $i < size($selected); $i++)
		{
			string $objName = $selected[$i];
			if (size($objName)>0)
				$connectNames  = $connectNames + " " + $objName;
		}
		if (size($connectNames) > 0)
		{
			string $connectCmd =
				"connectDynamic -f " + $fieldNames[0] + " " + $connectNames;
			evalEcho($connectCmd);
		}
	}
}
