// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      globalStitchToolScript
//
//  Description:
//	create wrapper around the globalStitch command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc globalStitchToolSetup( int $forceFactorySettings,
								   string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex globalStitchEuc` ) {
		optionVar -iv globalStitchEuc 1;
	}
	if( $forceFactorySettings || !`optionVar -ex globalStitchLac` ) {
		optionVar -iv globalStitchLac 0;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q globalStitchEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q globalStitchLac` $goToTool;
	}
}

global proc globalStitchValues ( string $toolName ) 
{
	performGlobalStitch( 3, $toolName );
	scriptCtxCommonValues( $toolName );
}

global proc globalStitchProperties()
{
	scriptCtxCommonProperties;
}

proc createGlobalStitchContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		globalStitchToolSetup( 0, "" );
		scriptCtx -i1 "globalStitch.png"
		  -title (uiRes("m_globalStitchToolScript.kGlobalStitchTool"))
		  -bcn "globalStitch"

		  -exitUponCompletion `optionVar -q globalStitchEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("globalStitchToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_globalStitchToolScript.kSelectSurface")) )
		  -setSelectionPrompt ((uiRes("m_globalStitchToolScript.kSelectMoreSurfaces")) )
		  -setDoneSelectionPrompt ((uiRes("m_globalStitchToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q globalStitchLac`
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		$tool;
	}
}

global proc string globalStitchToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolGlobalStitch` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "globalStitchContext";
			createGlobalStitchContext $tool;
		}
	}
	return scriptToolScript( "performGlobalStitch", $setToTool, $tool );
}
