// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  24 April 1997
//
//  Description:
//      This script provides an option box dialog for the blendShape command.
//
//  Input Arguments:
//		boolean showOptionBox	true - show the option box dialog
//								false - just execute the command
//
//  Return Value:
//      None.
//

//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	// there are no optionVars that apply
}

//
//  Procedure Name:
//      blendShapeRemoveSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//	forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc blendShapeRemoveSetup(string $parent, int $forceFactorySettings)
{
	// Retrieve the option settings

	setOptionVars ($forceFactorySettings);

	setParent $parent;

	// Query the optionVar's and set the values into the controls

	// node and target names are not options since it is unlikely the
	// user would want them to persist past one session
	//
	checkBoxGrp -edit -v1 0 bsSpec;
	checkBoxGrp -edit -v1 0 bstSpec;
	textFieldGrp -e -tx "" bsNode;
	textFieldGrp -e -tx "" bstNode;
	optionMenuGrp -e -enable 0 bsList;
	textFieldGrp -e -enable 0 bsNode;	
}

//
//  Procedure Name:
//      blendShapeRemoveCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc blendShapeRemoveCallback(string $parent, int $doIt)
{
    setParent $parent;

	// Set the optionVar's from the control values, and then perform the
	// command
	//
	if ($doIt) {
		performBlendShapeRemove 0; 
		addToRecentCommandQueue "performBlendShapeRemove 0" "BlendShapeRemove";
	}
}

//
//  Procedure Name:
//      blendShapeRemoveOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc blendShapeRemoveOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "blendShapeRemove";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	//	The value returned is the name of the layout to be used as
	//	the parent for the option box UI.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	//	Any default option box behaviour based on the command name is set 
	//	up with this call.  For example, updating the 'Help' menu item with
	//	the name of the command.
	//
	setOptionBoxCommandName("blendShape");
	
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	//	Activate the default UI template so that the layout of this 
	//	option box is consistent with the layout of the rest of the 
	//	application.
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	STEP 4: Create option box contents.
	//	===================================
	//	
	//	This, of course, will vary from option box to option box.	
	
	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	tabLayout -tabsVisible 0 -scrollable 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;
	
	checkBoxGrp	
		-numberOfCheckBoxes 1
		-label (uiRes("m_performBlendShapeRemove.kSpecifyNode")) 
		-label1 ""
		-v1 0 
		-on1 "specifyBlendShapeCallback 1"
 		-ofc "specifyBlendShapeCallback 0"
		bsSpec;

	textFieldGrp -enable 0 -label (uiRes("m_performBlendShapeRemove.kBlendShapeNode"))  -tx "" bsNode;
	// Create an option menu listing existing blendShapes
	//
	optionMenuGrp -enable 0 -label (uiRes("m_performBlendShapeRemove.kExistingNodes")) 
		-cc "textFieldGrp -e -tx `optionMenuGrp -q -v bsList` bsNode" bsList;

	// add all the blendShapes to the menu
	//
	int $pp;
	string $bnArray[];
	$bnArray = `ls -type blendShape`;
	int $bnCount = size($bnArray);
	if ($bnCount > 0) {
		for ($pp = 0; $pp < $bnCount; $pp++)
		{
			menuItem -label $bnArray[$pp];
		}
	} else {
		menuItem -label (uiRes("m_performBlendShapeRemove.kNoBlendShapeSelected")) ;
	}

	separator;
	
	checkBoxGrp	
		-numberOfCheckBoxes 1
		-label (uiRes("m_performBlendShapeRemove.kSpecifyTarget")) 
		-label1 ""
		-v1 0 
		-on1 "textFieldGrp -e -enable 1 bstNode"
 		-offCommand "textFieldGrp -e -enable 0 bstNode"
		bstSpec;

	textFieldGrp -enable 0 -label (uiRes("m_performBlendShapeRemove.kTargetName"))  -tx "" bstNode;
	separator;

	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Step 5: Deactivate the default UI template.
	//	===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	//	Provide more descriptive labels for the buttons.  This is not 
	//	necessary, but in some cases, for example, a button labelled 
	//	'Create' may be more meaningful to the user than one labelled
	//	'Apply'.
	//
	//	Disable those buttons that are not applicable to the option box.
	//
	//	Attach actions to those buttons that are applicable to the option
	//	box.  Note that the 'Close' button has a default action attached 
	//	to it that will hide the window.  If a a custom action is
	//	attached to the 'Close' button then be sure to call the 'hide the
	//	option box' procedure within the custom action so that the option
	//	box is hidden properly.

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle (uiRes("m_performBlendShapeRemove.kRemoveBlendShapeTargetOptions")); 

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "EditBlendShapeRemove" );

	//	Step 9: Set the current values of the option box.
	//	=================================================
	//
	eval (($setup + " " + $parent + " " + 0));	
	
	//	Step 10: Show the option box.
	//	=============================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      blendShapeRemoveHelp
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string blendShapeRemoveHelp()
{

	return 
	"  Command: blendShapeRemove";
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
proc string assembleCmd()
{
	string $cmd;
	
	setOptionVars(false);

	$cmd = "doBlendShape";

	//	This is remove mode = 3

	int $radio = 3;

	int $bsp=0;
	if (`checkBoxGrp -exists bsSpec`) {
		$bsp = `checkBoxGrp -query -v1 bsSpec`;
	}
	
	string $bsn="\"\"";
	if (`textFieldGrp -exists bsNode`) {
		$bsn = `textFieldGrp -query -tx bsNode`;
		if ($bsn=="") $bsn="\"\"";
	}
	
	int $bstp=0;
	if (`checkBoxGrp -exists bstSpec`) {
		$bstp = `checkBoxGrp -query -v1 bstSpec`;
	}
	
	string $bstn="\"\"";
	if (`textFieldGrp -exists bstNode`) {
		$bstn = `textFieldGrp -query -tx bstNode`;
		if ($bstn=="") $bstn="\"\"";
	}

	// the topology check, delete targets, wt and tween arguments are
	// not applicable to blendShapeRemove
	//
	int $top = 0;
	int $deltgt = 0;
	int $tween = 0;
	float $bstw=0;

	$cmd += (" " + $radio + " " + $tween + " " + $top + " " 
			 + $bsp + " " + $bsn + " "  + $bstp + " " + $bstn + " " + $bstw
		+ " " + $deltgt);
	
	return $cmd;
}

//
//  Procedure Name:
//      performBlendShapeRemove
//
//  Description:
//		Perform the optionBoxExample1 command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the optionBoxExample1 command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
global proc string performBlendShapeRemove(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Get the command.
			//
			$cmd = `assembleCmd`;

			//	Execute the command with the option settings.
			//
			eval($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			blendShapeRemoveOptions;
			break;

		//	Return the command string.
		//
		case 2:
			//	Get the command.
			//
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

