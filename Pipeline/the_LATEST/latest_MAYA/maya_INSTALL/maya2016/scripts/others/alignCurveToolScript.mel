// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      alignCurveToolScript
//
//  Description:
//	create wrapper around the alignCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc alignCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex alignCurveEuc` ) {
		optionVar -iv alignCurveEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex alignCurveLac` ) {
		optionVar -iv alignCurveLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q alignCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q alignCurveLac` $goToTool;
	}
}

global proc alignCurveValues ( string $toolName ) 
{
	performAlign 3 "Curve" $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc alignCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createAlignCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		alignCurveToolSetup( 0, "" );
		scriptCtx -i1 "alignCurve.png"
			-title (uiRes("m_alignCurveToolScript.kAlignCurvesTool"))
			-bcn "alignCurve"

			-exitUponCompletion `optionVar -q alignCurveEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("alignCurveToolScript 0")

			-setNoSelectionPrompt (uiRes("m_alignCurveToolScript.kSelectTwoCurves"))
			-setSelectionPrompt (uiRes("m_alignCurveToolScript.kSelectSecondCurves"))

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q alignCurveLac`
			-showManipulators true
			-setSelectionCount 2

			-nurbsCurve true
			-curveOnSurface true
			-curveParameterPoint true
			-editPoint true
		$tool;
	}
}

global proc string performAlignCurve( int $action,
									 string $goToTool )
{
	return performAlign( $action, "Curve", $goToTool );
}

global proc string alignCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolAlignCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "alignCurveContext";
			createAlignCurveContext $tool;
		}
	}
	return scriptToolScript( "performAlignCurve", $setToTool, $tool );
}
