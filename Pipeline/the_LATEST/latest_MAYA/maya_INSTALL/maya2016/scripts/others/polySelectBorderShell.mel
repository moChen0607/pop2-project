// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc polySelectBorderShell(int $borderOnly)
//
// Description:
//		Select either a shell or border based on
//		the current component selection
// Arguments:
//		borderOnly
// Returns:
//
//
{
	string $compSel[]; 
	
	// Process all the subdiv components first
	string $subdComps[];

	if (`isTrue "SubdivExists"`) {
	
		$subdComps = `subdListComponentConversion -fv -ff -fuv -fe -tuv`;

		// Process only one subd component type: UVs, Faces, edges
 		$subdComps = `filterExpand -ex false -sm 73 $subdComps`;
		if (size($subdComps)) {
	
			// Convert selected subd uvs to shell uvs or border
			if( $borderOnly ) {
				$subdComps = `subdListComponentConversion -fuv -tuv -uvb $subdComps`;
			} else {
				$subdComps = `subdListComponentConversion -fuv -tuv -uvs $subdComps`;
			}
		}
	}	// if SubdivUIExists

	// Turn on shell mode for current selection. 
	polySelectConstraint -t 0;
	polySelectConstraint -sh 1 -bo 0 -m 2; 

	// If want borders filter AFTER getting the entire shell
	if ($borderOnly)
	{
		// // Only do one type. Order of testing is: UVs, Vtx, Face, Edge
 		// uv
 		$compSel = `filterExpand -ex false -sm 35`;
		if (size($compSel))
		{
			polySelectConstraint -t 0x0010 -sh 0 -bo 1 -m 2;
			polySelectConstraint -t 0x0010 -sh 1 -bo 0 -m 0;
		}
		else
		{
			// Vertex
			$compSel = `filterExpand -ex false -sm 31`;
			if (size($compSel))
			{
				polySelectConstraint -w 1 -t 0x0001 -m 2;
				polySelectConstraint -w 0 -t 0x0001 -m 0;
			}
			else
			{
				// edge
				$compSel = `filterExpand -ex false -sm 32`;
				if (size($compSel))
				{
					polySelectConstraint -w 1 -t 0x8000 -m 2;
					polySelectConstraint -w 0 -t 0x8000 -m 0;
				}
				else
				{
					// face
					$compSel = `filterExpand -ex false -sm 34`;
					if (size($compSel))
					{
						polySelectConstraint -w 1 -t 0x0008 -m 2;
						polySelectConstraint -w 0 -t 0x0008 -m 0;
					}
				}
			}
		}
	}

	// Reset shell constraint
	polySelectConstraint -sh 0 -bo 0 -m 0;

	// Add to the selection list the subd components
	if( size($subdComps) > 0 ) {
		select -add $subdComps;
	}
}
