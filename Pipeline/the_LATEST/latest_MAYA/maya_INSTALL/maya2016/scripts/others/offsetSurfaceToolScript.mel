// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      offsetSurfaceToolScript
//
//  Description:
//	create wrapper around the offsetSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc offsetSurfaceToolSetup(int $forceFactorySettings, string $goToTool)
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex offsetSurfaceEuc` ) {
		optionVar -iv offsetSurfaceEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex offsetSurfaceLac` ) {
		optionVar -iv offsetSurfaceLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q offsetSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q offsetSurfaceLac` $goToTool;
	}
}

global proc offsetSurfaceValues ( string $toolName ) 
{
	performOffsetSurface 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc offsetSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createOffsetSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		offsetSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "offsetSurface.png"
		  -title (uiRes("m_offsetSurfaceToolScript.kOffsetCurve"))
		  -bcn "offsetSurface"

		  -exitUponCompletion `optionVar -q offsetSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("offsetSurfaceToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_offsetSurfaceToolScript.kSelectNURBSOffset"))
		  -setSelectionPrompt (uiRes("m_offsetSurfaceToolScript.kSelectMoreNURBSOffset"))
		  -setDoneSelectionPrompt (uiRes("m_offsetSurfaceToolScript.kDoneSelect"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q offsetSurfaceLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		$tool;
	}
}

global proc string offsetSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolOffsetSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "offsetSurfaceContext";
			createOffsetSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performOffsetSurface", $setToTool, $tool );
}
