// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


//<doc>
//<name findRelatedDeformer>
//
//<synopsis>
//		findRelatedDeformer (string $obj)
//
//<returns>
//		None
//
//<description>
//      Script for finding the deformers that deform the specified shape.
//
//<examples>
// // To find the deformers for a shape called "johnBoy", type:
//
// findRelatedDeformer("johnBoy");
//
//</doc>
/////////////////////////////////////////////////////////////////////////
//
global proc string[] findRelatedDeformer(string $obj)
{
	string $deformers[];
	string $shape = "";
	string $shapeWithPath;
	string $hiddenShape = "";
	string $hiddenShapeWithPath;
	string $cpTest[] = `ls -type controlPoint $obj`;
	if (size($cpTest)) {
		$shape = $obj;
	} else {
		string $rels[] = `listRelatives $obj`;
		for ($r in $rels) {
			$cpTest = `ls -type controlPoint ($obj+"|"+$r)`;
			if (0 == size($cpTest)) {
				continue;
			}
			int $io = `getAttr ($obj+"|"+$r+".io")`;
			if ($io) {
				continue;
			}

			int $visible = `getAttr ($obj+"|"+$r+".v")`;
			if (! $visible) {
				$hiddenShape = $r;
				$hiddenShapeWithPath = ($obj+"|"+$r);
				continue;
			}

			$shape = $r;
			$shapeWithPath = ($obj+"|"+$r);
			break;
		}
	}

	if ($shape == "") {
		if ($hiddenShape == "") {
			return $deformers;
		} else {
			$shape = $hiddenShape;
			$shapeWithPath = $hiddenShapeWithPath;
		}
	}

	string $wtGeoms[] = `ls -type geometryFilter`;
	for ($d in $wtGeoms) {
		string $geom[] = `deformer -q -g $d`;
		for ($g in $geom) {
			if ($g == $shape || $g == $shapeWithPath) {
				$deformers[size($deformers)] = $d;
			}
		}
	}

	return $deformers;
}
