// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  Oct 18, 2004
//
//  Procedure Name:
//      proxyMultiSwitch
//
//  Description:
//      Switches the active reference to the given tag, for the given array of
//  reference nodes (as specified by the global gProxyMultiSwitchRefNodes).
//	Each reference node implicitly defines a distinct proxy set.
//
//  Input Arguments:
//      Tag, list of reference nodes.
//
//  Return Value:
//      None.
//

global proc proxyMultiSwitch(string $tag)
//
//	Description:
//
//		Given a tag, and a list of references, for each reference that has
//	a proxy with this tag, make it active, and if necessary, load it.
//
{
	global string $gProxyMultiSwitchRefNodes[];
	if( !`exists isActiveProxy` ){
		source "proxyUtils.mel";
	}
	int $refIndex = 0;
	int $refSize = size($gProxyMultiSwitchRefNodes);
	while( $refIndex < $refSize ){
		if( !`objExists $gProxyMultiSwitchRefNodes[$refIndex]` ){
			// This could happen if we switched a proxy that was a parent of
			// this reference originally, such that this reference node is
			// actually no longer part of the scene. In such cases, we just
			// skip this reference node.
			//
			$refIndex++;
			continue;
		}
		string $proxyNodes[] = `getRelatedProxies $gProxyMultiSwitchRefNodes[$refIndex]`;
		int $proxySize = size( $proxyNodes );
		int $proxyIndex = 0;
		while( $proxyIndex < $proxySize ){
			string $proxyNode = $proxyNodes[$proxyIndex];
			string $proxyTag=`getAttr ($proxyNode + ".proxyTag")`;
			if( $proxyTag == $tag ){
				// If this isn't the active proxy, make it so.
				//
				if( !isActiveProxy( $proxyNode ) ){
					proxySwitch( $proxyNode );
				}
				break;	// tags should be unique
			}
			$proxyIndex++;
		}
		$refIndex++;
	}
}
