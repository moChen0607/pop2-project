// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  August 14, 1996
//
//  Description:
//      Create text using the option box defaults.
//
//  Input Arguments:
//      The string.
//
//  Return Value:
//      None.
//

global proc textCurvesSet(
 	int $dohistory,
	int $dohow,
	string $dofont,
	string $text )
{
	string $textDag[], $letters[], $curves[];
	string $results, $tmp[], $name, $tmpCmd;
	int $i, $n;
	int $historyOnTextCurvesNeeded = 0;

	// replace any \ by \\ and " by \" in the string so that the command will
	// create the \ and " as well
	//
	string $totalText = substituteAllString( $text, "\\", "\\\\" );
	$totalText = substituteAllString( $totalText, "\"", "\\\"" );

	string $cmd = "textCurves -ch " + $historyOnTextCurvesNeeded +
		" -f \"" + $dofont + "\" -t \"" + $totalText + "\"";
    if ( catch( $textDag = evalEcho($cmd) ) )
	{
		// if textCurves failed then no need to continue since nothing was
		// created
		return;
	}

	if( ! $dohow ) {
		select -r $textDag[0];
		return;
	}

	$letters = `listRelatives -c $textDag[0]`;

	float $tol = `optionVar -q positionalTolerance`;
	$n = size( $letters );
	for( $i=0; $i<$n; $i++ ) {
		$name = ("Trim " + $letters[$i] + " #");
		$tmpCmd = "planarSrf -name \"" + $name + "\" -ch " +
			$dohistory + " -tol " + $tol + " -o on -po " + ($dohow == 2) + " " + $letters[$i];
 		$tmp = evalEcho( $tmpCmd );
		if( "" == $results ) {
			$results = `group -n ("Trim " + $textDag[0] + " #") $tmp[0]`;
		}
		else {
			parent $tmp[0] $results;
		}
	}

	if( ! $dohistory ) {
		delete $textDag[0];
	}
	select -r $results;
}

