// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


proc updateFolliclesWithDefaultMap(string $polyObj)
{
	string $folliclesList[] = `listConnections -sh 1 -t follicle ($polyObj + ".outMesh")`;
	int $mapSetId = 0;
	string $defMap = "map1";

	for($i = 0; $i < size($folliclesList); $i ++)
	{
		setAttr ($folliclesList[$i] + ".mapSetName") -type "string" $defMap;
	}
}

proc string getUVSetNameFromValue(string $polyObj, string $uvSetName)
{
	string $uvAttrList[] = `listAttr -m -st uvSetName $polyObj`;
	string $emptyString;

	for($i = 0; $i < size($uvAttrList); $i++)
	{
		string $polyUVAttr = $polyObj + "." + $uvAttrList[$i];

		string $attrValid = `getAttr $polyUVAttr`;

		if($attrValid == $uvSetName)
			return $polyUVAttr;
	}
	return $emptyString;
}

proc int checkExistenceOfUV(string $polyObj, string $folliclesList[])
{
	string $mapSetName;

	if(size($folliclesList))
		$mapSetName = `getAttr ($folliclesList[0] + ".mapSetName")`;

	string $attrValue = getUVSetNameFromValue($polyObj, $mapSetName);
	if(size($attrValue))
		return 1;

	return 0;
}

global proc string hairGetUVSetOfHairSystem(string $hairSys, string $shapeObj)
{
	string $outMeshAttr = $shapeObj + ".outMesh";
	string $connFollicles[] = `listConnections -sh 1 -t follicle $outMeshAttr`;

	if(checkExistenceOfUV($shapeObj, $connFollicles) == 0)
	{
		updateFolliclesWithDefaultMap($shapeObj);
		return ($shapeObj + ".uvSet[0].uvSetName");
	}

	string $usedMapSetName;

	if(size($connFollicles))
		$usedMapSetName = `getAttr ($connFollicles[0] + ".mapSetName")`;

	string $attrValue = getUVSetNameFromValue($shapeObj, $usedMapSetName);

	return $attrValue;
}