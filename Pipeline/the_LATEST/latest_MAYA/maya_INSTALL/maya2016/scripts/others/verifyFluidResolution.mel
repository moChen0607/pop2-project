// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  20 Feb 2002
//
//  Description:
//      Verify fluid resolution
//		
//
global proc int verifyFluidResolution(string $fluid,  int $newResX,
									   int $newResY,
									   int $newResZ )
//
// Description:
//	Put up a confirm box if the new resolution might be too large.
//	Return true if resolution is verified (either not too large
//  or user chooses to continue anyway), false if not.
//  
//  For 2D fluids, pass in "1" for $newResZ.
//  For initial fluid creation , pass in "" for fluid name
//  
{
	int $resOK = true;
	int $newRes = $newResX * $newResY * $newResZ; // negative if too large

	if( size($fluid) > 0 ) {
		int $oldResX = getAttr( $fluid + ".resolutionW" );
		int $oldResY = getAttr( $fluid + ".resolutionH" );
		int $oldResZ = getAttr( $fluid + ".resolutionD" );	    
		int $oldRes = $oldResX * $oldResY * $oldResZ;
		if( ($oldRes >= $newRes) && ( $newRes > 0 )) {
			return $resOK;
		}
	}

	if( $newRes > 3375000 || $newRes < 0 ) {
		string $title   = (uiRes("m_verifyFluidResolution.kTitle"));
		string $cancel = (uiRes("m_verifyFluidResolution.kCancel"));
		string $saveAndContinue = (uiRes("m_verifyFluidResolution.kSaveandContinue")); 
		string $message = (uiRes("m_verifyFluidResolution.kPerformanceAfectedMessage"));
		$title = `format -s $newResX -s $newResY -s $newResZ $title`;
		string $lastChance = `confirmDialog 
			-title $title
			-message $message 
			-button $saveAndContinue  
			-button (uiRes("m_verifyFluidResolution.kContinue"))
			-button $cancel
			-defaultButton $saveAndContinue
			-cancelButton $cancel 
			-dismissString $cancel`;	

		if( $lastChance == $cancel ) {
			$resOK = false;
		} else if( $lastChance == $saveAndContinue ) {
			FileMenu_SaveItem();
		}
	}

	return $resOK;
}

