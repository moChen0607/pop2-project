// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      offsetCosToolScript
//
//  Description:
//	create wrapper around the offsetCos command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc offsetCosToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex offsetCosEuc` ) {
		optionVar -iv offsetCosEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex offsetCosLac` ) {
		optionVar -iv offsetCosLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q offsetCosEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q offsetCosLac` $goToTool;
	}
}

global proc offsetCosValues ( string $toolName ) 
{
	performOffsetCos 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc offsetCosProperties()
{
	scriptCtxCommonProperties;
}

proc createOffsetCosContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		offsetCosToolSetup( 0, "" );
		scriptCtx -i1 "offsetCos.png"
		  -title (uiRes("m_offsetCosToolScript.kOffsetCurveSurfaceTool"))
		  -bcn "offsetCos"

		  -exitUponCompletion `optionVar -q offsetCosEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("offsetCosToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_offsetCosToolScript.kSelectCurves"))
		  -setSelectionPrompt (uiRes("m_offsetCosToolScript.kSelectAdditionalCurves"))
		  -setDoneSelectionPrompt (uiRes("m_offsetCosToolScript.kComputeOffset"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q offsetCosLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		$tool;
	}
}

global proc string offsetCosToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolOffsetCoS` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "offsetCosContext";
			createOffsetCosContext $tool;
		}
	}
	return scriptToolScript( "performOffsetCos", $setToTool, $tool );
}
