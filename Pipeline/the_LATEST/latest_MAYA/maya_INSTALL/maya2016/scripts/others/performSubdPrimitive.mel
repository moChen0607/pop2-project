// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date: Dec 2000
//
//  Description:
//	This script defines the option box for the subdiv With History menu item.
//
//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	if ($forceFactorySettings ||  !`optionVar -exists subdPrimitiveStyle`) {
		optionVar -intValue subdPrimitiveStyle 1;
	}
	if ($forceFactorySettings ||  !`optionVar -exists subdPrimitiveShape`) {
		optionVar -intValue subdPrimitiveShape 6;
	}
}

//
//  Procedure Name:
//      subdPrimitiveSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//	forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc subdPrimitiveSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	//	Query the optionVar's and set the values into the controls.

	//Primitive Style
	int $subdPrimitiveStyle = `optionVar -q subdPrimitiveStyle`;
	radioButtonGrp -e -select $subdPrimitiveStyle subdPrimitiveStyle;

	//Primitive Shape
	int $subdPrimitiveShape = `optionVar -q subdPrimitiveShape`;
	optionMenuGrp -e -select $subdPrimitiveShape subdPrimitiveType;
}

//
//  Procedure Name:
//      subdPrimitiveCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc subdPrimitiveCallback(string $parent, int $doIt)
{
    setParent $parent;

	//	Set the optionVar's from the control values, and then
	//	perform the command.

	optionVar -intValue subdPrimitiveStyle `radioButtonGrp -q -select subdPrimitiveStyle`;
	optionVar -intValue subdPrimitiveShape `optionMenuGrp -q -select subdPrimitiveType`;

	if ($doIt) {
		performSubdPrimitive 0; 
		addToRecentCommandQueue "performSubdPrimitive 0" "Subdiv Primitive";
	}
}

//
//  Procedure Name:
//      subdPrimitiveOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc subdPrimitiveOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "subdPrimitive";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	//	The value returned is the name of the layout to be used as
	//	the parent for the option box UI.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	//	Any default option box behaviour based on the command name is set 
	//	up with this call.  For example, updating the 'Help' menu item with
	//	the name of the command.
	//
	setOptionBoxCommandName("createSubdPrimitive");
	
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	//	Activate the default UI template so that the layout of this 
	//	option box is consistent with the layout of the rest of the 
	//	application.
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	//	STEP 4: Create option box contents.
	//	===================================
	//
	tabLayout -scr true -tv false;
	string $parent = `columnLayout -adjustableColumn 1`;

	optionMenuGrp -label (uiRes("m_performSubdPrimitive.kPrimitive"))
		-cal 1 "right"
		subdPrimitiveType;
		menuItem -label (uiRes("m_performSubdPrimitive.kSphere"));
		menuItem -label (uiRes("m_performSubdPrimitive.kCube"));
		menuItem -label (uiRes("m_performSubdPrimitive.kCylinder"));
		menuItem -label (uiRes("m_performSubdPrimitive.kCone"));
		menuItem -label (uiRes("m_performSubdPrimitive.kPlane"));
		menuItem -label (uiRes("m_performSubdPrimitive.kTorus"));
		setParent -menu ..;

	radioButtonGrp -label (uiRes("m_performSubdPrimitive.kPrimitiveStyle")) 
		-numberOfRadioButtons 2
		-l1 (uiRes("m_performSubdPrimitive.kNurbs"))
		-l2 (uiRes("m_performSubdPrimitive.kPolygon"))
		-select 1
		subdPrimitiveStyle;

	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Step 5: Deactivate the default UI template.
	//      ===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	//	Provide more descriptive labels for the buttons.
	//	Disable those buttons that are not applicable to the option box.
	//	Attach actions to those buttons that are applicable to the option box.

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performSubdPrimitive.kCreate"))
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle((uiRes("m_performSubdPrimitive.kSubdivPrimitiveWithHistoryOptions")) );

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "" );

	//	Set the current values of the option box.
	//	=========================================
	//
	eval (($setup + " " + $parent + " " + 0));	
	
	//	Show the option box.
	//	====================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      subdPrimitiveHelp 
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string nurbsSquareHelp()
{
	return (uiRes("m_performSubdPrimitive.kNurbsSquareHelp"));
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
proc string assembleCmd()
{
	string $cmd = "createSubdPrimitive(";

	setOptionVars(false);

	int $primitiveStyle = `optionVar -q subdPrimitiveStyle`;
	$cmd = $cmd + $primitiveStyle + ", " ;

	int $primitiveShape = `optionVar -q subdPrimitiveShape`;
	$cmd = $cmd + $primitiveShape + ")";

	return $cmd;
}

//
//  Procedure Name:
//      performSubdPrimitive
//
//  Description:
//		Perform the createSubdPrimitive command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performSubdPrimitive(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Get the command.
			//
			$cmd = `assembleCmd`;

			//	Execute the command with the option settings.
			//
			evalEcho($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			subdPrimitiveOptions;
			break;

		//	Return the command string.
		//
		case 2:
			//	Get the command.
			//
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}
