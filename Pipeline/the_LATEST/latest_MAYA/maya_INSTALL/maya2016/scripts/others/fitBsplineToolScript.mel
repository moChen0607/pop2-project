// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      fitBsplineToolScript
//
//  Description:
//	create wrapper around the fitBspline command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc fitBsplineToolSetup( int $forceFactorySettings, string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex fitBsplineEuc` ) {
		optionVar -iv fitBsplineEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex fitBsplineLac` ) {
		optionVar -iv fitBsplineLac 1;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q fitBsplineEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q fitBsplineLac` $goToTool;
	}
}

global proc fitBsplineValues ( string $toolName ) 
{
	performFitBspline 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc fitBsplineProperties()
{
	scriptCtxCommonProperties;
}

proc createFitBsplineContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		fitBsplineToolSetup( 0, "" );
		scriptCtx -i1 "fitBSpline.png"
			-title (uiRes("m_fitBsplineToolScript.kFitBsplineTool"))
			-bcn "fitBspline"

			-exitUponCompletion `optionVar -q fitBsplineEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("fitBsplineToolScript 0")

			-setNoSelectionPrompt (uiRes("m_fitBsplineToolScript.kSelectCurves"))
			-setSelectionPrompt (uiRes("m_fitBsplineToolScript.kSelectAdditionalCurves"))

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q fitBsplineLac`
			-setAllowExcessCount true
			-showManipulators true

			-setSelectionCount 0
			-nurbsCurve true
			-isoparm true
			-curveOnSurface true
			-surfaceEdge true
			-polymeshEdge true
		$tool;
	}
}

global proc string fitBsplineToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolFitBspline` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "fitBsplineContext";
			createFitBsplineContext $tool;
		}
	}
	return scriptToolScript( "performFitBspline", $setToTool, $tool );
}
