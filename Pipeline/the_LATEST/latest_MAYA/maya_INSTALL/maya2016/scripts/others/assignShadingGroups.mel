// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  12 Dec 1996
//
//  Description:
//      Assign the nodes in thisNode[] to the second node.
//

global proc assignShadingGroups(string $thisNode[], string $toThisNode)
//
//  Procedure Name:
//      assignShadingGroups
//
//  Description:
//      Implements a render assign.  The names in the array should all be 
//		names of renderable geometry.  If $toThisNode is a shadingEngine (SE),
//		each array element is added as a member of it.  If $toThisNode isn't
//		an SE, the optionVar "assignPref" is checked.  If True, a new SE is
//		created, each array element is added as a member, and $toThisNode is
//		attached to the surfaceshader attr of the new SE.
//		
//  Input Arguments:
//		string $thisNode[] - array of nodes to assign
//		string $toThisNode - single node name to assign *to*
//
//  Return Value:
//      None.
//
{
	string $toType = `nodeType $toThisNode`;
	string $SEname = $toThisNode;
	string $cmd;
	string $ok = (uiRes("m_assignShadingGroups.kOk"));
	string $exitVal = $ok ;

	if($toType != "shadingEngine") {
		string $cancel =  (uiRes("m_assignShadingGroups.kCancel"));  
		string $msg = (uiRes("m_assignShadingGroups.kShadingGroupMsg"));
		$msg = `format -s $toThisNode -s $ok -s $toThisNode -s $cancel $msg`;
		$exitVal = `confirmDialog -title (uiRes("m_assignShadingGroups.kWarning")) -message $msg
						 -button $ok -button $cancel
						 -defaultButton $ok -cancelButton $cancel
						 -dismissString $cancel`;

		if ($exitVal == $ok) {

			$cmd = "sets -renderable true -noSurfaceShader true -name " + $toThisNode + "SG -empty";
			$SEname = `evalEcho $cmd`;
		
			$cmd = "defaultNavigation -f true -connectToExisting -source " + $toThisNode + " -destination " + $SEname;
			evalEcho $cmd;
		}
	}
	
	if ($exitVal == $ok) {
		$cmd = "sets -e -forceElement " + $SEname;

		int $i;
		int $numNodes = `size $thisNode`;
		
		for ($i = 0; $i < $numNodes; $i++) {
			$cmd += " " + $thisNode[$i];
		}
			
		evalEcho $cmd;
	}
}
