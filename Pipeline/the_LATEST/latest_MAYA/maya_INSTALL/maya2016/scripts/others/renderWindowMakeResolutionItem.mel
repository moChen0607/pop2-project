// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

// Description:  This procedure is used to get the render resolution.
//
proc int[] getGlobalsResolution()
{
	//
	//	Render globals item.
	//
	string	$globals[] = `ls -renderGlobals`;
	int		$res[2] = { 640, 480 };

	if( size($globals[0]) > 0 )
	{
		string	$connect[] = `listConnections ($globals[0] + ".resolution")`;

		if( size($connect[0]) > 0 )
		{
			$res[0] = `getAttr ($connect[0] + ".width" )`;
			$res[1] = `getAttr ($connect[0] + ".height" )`;
		}
	}

	return $res;
} 

// Description:  This procedure is used to make the resolution
//      submenu under the menus 
//          . "render window --> Options --> Test Resolution"
//          . or the "main window --> Render --> Test Resolution".
//
global proc renderWindowMakeResolutionItem ( string $subMenuName, string $panelName )
{
	global int $renderViewResolutions[];
	popupMenu -e -deleteAllItems $subMenuName;
	setParent -m $subMenuName;

	//
	//	Use panel resolution item.
	//
	int $testRes	= `optionVar -q renderViewTestResolution`;
	int	$panelBool	= ( $testRes == 0 );
	int $fullBool	= ( $testRes == 1 );
	int $res[]		= `getGlobalsResolution`;

	menuItem -label (uiRes("m_renderWindowMakeResolutionItem.kCameraPanel"))  
             -checkBox $panelBool -c "setTestResolutionVar(0)";

    string $renderSettingString = (localizedUIMisc("Render Settings"));
    $renderSettingString += " (" + $res[0] + "x" + $res[1] + ")";
	menuItem -label $renderSettingString
			 -checkBox $fullBool -c "setTestResolutionVar(1)";

	menuItem -divider 1;

	$renderSettingString = (uiRes("m_renderWindowMakeResolutionItem.kPercentageSettings"));
	for ($i = 2 ; $i < size($renderViewResolutions) ; $i++ ) {
		$x = $res[0] * $renderViewResolutions[$i];
		$y = $res[1] * $renderViewResolutions[$i];
		$x /= 100;
		$y /= 100;
		int $perc = $renderViewResolutions[$i];
		
		menuItem -label `format -s $perc -s $x -s $y $renderSettingString`
			-checkBox ($testRes == $i) -c ("setTestResolutionVar("+$i+")");
		if ($i == 5) { menuItem -divider 1; }
	}
}
