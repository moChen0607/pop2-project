// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//
//
//
//
//	Creation Date:  3 Aug 1998
//
//  Procedure Name:
//      newSkinCluster
//
//  Description:
//      Creates a skinCluster
//

proc int hasSingleBindPoseMovedOrExistsMultipleBindPoses(string $jointName)
{
	int $result = 0;

	// Get all bindPose-based connections to this joint
	//
	string $bindPoseConnections[] = `dagPose -q -bindPose $jointName`;

	// Cases:
	// - If at the joint there is one bindPose connection, then either the pose
	//   changed or remained at pose
	// - If at the joint there are two or more bindPose connections
	//   then there are multiple bind poses at the joint
	if (size($bindPoseConnections)>0)
	{
		string $bindPoses[] = eval("dagPose -q -atPose " + $bindPoseConnections[0]);
		if (size($bindPoses) > 0)
		{
			$result=1;
		}
	}

	return $result;
}

proc int validateMultipleBindPoses()
{
	int $result=1;

	string $cmd = "ls -selection -type joint";
	string $joints[]=eval($cmd);
	for ($i=0;$i<size($joints);$i=$i+1)
	{
		if (hasSingleBindPoseMovedOrExistsMultipleBindPoses($joints[$i]))
		{
			string $msg = (uiRes("m_newSkinCluster.kBoundAtDifferentPose"));
			error `format -s $joints[$i] $msg`;
			$result=0;
			break;
		}
	}

	return $result;
}

global proc string[] newSkinCluster(string $inArgs)
//
// $inArgs: Prior to MacGyver, this was just a string of flags to be sent to
//		  the skinCluster command. Post MacGyver, to support additional settings,
//		  it can optionally contain a comma separated list of additional settings.
//        
//	So after tokenizing $inArgs:
//		token 0 = the flags to send to the skinCluster command
//  Remaining tokens come in pairs of (description of option, value of option).
//  Supported options: multipleBindPose
//	
{
	string $res[];
	string $buff[];
	tokenize($inArgs,",",$buff);

	string $args = $buff[0];
	int $allowMultipleBindPoses = 1;
	if (size($buff) > 1) {
		for ($ii = 1; $ii < size($buff); $ii+=2) {
			if ($buff[$ii] == "multipleBindPose") {
				$allowMultipleBindPoses = $buff[$ii+1];
			}
		}
	}
	
	if ($allowMultipleBindPoses || validateMultipleBindPoses())
	{
		$cmd = "createSkinCluster \""+ $args + "\"";
		$res = eval($cmd);
	}

	return $res;
}
