// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

// Selects the appropriate pickWalk command,
//   depending on the current selection.
//
//

global proc pickWalkRight()
{
	string $cmd;

	string $p = `getPanel -withFocus`;
	int $isUV = startsWith($p, "polyTexturePlacementPanel");

	if ($isUV)
	{
		// Turn off soft select
		if(`softSelect -q -sse`) {
			softSelect -sse 0;
		}

		// Convert all faces/edges/vertices/UVs to UVs
		select -r `polyListComponentConversion -ff -fe -fv -fuv -tuv`;

		polySelectConstraint -t 0x0010 -sh 1 -bo 0 -m 2;
		polySelectConstraint -t 0x0010 -sh 0 -bo 0 -cr 0 -m 0;

		$cmd = ("polyEditUV -u 1.0");
	}
	else
	{	
		$cmd = "pickWalk -d right";
	
		string $type = "";
	
		// if any keys are selected, do a key pickwalk
		float $keys[] = `keyframe -q -selected`;
		if ( size( $keys ) != 0 ) {
			$type = "keys";
		}

		if ( size( $type ) == 0 ) {
			// if any lattice points are selected, do a lattice pickwalk
			string $latticeSel[] = `filterExpand -sm 46`;
			if ( size( $latticeSel ) != 0 ) {
				$type = "latticePoints";
			}
		}
	
		if ( size( $type ) == 0 ) {
			// if any poly edges are selected, do an edgeloop pickwalk
			string $edgeSel[] = `filterExpand -sm 32`;
			if ( size( $edgeSel ) != 0 ) {
				$type = "edgeloop";
			}
			else {
				string $faceSel[] = `filterExpand -sm 34`;
				if (size($faceSel) != 0){
					$type = "faceloop";
				}
			}
		}

		if ( size( $type ) == 0 ) {
			// if any motoinTrail points are selected, do a motionTrail pickwalk. Note that the '84' is defined in TselectionTypeSet.cpp
			string $motionTrailSel[] = `filterExpand -sm 84`;  
			if ( size( $motionTrailSel ) != 0 ) {
				$type = "motiontrailpoints";
			}
		}	

		// append the type, if any
		if ( size( $type ) != 0 ) {
			$cmd += " -type " + $type;
		}
	}

	evalEcho($cmd);
}
