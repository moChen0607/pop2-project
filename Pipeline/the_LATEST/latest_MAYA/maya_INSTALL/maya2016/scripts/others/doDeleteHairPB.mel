// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  20 Feb 2002
//
//  Description:
//      Delete HairSystem Cache
//		
//

proc deleteHairCache( string $cacheNode, int $deleteCacheFile )
//
// Description:
//	When we clear the cache: 
//		* delete the diskCache node
//		* delete any hidden disk files associated with this node
//		* delete the real disk file only if $deleteCacheFile == true
//	
{
	string $rules[] = `workspace -q -fr`;
	string $dataDir = "";
	int $i = 0;
	for( ; $i < size($rules)-1; $i+=2 ) {
		if( $rules[$i] == "diskCache" ) {
			$dataDir = $rules[$i+1];
			break;
		}
	}

	string $realPath = (`workspace -q -rd` + $dataDir);
	string $realFile = `getAttr ( $cacheNode + ".cacheName" )`;
	string $hiddenPath = `diskCache -q -tmp`;
	string $hiddenFile = `getAttr ( $cacheNode + ".hiddenCacheName" )`;
		
	if( $hiddenFile != $realFile ) {
		eval ("sysFile -del \"" + $hiddenPath + "/" + $hiddenFile + "\"" );
	}

	if( $deleteCacheFile ) {
		eval ("sysFile -del \"" + $realPath + "/" + $realFile + "\"" );
	}

	delete $cacheNode;
}



proc string clearHairPBCache( string $hair, int $deleteCacheFile )
{
	string $dskC;
	if(`connectionInfo -id ($hair + ".diskCache")` > 0 ) {
		string $src = `connectionInfo -sfd ($hair + ".diskCache")`;
		string $buffer[];
		tokenize($src, ".", $buffer);
		$dskC = $buffer[0];
		deleteHairCache( $dskC, $deleteCacheFile );
	}
	return $dskC;
}

proc clearActiveHairPBCaches( int $deleteFile )
{
	string $hairSystemShapes[] = `getSelectedHairSystems`;	
	for( $hair in $hairSystemShapes ) {
		clearHairPBCache( $hair, $deleteFile );
	}
}

global proc doDeleteHairPB()
//
// Description:
//	
//	
{
	// Make sure that we have a hair selected
	// or warn the user and fail
	//
	string $hairSystemShapes[] = getSelectedHairSystems();
	if( size($hairSystemShapes) == 0 )
	{
		error((uiRes("m_doDeleteHairPB.kNoHairSystemSelected")));
		return;
	}

	clearActiveHairPBCaches false;
}
