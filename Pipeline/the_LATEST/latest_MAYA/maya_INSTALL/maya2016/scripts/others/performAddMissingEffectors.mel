// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Description:
//		This script provides an option box dialog for the 
//   "characterize -e -addMissingEffectors" command. It is used to
//   add additional effectors to an existing full body setup. For
//   example if you animate a skeleton using fbik and then decide to
//   add fingers.
//
//	Input Arguments:
//		boolean showOptionBox	true - show the option box dialog
//								false - just execute the command
//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	//	Type: default is Labels
	//
	if ($forceFactorySettings || !`optionVar -exists fbikAmeType`) {
		optionVar -intValue fbikAmeType 1;
	}
}

//
//  Procedure Name:
//      addMissingEffectorsSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//		forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc addMissingEffectorsSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	//	Query the optionVar's and set the values into the controls.

	//	Type: Labels vs. Names
	//
	radioButtonGrp -edit 
		-select `optionVar -query fbikAmeType`
		fbikAmeTypeGrp;
}

//
//  Procedure Name:
//      addMissingEffectorsCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//		doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc addMissingEffectorsCallback(string $parent, int $doIt)
{
    setParent $parent;

	//	Set the optionVar's from the control values, and then
	//	perform the command.

	//	Type: Labels or Names
	//
	optionVar -intValue fbikAmeType
		`radioButtonGrp -query -select fbikAmeTypeGrp`;

	if ($doIt) {
		performAddMissingEffectors 0; 
		addToRecentCommandQueue "performAddMissingEffectors 0" "AddMissingFBIKEffectors";
	}
}

//
//  Procedure Name:
//      addMissingEffectorsOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc addMissingEffectorsOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "addMissingEffectors";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	Get the option box.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	Pass the command name to the option box.
	//
	setOptionBoxCommandName("characterize");
	
	//	Activate the default UI template.
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	tabLayout -scr true -tv false;

	string $parent = `columnLayout -adjustableColumn 1`;
	
	radioButtonGrp -label (uiRes("m_performAddMissingEffectors.kIdentifyJoints")) 
		-numberOfRadioButtons 2
		-label1 (uiRes("m_performAddMissingEffectors.kByLabel")) 
		-label2 (uiRes("m_performAddMissingEffectors.kByName")) 
		-select 1
		fbikAmeTypeGrp;

	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Deactivate the default UI template.
	//
	setUITemplate -popTemplate;

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performAddMissingEffectors.kAdd")) 
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Set the option box title.
	//
	setOptionBoxTitle (uiRes("m_performAddMissingEffectors.kAddMissingFBIKOptions"));

	//	Customize the 'Help' menu item.
	//
	setOptionBoxHelpTag( "AddMissingEffectors" );

	//	Set the current values of the option box.
	//
	eval (($setup + " " + $parent + " " + 0));
	
	//	Show the option box.
	//
	showOptionBox();
}

//
//  Procedure Name:
//      addMissingEffectorsHelp
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string addMissingEffectorsHelp()
{
	return 
	"  Command: characterize -e -addMissingEffectors - adds additional full body ik to a skeleton.\n" +
	"Selection: root joint";	
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc string assembleCmd()
{
	string $cmd = "doAddMissingEffectors(";

	setOptionVars(false);

	string $typeFlag;
	if (`optionVar -query fbikAmeType` == 1) {
		$typeFlag = " -type label";
	} else {
		$typeFlag = " -type name";
	}

	$cmd = ($cmd + "\"" + $typeFlag + "\")");
	return $cmd;
}

//
//  Procedure Name:
//      performAddMissingEffectors
//
//  Description:
//		Perform the addMissingEffectors command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the addMissingEffectors command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performAddMissingEffectors(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Retrieve the option settings
			//
			setOptionVars(false);

			//	Get the command.
			//
			$cmd = `assembleCmd`;

			//	Execute the command with the option settings.
			//
			evalEcho($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			addMissingEffectorsOptions;
			break;

		//	Return the command string.
		//
		case 2:
			//	Retrieve the option settings.
			//
			setOptionVars (false);

			//	Get the command.
			//
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

