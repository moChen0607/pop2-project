// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	fltOptions()
//
//	Description:
//
//		This script posts the OpenFlight file translator options.
//		The optionsString is of the form:
//
//			varName1=value1;varName2=value2;...
//
//	Parameters:
//
//		$parent	- The ELF parent layout for this options layout.
//		$action	- The action that is to be performed with the invocation of this procedure.
//				  Valid options are:
//
//					"query"	- Construct the options string and pass it to the resultCallback.
//					"post"	- post all the ELF controls.
//
//		$initialSettings	- The current options string in effect at the time this procedure is called.
//		$resultCallback	- This is the procedure to be called with the result string:
//
//					  "resultCallback( string $optionsString );"
//
//	Return Values:
//
//		1 - if successful
//		0 - otherwise
//
global proc int fltOptions( string $parent,
				    string $action,
				    string $initialSettings,
				    string $resultCallback )
{
	int $result = 1;
	string $optionsString = $initialSettings;

	//	If we are in "post" mode, generate the UI for the translator options.
	//
	if( $action == "post" )
	{
		setParent $parent;

		//	Hide the "optionsBoxForm" layout during UI generation.
		//
		formLayout -e -vis false optionsBoxForm;

		//	Generate UI.
		//
		columnLayout -adj true
				 -rs 10;

			columnLayout -adj true;

				checkBox -l "Convert Grouped Faces to Mesh Objects"
					   fltConvertToMesh;

				checkBox -l "Import unused records"
					   fltImportAllRecords;

				setParent ..;

			frameLayout -l "Selective Loading"
					-cll false
					-li 5
					-fn "smallPlainLabelFont"
					-mh 10
					-mw 5
					fltSelectiveLoading;

				columnLayout -adj true;

					checkBoxGrp -ncb 2
							-l1 "Polygon Meshes"
							-l2 "Materials"
							fltSelectiveLoadOpts1;

					checkBoxGrp -ncb 2
							-l1 "Textures"
							-l2 "Lights"
							fltSelectiveLoadOpts2;

					setParent ..;

				setParent ..;

			frameLayout -l "File I/O Reporting"
					-cll false
					-li 5
					-fn "smallPlainLabelFont"
					-mh 10
					-mw 5
					fltFileIOReporting;

				columnLayout -adj true;

					text -l "Import Reports"
					     -al "left"
					     -fn "smallPlainLabelFont"
					     fltImportReportLabel;

					separator -h 5 -style "none";

					checkBox -l "Post Process Report"
						   fltImpPostProcReport;

					checkBox -l "File Structure Report"
						   fltImpFileStructReport;

					checkBox -l "Read Structure Report"
						   fltImpReadStructReport;

					setParent ..;

				setParent ..;

			setParent..;

		formLayout -e -vis true optionsBoxForm;
	}
	//	Else, if we are in "query mode", construct the options string and pass it to
	//	the resultCallback.
	//
	else if( $action == "query" )
	{
		//	File I/O Reporting
		//
		$optionsString += "ImpProcRpt=" + `checkBox -q -v fltImpPostProcReport` + ";";
		$optionsString += "ImpFileRpt=" + `checkBox -q -v fltImpFileStructReport` + ";";
		$optionsString += "ImpReadRpt=" + `checkBox -q -v fltImpReadStructReport` + ";";

		eval( $resultCallback + " \"" + $optionsString + "\"" );
	}

	return $result;
}
