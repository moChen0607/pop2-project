// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      cutAttachCurvesToolScript
//
//  Description:
//	create wrapper around the cutAttachCurves command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc cutAttachCurvesToolSetup( int $forceFactorySettings, string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex cutAttachCurvesEuc` ) {
		optionVar -iv cutAttachCurvesEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex cutAttachCurvesLac` ) {
		optionVar -iv cutAttachCurvesLac 1;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q cutAttachCurvesEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q cutAttachCurvesLac` $goToTool;
	}
}

global proc cutAttachCurvesValues ( string $toolName ) 
{
	scriptCtxCommonValues( $toolName );
}

global proc cutAttachCurvesProperties()
{
	scriptCtxCommonProperties;
}

proc createCutAttachCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		cutAttachCurvesToolSetup( 0, "" );

		int $n = 0;
		if( `optionVar -q cutAttachCurvesLac` ) {
			$n = 2;
		}

		scriptCtx -i1 "vacantCell.png"
			-title (uiRes("m_cutAttachCurvesToolScript.kCutAttachCurvesTool"))
//			-bcn "cutCurves"

			-exitUponCompletion `optionVar -q cutAttachCurvesEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("cutAttachCurvesToolScript 0")

			-setNoSelectionPrompt ((uiRes("m_cutAttachCurvesToolScript.kSelectTwoCurves")) )
			-setSelectionPrompt ((uiRes("m_cutAttachCurvesToolScript.kSelectAnotherCurve")) )

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q cutAttachCurvesLac`
			-showManipulators true
			-setSelectionCount $n

			-nurbsCurve true
			-curveParameterPoint true
			-editPoint true
		$tool;
	}
}

global proc string cutAttachCurvesToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command		ONLY ALLOW THIS FOR NOW
{
	string $tool = "";
	string $afterCut[];
	int $n, $i;

	switch( $setToTool ) {
	  case 4:
		$tool = "cutAttachCurveContext";
		createCutAttachCurveContext $tool;
		setToolTo $tool;
		break;
	  case 0:
	  default:
		cutCurvePreset(0,1,0.1,6,0,0,1,4,1);
		$afterCut = `ls -sl`;
		$n = size($afterCut);
		for( $i=0; $i<$n; $i+=1 ) {
			// Now, two of these are bad, two are OK:
			if( `getAttr ($afterCut[$i] + ".spans")` < 1) delete $afterCut[$i];
		}
		doAttachCurveArgList( 2, { "0", "1", "1", "0", "0.5", "0", "0.1" } );
		break;
	}

	return "";
}
