// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
// Creation Date:  28 June 1996
//
//
//  Procedure Name:
//      ModelEdMenu
//
//  Description:
//      This procedure creates the popup/marking menu
//      that gets attached to the main view panes.  
//		This menu builds dynamically, and changes its
//		contents based what item the mouse is clicked over.
//
//  Input Arguments:
//      The parent item to parent the popup menu to.
//
//  Return Value:
//      None.
//


global proc ModelEdMenu( string $panelName ) {

	global int $gIsMarkingMenuOn;

	//  Find out current location of the panel and ensure that it has
	//  a control layout associated with it.
	//
	string $panelControl = `modelEditor -q -control $panelName`;
	if( $panelControl != "" )
	{
		//	regular popup mappings.
		int $objectButton = 3;
		int $ctlObjectQualifier = 0;
		int $shObjectQualifier = 0;
			
		string $objectMenuName = ( $panelName + "ObjectPop" );
		string $convertMenuName = ( $panelName + "ConvertPop" );
		string $commandMenuName = ( $panelName + "CommandPop" );
		string $toolOptionsMenuName = ( $panelName + "ToolOptionsPop" );

		// Build the object specific menu
		if( !`popupMenu -exists $objectMenuName` )
		{
			string $fullObjectMenuName = `popupMenu -parent $panelControl $objectMenuName`;

			string $buildObjectMenuItemsNow =  "buildObjectMenuItemsNow";

			popupMenu -e 
				-mm $gIsMarkingMenuOn
				-button $objectButton 
				-sh $shObjectQualifier  
				-ctl $ctlObjectQualifier 
				-aob true 
				-pmc (  $buildObjectMenuItemsNow + " \"" + $fullObjectMenuName + "\"")
				$objectMenuName;
		}

		if (!`popupMenu -exists $convertMenuName`) {
			
			string $fullConvertMenuName = `popupMenu -parent $panelControl
				-allowOptionBoxes true $convertMenuName`;
			string $buildConvertMM = "buildConvertMM";

			popupMenu -edit
				-markingMenu $gIsMarkingMenuOn 
				-ctrlModifier true 
				-button 3 
				-postMenuCommand ($buildConvertMM + " \"" + $fullConvertMenuName + "\"")
				$convertMenuName;
		}

		if (!`popupMenu -exists $commandMenuName`) {
			
			string $fullCommandMenuName = `popupMenu -parent $panelControl
				-allowOptionBoxes true $commandMenuName`;
			string $buildCommandMM = "contextToolsMM";

			popupMenu -edit
				-markingMenu $gIsMarkingMenuOn 
				-ctrlModifier false
				-shiftModifier true
				-button 3 
				-postMenuCommand ($buildCommandMM + " \"" + $fullCommandMenuName + "\"")
				$commandMenuName;
		}

		if (!`popupMenu -exists $toolOptionsMenuName`) {
			
			string $fullToolOptionsMenuName = `popupMenu -parent $panelControl
				-allowOptionBoxes true $toolOptionsMenuName`;
			string $buildCommandMM = "buildToolOptionsMM";

			popupMenu -edit
				-markingMenu $gIsMarkingMenuOn 
				-ctrlModifier true
				-shiftModifier true
				-button 3 
				-postMenuCommand ($buildCommandMM + " \"" + $fullToolOptionsMenuName + "\"")
				$toolOptionsMenuName;
		}
	}
}
