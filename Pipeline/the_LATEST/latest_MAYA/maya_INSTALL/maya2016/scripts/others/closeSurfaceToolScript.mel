// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      closeSurfaceToolScript
//
//  Description:
//	create wrapper around the closeSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc closeSurfaceToolSetup( int $forceFactorySettings,
									string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex closeSurfaceEuc` ) {
		optionVar -iv closeSurfaceEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex closeSurfaceLac` ) {
		optionVar -iv closeSurfaceLac 1;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q closeSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q closeSurfaceLac` $goToTool;
	}
}

global proc closeSurfaceValues ( string $toolName ) 
{
	performSurfaceClose 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc closeSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createCloseSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		closeSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "openCloseSurface.png"
		  -title (uiRes("m_closeSurfaceToolScript.kOpenCloseSurfaceTool"))
		  -bcn "closeSurface"

		  -exitUponCompletion `optionVar -q closeSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("closeSurfaceToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_closeSurfaceToolScript.kSelectToOpenClose")) )
		  -setSelectionPrompt ((uiRes("m_closeSurfaceToolScript.kSelectAdditionalOrOpenClose")) )
		  -setDoneSelectionPrompt ((uiRes("m_closeSurfaceToolScript.kEnterToOpenClose")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q closeSurfaceLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		  -isoparm true
		$tool;
	}
}

global proc string closeSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolCloseSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "closeSurfaceContext";
			createCloseSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performSurfaceClose", $setToTool, $tool );
}
