// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//

global proc breakRigidBodyConnections()
//
// This proc deletes all the connections to a rigid body that are not connections
// from the solver.
//
{
	int    $i, $j, $k;
    string $selObj[] = `ls -sl`;

	// For each object in the selection list, check to see if
    // and rigid bodies are present.
    //
    for ($i = 0; $i < size( $selObj ); $i++)
    {
        // Get a list of selected items and thier children.
        //
        string $kids[] = `ls -dag -leaf -showType $selObj[$i]`;

        // Check to see if we have any rigid bodies.
        //
        for ($j = 0; $j < size( $kids ); $j += 2)
        {
            // If a rigid body is found process it.
            //
            if ($kids[$j+1] == "rigidBody")
            {
				string $rigidName    = $kids[$j] + ".choice";
				string $bakeSimIndex = $kids[$j] + ".bakeSimulationIndex";

			    string $choiceList[] = `connectionInfo -dfs $rigidName`;

				// Get the choice node.
				//
				for ( $k = 0; $k < size( $choiceList ); $k++ )
				{
					string $choiceNode[] = `ls -o $choiceList[$k]`;
					string $destination  = $choiceNode[0] + ".input[1]";
					string $source       = `connectionInfo -sfd $destination`;

					if ( ( size( $source ) > 0 ) && ( size( $destination ) > 0 ) )
					{
						disconnectAttr $source $destination;
					}

					$destination  = $choiceNode[0] + ".input[2]";
					$source       = `connectionInfo -sfd $destination`;

					if ( ( size( $source ) > 0 ) && ( size( $destination ) > 0 ) )
					{
						disconnectAttr $source $destination;
					}
				}

				// If there is a connection to the bake sim attribute then we have
				// been baked.  Disconnect the connection to this attribute.
				//
				if ( $bakeSimIndex != -1 )
				{
					string $source = `connectionInfo -sfd $bakeSimIndex`;

					if ( ( size( $source ) > 0 ) && ( size( $bakeSimIndex ) > 0 ) )
					{
						disconnectAttr $source $bakeSimIndex;
					}

					// Reset the bake sim index.
					//
					setAttr $bakeSimIndex -1;
				}
			}
 		}
	}
}
