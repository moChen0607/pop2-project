// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  5 May 1997
//
//  Description:
//	This script defines the option box for the NURBS torus menu item.
//



//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	if( $forceFactorySettings ||
		!`optionVar -exists nurbsTorusUseGlobalTolerance`){
		optionVar -intValue nurbsTorusUseGlobalTolerance false;
	}

	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusPivotType`) {
		optionVar -intValue nurbsTorusPivotType 1;
	}

	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusPivotX`) {
		optionVar -floatValue nurbsTorusPivotX 0.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusPivotY`) {
		optionVar -floatValue nurbsTorusPivotY 0.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusPivotZ`) {
		optionVar -floatValue nurbsTorusPivotZ 0.0;
	}


	string $isitYup = `upAxis -q -ax`;
	if( "y" == $isitYup ) {
		if ($forceFactorySettings ||
			!`optionVar -exists nurbsTorusAxisType`) {
			optionVar -intValue nurbsTorusAxisType 2;
		}

		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisX`) {
			optionVar -floatValue nurbsTorusAxisX 0.0;
		}
		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisY`) {
			optionVar -floatValue nurbsTorusAxisY 1.0;
		}
		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisZ`) {
			optionVar -floatValue nurbsTorusAxisZ 0.0;
		}
	}
	else {
		if ($forceFactorySettings ||
			!`optionVar -exists nurbsTorusAxisType`) {
			optionVar -intValue nurbsTorusAxisType 3;
		}

		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisX`) {
			optionVar -floatValue nurbsTorusAxisX 0.0;
		}
		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisY`) {
			optionVar -floatValue nurbsTorusAxisY 0.0;
		}
		if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusAxisZ`) {
			optionVar -floatValue nurbsTorusAxisZ 1.0;
		}
	}


	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusStartSweep`) {
		optionVar -floatValue nurbsTorusStartSweep 0.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusEndSweep`) {
		optionVar -floatValue nurbsTorusEndSweep 360.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusMinorSweep`) {
		optionVar -floatValue nurbsTorusMinorSweep 360.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusRadius`) {
		optionVar -floatValue nurbsTorusRadius 1.0;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusHeightRatio`) {
		optionVar -floatValue nurbsTorusHeightRatio 0.5;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusDegree`) {
		optionVar -intValue nurbsTorusDegree 3;
	}



	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusUseTolerance`) {
		optionVar -intValue nurbsTorusUseTolerance false;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusTolerance`) {
		optionVar -floatValue nurbsTorusTolerance 0.01;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusSections`) {
		optionVar -intValue nurbsTorusSections 8;
	}
	if ($forceFactorySettings ||  !`optionVar -exists nurbsTorusSpans`) {
		optionVar -intValue nurbsTorusSpans 4;
	}
}

//
//  Procedure Name:
//      nurbsTorusSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//	forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc nurbsTorusSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	//	Query the optionVar's and set the values into the controls.

	// Pivot
	//
	int $pivotType = `optionVar -q nurbsTorusPivotType`;
	float $pivotX = `optionVar -q nurbsTorusPivotX`;
	float $pivotY = `optionVar -q nurbsTorusPivotY`;
	float $pivotZ = `optionVar -q nurbsTorusPivotZ`;
	if ($pivotType == 1) {
		$pivotX = 0;
		$pivotY = 0;
		$pivotZ = 0;
	}
	radioButtonGrp -e -select $pivotType nurbsTorusPivotType;
	floatFieldGrp -e
		-v1 $pivotX -v2 $pivotY -v3 $pivotZ
		nurbsTorusPivot;

	// Axis
	//
	int $axisType = `optionVar -q nurbsTorusAxisType`;
    float $axis[3];
    switch($axisType) {
      case 1:
        $axis[0] = 1;
        $axis[1] = 0;
        $axis[2] = 0;
        break;
      case 2:
        $axis[0] = 0;
        $axis[1] = 1;
        $axis[2] = 0;
        break;
      case 3:
        $axis[0] = 0;
        $axis[1] = 0;
        $axis[2] = 1;
        break;
      case 4:
        $axis[0] = `optionVar -q nurbsTorusAxisX`;
        $axis[1] = `optionVar -q nurbsTorusAxisY`;
        $axis[2] = `optionVar -q nurbsTorusAxisZ`;
        break;
      case 5:
        $axis = `nurbsViewDirectionVector 1`;
        break;
    }

	if( $axisType < 4 ) {
		radioButtonGrp -e -select $axisType nurbsTorusAxisType1;
	}
	else {
		radioButtonGrp -e -select ($axisType - 3) nurbsTorusAxisType2;
	}
	floatFieldGrp -e
		-v1 $axis[0] -v2 $axis[1] -v3 $axis[2]
		nurbsTorusAxis;

	// Sweep Angle (Start and End)
	//
	floatSliderGrp -e
		-v `optionVar -q nurbsTorusStartSweep`
		nurbsTorusStartSweep;
	floatSliderGrp -e
		-v `optionVar -q nurbsTorusEndSweep`
		nurbsTorusEndSweep;
	floatSliderGrp -e
		-v `optionVar -q nurbsTorusMinorSweep`
		nurbsTorusMinorSweep;

	// Radius
	//
	float $radius = `optionVar -q nurbsTorusRadius`;
	floatSliderGrp -e
		-v $radius
		nurbsTorusRadius;

	// Height Ratio
	//
	float $ratio = `optionVar -q nurbsTorusHeightRatio`;
	floatSliderGrp -e
		-v `nurbsRatioConvert $radius $ratio false`
		nurbsTorusHeight;

	// Degree
	//
	int $degree = `optionVar -q nurbsTorusDegree`;
	int $degreeBtn;
	switch($degree) {
		case 1: $degreeBtn = 1; break;
		case 3: $degreeBtn = 2; break;
	}
	radioButtonGrp -e
		-select $degreeBtn
		nurbsTorusDegree;

	// Tolerance
	//
	int $useGlobalTol = `optionVar -q nurbsTorusUseGlobalTolerance`;
	int $useTolerance = `optionVar -q nurbsTorusUseTolerance`;
	if( $useTolerance ) {
		if( $useGlobalTol ) {
			radioButtonGrp -e -select 3 nurbsTorusUseTolerance;
		}
		else {
			radioButtonGrp -e -select 2 nurbsTorusUseTolerance;
		}
	}
	else {
		radioButtonGrp -e -select 1 nurbsTorusUseTolerance;
	}

	floatSliderGrp -e
		-v `optionVar -q nurbsTorusTolerance`
		nurbsTorusTolerance;


	// Sections
	//
	intSliderGrp -e
		-v `optionVar -q nurbsTorusSections`
		nurbsTorusSections;

	// Spans
	//
	intSliderGrp -e
		-v `optionVar -q nurbsTorusSpans`
		nurbsTorusSpans;

	// Disable or enable some controls depending on other values.
	//
	if ($pivotType == 2)
		floatFieldGrp -e -enable 1 nurbsTorusPivot;
	else
		floatFieldGrp -e -enable 0 nurbsTorusPivot;

	if ($axisType == 4)
		floatFieldGrp -e -enable 1 nurbsTorusAxis;
	else
		floatFieldGrp -e -enable 0 nurbsTorusAxis;

	if ($useTolerance) {		
		if( $useGlobalTol ) {
			tabLayout -e -vis false tabLayoutForTolerance;
		}
		else {
			tabLayout -e -vis true -selectTab tabWithTolerance tabLayoutForTolerance;
		}
	}
	else {
		tabLayout -e -vis true -selectTab tabWithoutTolerance tabLayoutForTolerance;
	}
}

//
//  Procedure Name:
//      nurbsTorusCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc nurbsTorusCallback(string $parent, int $doIt)
{
    setParent $parent;

	//	Set the optionVar's from the control values, and then
	//	perform the command.

	// Pivot
	//
	int $pivotType = `radioButtonGrp -q -select nurbsTorusPivotType`;
	optionVar -intValue nurbsTorusPivotType $pivotType;
	if ($pivotType == 2) {
		optionVar -floatValue nurbsTorusPivotX `floatFieldGrp -q -v1 nurbsTorusPivot`;
		optionVar -floatValue nurbsTorusPivotY `floatFieldGrp -q -v2 nurbsTorusPivot`;
		optionVar -floatValue nurbsTorusPivotZ `floatFieldGrp -q -v3 nurbsTorusPivot`;
	}

	// Axis
	//
	int $axisType = `radioButtonGrp -q -select nurbsTorusAxisType1`;
	if( 0 == $axisType ) {
		$axisType = `radioButtonGrp -q -select nurbsTorusAxisType2` + 3;
	}
	optionVar -intValue nurbsTorusAxisType $axisType;
	if ($axisType == 4) {
		optionVar -floatValue nurbsTorusAxisX `floatFieldGrp -q -v1 nurbsTorusAxis`;
		optionVar -floatValue nurbsTorusAxisY `floatFieldGrp -q -v2 nurbsTorusAxis`;
		optionVar -floatValue nurbsTorusAxisZ `floatFieldGrp -q -v3 nurbsTorusAxis`;
	}

	// Sweep Angle (Start and End)
	//
	optionVar -floatValue nurbsTorusStartSweep
		`floatSliderGrp -q -v nurbsTorusStartSweep`;
	optionVar -floatValue nurbsTorusEndSweep
		`floatSliderGrp -q -v nurbsTorusEndSweep`;
	optionVar -floatValue nurbsTorusMinorSweep
		`floatSliderGrp -q -v nurbsTorusMinorSweep`;

	// Radius
	//
	float $radius = `floatSliderGrp -q -v nurbsTorusRadius`;
	optionVar -floatValue nurbsTorusRadius $radius;

	// Height Ratio
	//
	float $height = `floatSliderGrp -q -v nurbsTorusHeight`;
	optionVar -floatValue nurbsTorusHeightRatio
		`nurbsRatioConvert $radius $height true`;

	// Degree
	//
	int $degreeBtn = `radioButtonGrp -q -select nurbsTorusDegree`;
	int $degree;
	switch($degreeBtn) {
		case 1: $degree = 1; break;
		case 2: $degree = 3; break;
	}
	optionVar -intValue nurbsTorusDegree
		$degree;

	// Tolerance
	//
	int $useTolerance = `radioButtonGrp -q -select nurbsTorusUseTolerance`;

	switch( $useTolerance ) {
	  case 1:
		optionVar -intValue nurbsTorusUseTolerance false;
		break;
	  case 2:
		optionVar -intValue nurbsTorusUseTolerance true;
		optionVar -intValue nurbsTorusUseGlobalTolerance false;
		break;
	  case 3:
	  default:
		optionVar -intValue nurbsTorusUseTolerance true;
		optionVar -intValue nurbsTorusUseGlobalTolerance true;
		break;
	}

	optionVar -floatValue nurbsTorusTolerance
		`floatSliderGrp -q -v nurbsTorusTolerance`;

	// Sections
	//
	optionVar -intValue nurbsTorusSections
		`intSliderGrp -q -v nurbsTorusSections`;

	// Spans
	//
	optionVar -intValue nurbsTorusSpans
		`intSliderGrp -q -v nurbsTorusSpans`;

	if ($doIt) {
		performNurbsTorus 0; 
		addToRecentCommandQueue "performNurbsTorus 0" "NURBS Torus";
	}
}

//
//  Procedure Name:
//      nurbsTorusOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc nurbsTorusOptions()
{

	//	Name of the command for this option box.
	//
	string $commandName = "nurbsTorus";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	//	The value returned is the name of the layout to be used as
	//	the parent for the option box UI.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	//	Any default option box behaviour based on the command name is set 
	//	up with this call.  For example, updating the 'Help' menu item with
	//	the name of the command.
	//
	setOptionBoxCommandName("torus");
	
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	//	Activate the default UI template so that the layout of this 
	//	option box is consistent with the layout of the rest of the 
	//	application.
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	//	STEP 4: Create option box contents.
	//	===================================
	//
	tabLayout -scr true -tv false;
	string $parent = `columnLayout -adjustableColumn 1`;

	radioButtonGrp -label (uiRes("m_performNurbsTorus.kPivot")) 
		-numberOfRadioButtons 2
		-label1 (uiRes("m_performNurbsTorus.kObject")) 
		-label2 (uiRes("m_performNurbsTorus.kUserDefined")) 
		-select 1
		nurbsTorusPivotType;

	floatFieldGrp -label (uiRes("m_performNurbsTorus.kPivotPoint")) 
		-numberOfFields 3
		nurbsTorusPivot;

	radioButtonGrp -label (uiRes("m_performNurbsTorus.kAxis")) 
		-numberOfRadioButtons 3
		-label1 (uiRes("m_performNurbsTorus.kX")) 
		-label2 (uiRes("m_performNurbsTorus.kY")) 
		-label3 (uiRes("m_performNurbsTorus.kZ")) 
		-select 1
		nurbsTorusAxisType1;

	radioButtonGrp
		-numberOfRadioButtons 2
		-label1 (uiRes("m_performNurbsTorus.kFree")) 
		-label2 (uiRes("m_performNurbsTorus.kActiveView")) 
		-scl "nurbsTorusAxisType1"
		nurbsTorusAxisType2;

	floatFieldGrp -label (uiRes("m_performNurbsTorus.kAxisDefinition")) 
		-numberOfFields 3
		nurbsTorusAxis;

	separator;

	floatSliderGrp -label (uiRes("m_performNurbsTorus.kStartSweepAngle")) 
		-min 0 -max 360 -fmn -3600 -fmx 3600
		nurbsTorusStartSweep;

	floatSliderGrp -label (uiRes("m_performNurbsTorus.kEndSweepAngle")) 
		-min 0 -max 360 -fmn -3600 -fmx 3600
		nurbsTorusEndSweep;

	floatSliderGrp -label (uiRes("m_performNurbsTorus.kMinorSweepAngle")) 
		-min 0 -max 360 -fmn -3600 -fmx 3600
		nurbsTorusMinorSweep;

	floatSliderGrp -label (uiRes("m_performNurbsTorus.kRadius")) 
		-min 0.0001 -max 100
		-fmn 0.0001 -fmx 1000.0
		nurbsTorusRadius;

	floatSliderGrp -label (uiRes("m_performNurbsTorus.kMinorRadius")) 
		-min 0 -max 100
		-fmn 0 -fmx 1000.0
		nurbsTorusHeight;

	radioButtonGrp -label (uiRes("m_performNurbsTorus.kSurfaceDegree")) 
		-numberOfRadioButtons 2
		-label1 (uiRes("m_performNurbsTorus.kLinear")) 
		-label2 (uiRes("m_performNurbsTorus.kCubic")) 
		-select 2
		nurbsTorusDegree;

	// "Tolerance" disappears when "Use Tolerance" is false.
	// "Sections" disappears when "Use Tolerance" is true.
	// Note that the "-rational" flag no longer exists for
	// the torus command.  We do things as if the "-rational"
	// flag were always false.

	radioButtonGrp -nrb 3
		-label (uiRes("m_performNurbsTorus.kUseTolerance")) 
		-label1 (uiRes("m_performNurbsTorus.kNone")) 
		-label2 (uiRes("m_performNurbsTorus.kLocal")) 
		-label3 (uiRes("m_performNurbsTorus.kGlobal")) 
		nurbsTorusUseTolerance;

	tabLayout -tabsVisible false tabLayoutForTolerance;
		columnLayout tabWithoutTolerance;
			intSliderGrp -label (uiRes("m_performNurbsTorus.kNumberOfSections")) 
				-field true
				-min 4 -max 50 -fmx 200
				nurbsTorusSections;

			intSliderGrp -label (uiRes("m_performNurbsTorus.kNumberOfSpans")) 
				-field true
				-min 4 -max 50 -fmx 200
				nurbsTorusSpans;
		setParent ..;
		columnLayout tabWithTolerance;
			floatSliderGrp -label (uiRes("m_performNurbsTorus.kPositionalTolerance")) 
				-min 0.001 -max 1 -fmx 100.0
				nurbsTorusTolerance;
		setParent ..;
	setParent ..;

	// Set the pivot float fields to only be enabled when "Pivot" is "User Defined"
	//
	string $pivotEnable = ("floatFieldGrp -e -en 1 nurbsTorusPivot;"
		+ "floatFieldGrp -e"
		+ " -v1 `optionVar -q nurbsTorusPivotX`"
		+ " -v2 `optionVar -q nurbsTorusPivotY`"
		+ " -v3 `optionVar -q nurbsTorusPivotZ`"
		+ " nurbsTorusPivot;"
		);
	string $pivotDisable = "floatFieldGrp -e -en 0 nurbsTorusPivot;";
	radioButtonGrp -edit
		-cc1 ($pivotDisable + "floatFieldGrp -e -value 0.0 0.0 0.0 0.0 nurbsTorusPivot;")
		-cc2 $pivotEnable
		nurbsTorusPivotType;

	// Set the axis float fields to only be enabled when "Axis Preset" is "Free"
	//
	string $axisEnable = ("floatFieldGrp -e -en 1 nurbsTorusAxis;"
		+ "floatFieldGrp -e"
		+ " -v1 `optionVar -q nurbsTorusAxisX`"
		+ " -v2 `optionVar -q nurbsTorusAxisY`"
		+ " -v3 `optionVar -q nurbsTorusAxisZ`"
		+ " nurbsTorusAxis;"
		);
	string $axisDisable = "floatFieldGrp -e -en 0 nurbsTorusAxis;";
	radioButtonGrp -edit
		-cc1 ($axisDisable + "floatFieldGrp -e -value 1.0 0.0 0.0 0.0 nurbsTorusAxis;")
		-cc2 ($axisDisable + "floatFieldGrp -e -value 0.0 1.0 0.0 0.0 nurbsTorusAxis;")
		-cc3 ($axisDisable + "floatFieldGrp -e -value 0.0 0.0 1.0 0.0 nurbsTorusAxis;")
		nurbsTorusAxisType1;
	radioButtonGrp -edit
		-cc1 $axisEnable
		-cc2 $axisDisable
		nurbsTorusAxisType2;

	// Set the "Use Tolerance" checkbox so that if the checkbox is on, 
	// then the tolerance slider is visible.
	//
	radioButtonGrp -edit
	  -on1 "tabLayout -e -vis true -selectTab tabWithoutTolerance tabLayoutForTolerance"
	  -on2 "tabLayout -e -vis true -selectTab tabWithTolerance tabLayoutForTolerance"
	  -on3 "tabLayout -e -vis false  tabLayoutForTolerance"
		nurbsTorusUseTolerance;

	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Step 5: Deactivate the default UI template.
	//      ===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	//	Provide more descriptive labels for the buttons.
	//	Disable those buttons that are not applicable to the option box.
	//	Attach actions to those buttons that are applicable to the option box.

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performNurbsTorus.kCreateButton")) 
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle (uiRes("m_performNurbsTorus.kNURBSTorusOptions"));

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "NurbsTorus" );

	//	Set the current values of the option box.
	//	=========================================
	//
	eval (($setup + " " + $parent + " " + 0));	
	
	//	Show the option box.
	//	====================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      nurbsTorusHelp
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string nurbsTorusHelp()
{
	return 
	"  Command: torus - create a NURBS torus.\n";
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
proc string assembleCmd()
{
	string $cmd = "torus";

	setOptionVars(false);

	int $pivotType = `optionVar -q nurbsTorusPivotType`;
	float $pivotX = `optionVar -q nurbsTorusPivotX`;
	float $pivotY = `optionVar -q nurbsTorusPivotY`;
	float $pivotZ = `optionVar -q nurbsTorusPivotZ`;
	if ($pivotType == 1) {
		$pivotX = 0;
		$pivotY = 0;
		$pivotZ = 0;
	}

	// Axis
	//
	int $axisType = `optionVar -q nurbsTorusAxisType`;
    float $axis[3];
    switch($axisType) {
      case 1:
        $axis[0] = 1;
        $axis[1] = 0;
        $axis[2] = 0;
        break;
      case 2:
        $axis[0] = 0;
        $axis[1] = 1;
        $axis[2] = 0;
        break;
      case 3:
        $axis[0] = 0;
        $axis[1] = 0;
        $axis[2] = 1;
        break;
      case 4:
        $axis[0] = `optionVar -q nurbsTorusAxisX`;
        $axis[1] = `optionVar -q nurbsTorusAxisY`;
        $axis[2] = `optionVar -q nurbsTorusAxisZ`;
        break;
      case 5:
        $axis = `nurbsViewDirectionVector 1`;
        break;
    }

	// History
	//
	int $doHistory = `constructionHistory -q -toggle`;

	float $tol = `optionVar -query nurbsTorusTolerance`;
	if( `optionVar - q nurbsTorusUseGlobalTolerance` ) {
		$tol = `optionVar -q positionalTolerance`;
	}

	$cmd = ($cmd
		+ " -p " + $pivotX + " " + $pivotY + " " + $pivotZ
		+ " -ax " + $axis[0] + " " + $axis[1] + " " + $axis[2]
		+ " -ssw " + `optionVar -query nurbsTorusStartSweep`
		+ " -esw " + `optionVar -query nurbsTorusEndSweep`
		+ " -msw " + `optionVar -query nurbsTorusMinorSweep`
		+ " -r " + `optionVar -query nurbsTorusRadius`
		+ " -hr " + `optionVar -query nurbsTorusHeightRatio`
		+ " -d " + `optionVar -query nurbsTorusDegree`
		+ " -ut " + `optionVar -query nurbsTorusUseTolerance`
		+ " -tol " + $tol
		+ " -s " + `optionVar -query nurbsTorusSections`
		+ " -nsp " + `optionVar -query nurbsTorusSpans`
		+ " -ch " + $doHistory
		+ "; objectMoveCommand");

	if(`optionVar -q createNurbsPrimitiveAsTool`) {
		$cmd = "setToolTo \"CreateNurbsTorusCtx\"";
	}
	
	return $cmd;
}

//
//  Procedure Name:
//      performNurbsTorus
//
//  Description:
//		Perform the torus command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the torus command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performNurbsTorus(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Get the command.
			//
			$cmd = `assembleCmd`;

			//	Execute the command with the option settings.
			//
			evalEcho($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			if(`optionVar -q createNurbsPrimitiveAsTool`) {
				$cmd = `assembleCmd`;
				evalEcho($cmd);
				toolPropertyWindow;
			} else {
				nurbsTorusOptions;
			}
			break;

		//	Return the command string.
		//
		case 2:
			//	Get the command.
			//
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}
