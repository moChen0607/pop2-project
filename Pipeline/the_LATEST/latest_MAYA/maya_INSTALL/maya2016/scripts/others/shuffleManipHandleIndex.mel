// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  17 January 97
//
//
//  Procedure Name:
//		shuffleManipHandleIndex
//
//  Description:
//
//		Allows user to cycle through the handles on
//		the transformation manips, both forwards and
//		backwards.  Passing in 1 indexes up, and -1
//		indexes down.  Could be extended to other 
//		manip types, if they gain the ability to set
//		their active handles.
//
//  Input Arguments:
//		-next
//		-previous
//
//  Return Value:
//      None.
//

global proc shuffleManipHandleIndex( string $shuffle )
{
	global string $gMove, $gRotate, $gScale;
	//
	// Get the current context
	//
	string $currentCtx = `currentCtx`;
	string $cmdString;
	int $increment;

	if( $shuffle == "-next" ) {
		$increment = 1;
	} else {
		$increment = -1;
	}

	if( $currentCtx == $gMove ) {
		if (`superCtx -q $gMove` != "Move") return;
		//
		// Get the currently active handle on
		// the move context
		//
		int $newAH = `manipMoveContext -q -ah Move`;
		$newAH = ( $newAH + $increment );
		if( $newAH == 4 ) {
			//
			// Index is greater than 4, so set
			// it back to 0 to allow cycling
			//
			$newAH = 0;
		}
		if( $newAH == -1 ) {
			//
			// Index is less than 0, so set it
			// back to 3 to allow cycling
			//
			$newAH = 3;
		}
		// Build the command string to execute to change the active
		// manip handle
		//
		$cmdString = ( "manipMoveContext -e -ah "+$newAH+" Move" );
		eval $cmdString;
		return;
	}
	if( $currentCtx == "Rotate" ) {
		if (`superCtx -q $gMove` != "Rotate") return;

		//
		// Get the currently active handle on
		// the rotate context
		//
		int $newAH = `manipRotateContext -q -ah $currentCtx`;
		$newAH = ( $newAH + $increment );
		if( $newAH == 3 ) {
			//
			// Index is greater than 2, so set
			// it back to 0 to allow cycling - note
			// that the screen aligned handle is
			// being filtered out here - set this
			// to 4 to get the screen aligned handle
			// included in the cycling.
			//
			$newAH = 0;
		}
		if( $newAH == -1 ) {
			//
			// Index is less than 0, so set it
			// back to 2 to allow cycling
			//
			$newAH = 2;
		}
		// Build the command string to execute to change the active
		// manip handle
		//
		$cmdString = ( "manipRotateContext -e -ah "+$newAH+" "+$currentCtx );
		eval $cmdString;
		return;
	}
	if( $currentCtx == $gScale ) {
		if (`superCtx -q $gScale` != "Scale") return; 
		//
		// Get the currently active handle on
		// the scale context
		//
		int $newAH = `manipScaleContext -q -ah "Scale"`;
		$newAH = ( $newAH + $increment );
		if( $newAH == 4 ) {
			//
			// Index is greater than 4, so set
			// it back to 0 to allow cycling
			//
			$newAH = 0;
		}
		if( $newAH == -1 ) {
			//
			// Index is less than 0, so set it
			// back to 3 to allow cycling
			//
			$newAH = 3;
		}
		// Build the command string to execute to change the active
		// manip handle
		//
		$cmdString = ( "manipScaleContext -e -ah "+$newAH+" Scale" );
		eval $cmdString;
		return;
	}
}


