// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc addCurvesToHairSystem()
{
	string $hsys[] = getSelectedHairSystems();
	int $numHsys = size( $hsys );
	if( $numHsys < 1 ){
		warning( uiRes("m_doDeleteHairPB.kNoHairSystemSelected"));
		return;
	}

	int $i;
	int $numCurves = 0;
	for( $i = 0; $i < $numHsys; $i++ ){
		string $hsysOutHairGroup = "";
		string $hsysParent[] = `listTransforms $hsys[$i]`;
		if( size($hsysParent) > 0  ){
			$hsysOutHairGroup = ($hsysParent[0] + "OutputCurves");
			if( !objExists( $hsysOutHairGroup ) ){
				$hsysOutHairGroup = `group -em -name $hsysOutHairGroup`;
			}

			string $follicles[] = `listConnections -shapes true ( $hsys[$i] + ".inputHair")`;
			string $follicle;
			for( $follicle in $follicles ){
				if(size(`listConnections ($follicle + ".outCurve")`) > 0) {
					continue;
				}
				string $newCurve = `createNode nurbsCurve`;
				string $curveXform[] =  `listTransforms $newCurve`;
				connectAttr ($follicle + ".outCurve") ($newCurve + ".create");
				string $hairCons[] = `listConnections -plugs true ($follicle + ".outHair")`;
				string $parts[];
				tokenize( $hairCons[0],"[]",$parts );
				if(size($parts[1]) > 0) {
					string $backCons[] = `listConnections -plugs true ($follicle + ".currentPosition")`;
					if(size($backCons) == 0) {
						connectAttr ($hsys[$i] + ".outputHair[" + $parts[1] + "]") ($follicle + ".currentPosition");
					}
				}
				parent -relative $curveXform[0] $hsysOutHairGroup;
			
				$numCurves++;	      
			}
		}		
	}
	if($numCurves == 0){
		warning (uiRes("m_addCurvesToHairSystem.kNoCurvesCreated"));
	}
}
