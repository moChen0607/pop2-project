// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  April 3, 1997
//
//
//  Description:
//      The duplicateSurfacePreset() procedure executes one or more duplicateSurface
//		operations based on the duplicate Surface option vars. In general if
//		you have n surface on surface shapes selected, n  operations would be  
//		carried out.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//

global proc duplicateSurfacePresetArgList( string $version, string $args[] )
//
//	Description :
//		Proc to do one or more duplicateSurface ops depending on number
//		of valid selection items.
//
{
	int $doHistory, $local;

	$doHistory = 	$args[0];
	$local = 		$args[1];

	//---------------------------------------
	// put together a duplicateSurface cmd.
	//---------------------------------------
	//
	string $cmd = "duplicateSurface" +
		" -ch " + $doHistory +
		" -local " + $local +
		" ";

	//----------------------------------------
	// place holders for 1 selection items.
	//----------------------------------------
	//
	int $nitems = 1 ;
	$cmd = appendToCmdPlaceHoldersForSelectionItems($cmd,$nitems) ;
 

	//---------------------------------------------
	// Get the list of nurbs surfaces in select list. 
	//---------------------------------------------
	//
	global int $gSelectNurbsFaces;

	string $surfaceList[] = `filterExpand -ex false -sm $gSelectNurbsFaces`;
	int $surfaceCount = size($surfaceList) ;

	//--------------------------------------------
	// Valid # of items.
	//--------------------------------------------
	//
	if( $surfaceCount < 1 ) {
	   error (uiRes("m_duplicateSurfacePresetArgList.kDupNurbsNoValidObject"));
	}
	else {
		// all n. 
		string $duplicateResults[] ;
		int $i ;

		string $single[1];
		for( $i = 0 ; $i < $surfaceCount ; $i++ ) {
			$single[0] = $surfaceList[$i];
			string $results[] = executeCmdOnItems( $cmd,$single );	
			$duplicateResults = stringArrayCatenate( $duplicateResults,
													 $results );
		}

		// select the results.
		//
		int $resultCount = size($duplicateResults) ;
		if( $resultCount > 0 ) {

			global int $gSelectNurbsSurfacesBit;
			string $selectString;
        	$selectString = "select -r ";

			// select all including DG nodes.
			//
			$resultCount = size($duplicateResults) ;
			for( $i = 0 ; $i < $resultCount ; $i++ ) {
				$selectString +=  $duplicateResults[$i] ;
				$selectString += " ";	
			}
			$selectString += ";";
			select -cl ;
			eval($selectString) ;
		}
	}
}

