// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  Oct, 2000
//
//	Procedure Name:
//		toggleClipOffset
//
//	Description:
//		Make clip absolute or relative
//
//	Input Arguments:
//	$version: The version of this option box.  Used to know how to 
//	interpret the $args array.
//  
//	$args
//	Version 1
//	[0]		$name :  name of the clip window
//	[1]		$op :    type of enable operation:
//						0 = make relative
//						1 = make absolute
//						2 = toggle state
//  Version 2
//  [0]     $op : 	type of enable operation:
//						0 = make relative
//						1 = make absolute
//						2 = toggle state
//  [1]     $selectedClips : true/false, whether to also toggle selected clips
//  [2]		$clip :  single clip to operate upon, if not empty
//
//
global proc
toggleClipOffset( string $version, string $args[] )
{
	int $versionNum = $version;
	int $op;
	int $workOnSelection = 1;
	string $clip;

	if ($versionNum == 1) {
		$op = $args[1];
	} else {
		$op = $args[0];
		$workOnSelection = $args[1];
		if (size($args) > 2) {
			$clip = $args[2];
		}
	}


	string $selClips[];
	if ($workOnSelection) {
		$selClips = getSelectedClips("noOptions");
	}
	if (size($clip) > 0) {
		if (AWNumberOfOccurrencesInStringArray($clip,$selClips) == 0) {
			$selClips[size($selClips)] = $clip;
		}
	}

	int $nclips = size($selClips);
	if ($nclips == 0) {
		if ($op == 0) {
			error((uiRes("m_toggleClipOffset.kSelectClipRelative")));
		} else if ($op == 1) {
			error((uiRes("m_toggleClipOffset.kSelectClipAbsolute")));
		} else {
			error((uiRes("m_toggleClipOffset.kMustSelectAClip")));			
		}
	} else {
		for ($clip in $selClips) {
			string $cmdString;

			string $sched = getClipScheduler($clip);
			int $index = getClipIndex($clip,$sched);
			$cmdString = ("clipSchedule -ci "+$index+" ");
			if ($op == 0) {
				$cmdString += ("-allRelative ");
			} else if ($op == 1) {
				$cmdString += ("-allAbsolute ");
			} else {
				string $absClip = `clipSchedule -ci $index -q -absolute $sched`;
				if ($absClip) {
					$cmdString += ("-allRelative ");
				} else {
					$cmdString += ("-allAbsolute ");
				}
			}
			$cmdString += (" "+$sched);
			evalEcho($cmdString);
		}
	}
	string $printMsg;
	if ($op == 0 || $op == 1) {
		string $operName = ($op == 0) ?
			(uiRes("m_toggleClipOffset.kRelative")):
			(uiRes("m_toggleClipOffset.kAbsolute"));

		$printMsg = (uiRes("m_toggleClipOffset.kSetClips"));
		print(`format -s (size($selClips)) -s $operName $printMsg`);
	} else {
	    $printMsg = (uiRes("m_toggleClipOffset.kToggleRelativeOn"));
		print(`format -s (size($selClips)) $printMsg`);
	}
}
