// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  13 June 2005
//
//<doc>
//<name stringArrayRemoveExact>
//
//<synopsis>
//		string[] stringArrayRemoveExact(string[] $itemsToRemove, string[] $list)
//
//<description>
//		Remove the string items in the first string array from the
//		second string array.  A new string array with the
//		items removed is returned.  The second argument is left
//		unchanged.
//<P>
//		Note that, as opposed to 'stringArrayRemove', for each
//		occurrence of an entry in $item, only one entry from $list
//		will be removed.
//
//<flags>
//		string[] $itemsToRemove	A list of string values to remove from the string array.
//		string[] $list				The array to be acted upon.
//
//<returns>
//		string[] : Original list with all occurrences of the string items
//		removed.  If none of the string items are in the string array then
//		the returned string array is identical to the argument string array.
//
//<examples>
//  string $list[] = { "a", "b", "a", "a", "c" };
//  string $itemsToRemove[] = { "a", "c", "a" };
//  string $diff[] = stringArrayRemoveExact($itemsToRemove, $list);
//  // Result : { b, a } //
//
//</doc>
//

global proc string[] stringArrayRemoveExact( string $itemsToRemove[], 
											 string $list[])
{
	string $results[];
	int $itemsUsed[];
	string $str;
	string $item;
	int $keep;
	int	$resultIndex = 0;
	
	int $i = 0;
	int $numItems = size($itemsToRemove);
	
	// Initialize array to 0s. Once a match is found between
	// $itemsToRemove and $list, the corresponding index in
	// $itemsUsed will be set to 1.
	//
	for ( $i = $numItems; $i > 0; $i-- )
	{
		$itemsUsed[$i-1] = 0;
	}
	
	for ($str in $list) 
	{
		$i = 0;
		$keep = 1;
		for ( $i = 0; $i < $numItems; $i++ )
		{
			if ( !$itemsUsed[$i] &&
				 $itemsToRemove[$i] == $str )
			{
				$itemsUsed[$i] = 1;
				$keep = 0;
				break;
			}
		}
		
		if ($keep) 
		{
			// No match, we can add $str.
			//
			$results[$resultIndex++] = $str;
		}
	}
	
	return $results;
}


