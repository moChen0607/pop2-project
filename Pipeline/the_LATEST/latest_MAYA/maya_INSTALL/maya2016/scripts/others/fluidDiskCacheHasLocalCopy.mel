// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  21 Nov 2001
//
//  Description:
//		Editing of fluid disk caches requires a local
//		copy of the cache file to be stored in the 
//		the directory specified by the "diskCache -tmp"
//		command.  For performance reasons during playback
//		or rendering, you may wish to turn off creation 
//		of these local copies by setting the ".cacheLocally"  
//		attribute on the fluid's diskCache node to "false."
//		Note that operations like Truncate and Append will
//		fail for caches that do not have the "cacheLocally"
//		attribute enabled.
//		
global proc int fluidDiskCacheHasLocalCopy()
{
	int    $hasLocal = true;
	int    $localFound = true;
	string $diskCachePath;
	string $cache;

	if( !`exists getActiveFluidShapes` ) {
		source "getFluidShape.mel";
	}

	// For all active fluids...
	//
	string $fluidShapes[] = `getActiveFluidShapes`;	
	for( $f in $fluidShapes ) {
		$cache = fluidPlaybackCacheName( $f );

		// If there's one cache...
		//
		if( size( $cache ) ) {
			// And it has the copyLocally attr...
			//
			string $attr[] = `ls ($cache + ".copyLocally")`;
			if( size( $attr ) ) {
				// And the copyLocally attr is set to false...
				//
				$hasLocal = `getAttr ($cache + ".copyLocally")`;
				if( $hasLocal ) {
					// Make sure the local copy exists...
					//
					verifyWorkspaceFileRule( "diskCache", "data" );
					string $diskCacheDir = ( `workspace -q -rootDirectory` + 
											 `workspace -fileRuleEntry "diskCache"` );
					
					string $hiddenName   = `getAttr ($cache + ".hiddenCacheName")`;
					$diskCachePath = $diskCacheDir + "/" + $hiddenName;
					$localFound = `file -q -exists $diskCachePath`;
				} else {
					// Stop processing, can't edit, no local copy...
					//
					break;
				}
			}
		}
	}

	if( !$hasLocal ) {
		if( !$localFound ) {
			string $fmt = (uiRes("m_fluidDiskCacheHasLocalCopy.kNoLocalDiskCache"));
			error(`format -s $diskCachePath $fmt`);
		} else {
			string $fmt = (uiRes("m_fluidDiskCacheHasLocalCopy.kCannotEditDiskCache"));
			error(`format -s $cache $fmt`);
		}
	}

	return $hasLocal;
}
