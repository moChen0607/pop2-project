// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  18 April 1997
//
//  Description:
//      squash default options box.  The content for each tab 
//		is not created until it is accessed for the first time.
//
//////////////////////////////////////////////////////////////////////
//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
proc setOptionVars(int $forceFactorySettings)
{
	//	min.
	//
	if ($forceFactorySettings || !`optionVar -exists squashMin`) {
		optionVar -floatValue  squashMin -1.0;
	}

	//	max.
	//
	if ($forceFactorySettings || !`optionVar -exists squashMax`) {
		optionVar -floatValue  squashMax 1.0;
	}

	//	start smoothness.
	//
	if ($forceFactorySettings || !`optionVar -exists squashStartSmoothness`) {
		optionVar -floatValue  squashStartSmoothness 0.0;
	}

	//	end smoothness.
	//
	if ($forceFactorySettings || !`optionVar -exists squashEndSmoothness`) {
		optionVar -floatValue  squashEndSmoothness 0.0;
	}

	//	maxExpandPos.
	//
	if ($forceFactorySettings || !`optionVar -exists squashMaxExpandPos`) {
		optionVar -floatValue  squashMaxExpandPos 0.5;
	}

	//	expand.
	//
	if ($forceFactorySettings || !`optionVar -exists squashExpand`) {
		optionVar -floatValue  squashExpand 1.0;
	}

	//	factor.
	//
	if ($forceFactorySettings || !`optionVar -exists squashFactor`) {
		optionVar -floatValue  squashFactor 0.0;
	}

	// default options common to all deformers
	//
	if ($forceFactorySettings || !`optionVar -exists squashPositioning`) {
		optionVar -stringValue squashPositioning "default";
	}
	if ($forceFactorySettings || !`optionVar -exists squashExclusive`) {
		optionVar -intValue squashExclusive 0;
	}
	if ($forceFactorySettings || !`optionVar -exists squashExclName`) {
		// 0 == no exclusive
		// 1 == exclusive with new name
		// 2 == exclusive using an existing partition
		//
		optionVar -stringValue squashExclName "deformPartition";
	}
}

//
//  Procedure Name:
//      squashSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//		forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//      tabIndex		     - 0 = both tabs, 1 = basic, 2 = advanced
//
//  Return Value:
//      None.
//
global proc squashSetup(string $parent,
						int $forceFactorySettings,
						int $tabIndex)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);
	setParent $parent;

	//	Query the optionVar's and set the values into the controls.
	//
	if ($tabIndex != 2) {		
		//	min.
		//
		if (`floatSliderGrp -exists squashMin`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashMin`
				squashMin;
		}
		
		//	max.
		//
		if (`floatSliderGrp -exists squashMax`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashMax`
				squashMax;
		}
		
		//	start smoothness.
		//
		if (`floatSliderGrp -exists squashStartSmoothness`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashStartSmoothness`
				squashStartSmoothness;
		}
		
		//	end smoothness.
		//
		if (`floatSliderGrp -exists squashEndSmoothness`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashEndSmoothness`
				squashEndSmoothness;
		}
		
		//	maxExpandPos.
		//
		if (`floatSliderGrp -exists squashMaxExpandPos`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashMaxExpandPos`
				squashMaxExpandPos;
		}
		
		//	expand.
		//
		if (`floatSliderGrp -exists squashExpand`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashExpand`
				squashExpand;
		}
		
		//	factor.
		//
		if (`floatSliderGrp -exists squashFactor`) {
			floatSliderGrp -edit 
				-value `optionVar -query squashFactor`
				squashFactor;
		}
	}

	if ($tabIndex != 1) {
		// Positioning of the squash deformer in the DG
		//
		string $positioning = `optionVar -query squashPositioning`;
		if (`optionMenuGrp -exists squashPositioningWidget`) {
			if ($positioning == "default") {
				optionMenuGrp -edit -select 1 squashPositioningWidget;
			}
			else if ($positioning == "before") {
				optionMenuGrp -edit -select 2 squashPositioningWidget;
			}
			else if ($positioning == "after") {
				optionMenuGrp -edit -select 3 squashPositioningWidget;
			}
			else if ($positioning == "split") {
				optionMenuGrp -edit -select 4 squashPositioningWidget;
			}
			else if ($positioning == "parallel") {
				optionMenuGrp -edit -select 5 squashPositioningWidget;
			}
			else {
				optionMenuGrp -edit -select 1 squashPositioningWidget;
			}
		}
		
		int $exc=`optionVar -query squashExclusive`;
		if (`checkBoxGrp -exists exclWidget`) {
			checkBoxGrp -e -v1 $exc exclWidget;
		}
		string $exn=`optionVar -query squashExclName`;
		if (`textFieldGrp -exists partitionNameWidget`) {
			textFieldGrp -e -tx $exn -enable $exc partitionNameWidget;
		}
		if (`optionMenuGrp -exists partitionListWidget`) {
			optionMenuGrp -e  -enable $exc partitionListWidget;
		}
	}
}

//
//  Procedure Name:
//      squashCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//		doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc squashCallback(string $parent, int $doIt)
{
	//	Set the optionVar's from the control values, and then
	//	perform the command.

	//	Min.
	//
	if (`floatSliderGrp -exists squashMin`) {
		optionVar -floatValue squashMin		
			`floatSliderGrp -query -value squashMin`;
	}

	//	Max.
	//
	if (`floatSliderGrp -exists squashMax`) {
		optionVar -floatValue squashMax		
			`floatSliderGrp -query -value squashMax`;
	}

	//	start Smoothness.
	//
	if (`floatSliderGrp -exists squashStartSmoothness`) {
		optionVar -floatValue squashStartSmoothness		
			`floatSliderGrp -query -value squashStartSmoothness`;
	}

	//	end Smoothness.
	//
	if (`floatSliderGrp -exists squashEndSmoothness`) {
		optionVar -floatValue squashEndSmoothness		
			`floatSliderGrp -query -value squashEndSmoothness`;
	}

	//	MaxExpandPos.
	//
	if (`floatSliderGrp -exists squashMaxExpandPos`) {
		optionVar -floatValue squashMaxExpandPos		
			`floatSliderGrp -query -value squashMaxExpandPos`;
	}

	//	Expand.
	//
	if (`floatSliderGrp -exists squashExpand`) {
		optionVar -floatValue squashExpand		
			`floatSliderGrp -query -value squashExpand`;
	}

	//	Factor.
	//
	if (`floatSliderGrp -exists squashFactor`) {
		optionVar -floatValue squashFactor		
			`floatSliderGrp -query -value squashFactor`;
	}

	// Positioning of the squash deformer in the DG
	//
	string $positioning = "default";
	if (`optionMenuGrp -exists squashPositioningWidget`) {
		if (`optionMenuGrp -query -select squashPositioningWidget` == 1) {
			$positioning = "default";
		}
		else if (`optionMenuGrp -query -select squashPositioningWidget` == 2){
			$positioning = "before";
		}
		else if (`optionMenuGrp -query -select squashPositioningWidget` == 3){
			$positioning = "after";
		}
		else if (`optionMenuGrp -query -select squashPositioningWidget` == 4){
			$positioning = "split";
		}
		else if (`optionMenuGrp -query -select squashPositioningWidget` == 5){
			$positioning = "parallel";
		}
	}
	optionVar -stringValue squashPositioning $positioning;

	if (`checkBoxGrp -exists exclWidget`) {
		optionVar -intValue squashExclusive `checkBoxGrp -q -v1 exclWidget`;
	}
	if (`optionMenuGrp -exists partitionListWidget`) {
		string $partitionNameVal = `optionMenuGrp -q -v partitionListWidget`;
		if ($partitionNameVal == (uiRes("m_performSquash.kCreateNewPartition"))) {
			if (`textFieldGrp -exists partitionNameWidget`) {
				$partitionNameVal = `textFieldGrp -q -tx partitionNameWidget`;
			}
		} else {
			// a value of 2 indicates that we use an existing partition
			//
			optionVar -intValue squashExclusive 2;
		}
		
		optionVar -stringValue squashExclName $partitionNameVal;
	}
	
	if ($doIt) {
		performSquash 0; 
		addToRecentCommandQueue "performSquash 0" "Squash";
	}
}


//
//  Procedure Name:
//      createSquashTabUI
//
//  Description:
//		Create the tab UI.  The contents of each tab are created only 
//		when it is required, ie. if the tab is initially visible or 
//		if the tab is selected by the user.
//
//  Input Arguments:
//      The name of the tab layout.
//
//  Return Value:
//      None.
//
global proc createSquashTabUI(string $tabLayout)
{
	string $tab[] = `tabLayout -query -childArray $tabLayout`;
	int $currentTabIndex = `tabLayout -query -selectTabIndex $tabLayout`;

	//	Determine if the UI for this tab has been created yet.
	//	This is accomplished by querying the number of children
	//	in the current tab.  If the tab has no children then the UI
	//	must be created.
	//
	if (0 == `columnLayout -query -numberOfChildren $tab[$currentTabIndex-1]`) {

		setParent $tab[$currentTabIndex-1];

		string $label;
		int $index;

		//	Activate the default UI template so that the layout of this 
		//	option box is consistent with the layout of the rest of the 
		//	application.
		//
		setUITemplate -pushTemplate DefaultTemplate;

		//	Turn on the wait cursor.
		//
		waitCursor -state 1;

		//	The current tab has no children.  Determine which tab is
		//	active and create its UI.
		//
		//	RECOMMENDATION:  Use the 'Grp' commands where possible because
		//	they obey the formatting specified in the default template.
		//	This will result in a more consistent look throughout the
		//	application.
		//	
		if (1 == $currentTabIndex) {

	    	//	Create UI for the first tab.
	    	//
	    	floatSliderGrp -label (uiRes("m_performSquash.kLowBound"))  -fieldMinValue -10000.0
			  -minValue -10.0 -maxValue 0.0 squashMin;

	    	floatSliderGrp -label (uiRes("m_performSquash.kHighBound"))  -minValue 0.0 
			  -maxValue 10.0 -fieldMaxValue 10000.0 squashMax;

	    	floatSliderGrp -label (uiRes("m_performSquash.kStartSmoothness"))  -minValue 0.0 -maxValue 1.0 -step .01
	    		squashStartSmoothness;

	    	floatSliderGrp -label (uiRes("m_performSquash.kEndSmoothness"))  -minValue 0.0 -maxValue 1.0 -step .01
	    		squashEndSmoothness;

	    	floatSliderGrp -label (uiRes("m_performSquash.kMaxExpandPosition"))  -minValue 0.01 -maxValue 0.99 -step .01
	    		squashMaxExpandPos;

	    	floatSliderGrp -label (uiRes("m_performSquash.kExpand"))  -minValue 0.0 -maxValue 1.7 -step .01
				-fieldMaxValue 10000.0
	    		squashExpand;

	    	floatSliderGrp -label (uiRes("m_performSquash.kFactor"))  -minValue -10.0 -maxValue 10.0 
				-fieldMinValue -10000.0 -fieldMaxValue 10000.0
	    		squashFactor;

			setParent ..;
		} else if (2 == $currentTabIndex) {
			
			//	Create UI for the second tab.
			//
			columnLayout -adjustableColumn true;

			// Positioning of the squash deformer in the DG
			optionMenuGrp -label (uiRes("m_performSquash.kDeformationOrder"))  squashPositioningWidget;
			menuItem -label (uiRes("m_performSquash.kDefault"))        squashPosItem1;
			menuItem -label (uiRes("m_performSquash.kBefore"))         squashPosItem2;
			menuItem -label (uiRes("m_performSquash.kAfter"))          squashPosItem3;
			menuItem -label (uiRes("m_performSquash.kSplit"))          squashPosItem4;
			menuItem -label (uiRes("m_performSquash.kParallel"))       squashPosItem5;

			separator;
			checkBoxGrp	
				-numberOfCheckBoxes 1
				-label ""
				-label1 (uiRes("m_performSquash.kExclusive")) 
				-v1 0
				-on1 "optionMenuGrp -e -enable 1 partitionListWidget;textFieldGrp -e -enable 1 partitionNameWidget"
				-offCommand "optionMenuGrp -e -enable 0 partitionListWidget;textFieldGrp -e -enable 0 partitionNameWidget"
				exclWidget;

			// Create an option menu listing the partitions
			//
			optionMenuGrp -label (uiRes("m_performSquash.kPartitiontoUse"))  -enable `checkBoxGrp -q -v1 exclWidget`
				-cc "updatePartitionNameWidget" partitionListWidget;

			string $currentNameOption = "";
			if (`optionVar -exists squashExclName`) {
				$currentNameOption = `optionVar -q squashExclName`;
			}
			
			// add all the partitions to the menu
			//
			int $pp;
			string $partitionArray[];
			$partitionArray = `ls -type partition`;
			int $partitionCount = size($partitionArray);
			menuItem -label (uiRes("m_performSquash.kCreateNewPartition")) ;
			for ($pp = 0; $pp < $partitionCount; $pp++)
			{
				// Do not list the render partition as adding items to
				// it is only going to cause confusion.
				//
				if ($partitionArray[$pp] != "renderPartition" && 
					$partitionArray[$pp] != "characterPartition") {
					menuItem -label $partitionArray[$pp];
				}
				if ($currentNameOption == $partitionArray[$pp]) {
					optionVar -stringValue squashExclName "deformPartition";
				}
			}

			textFieldGrp -label (uiRes("m_performSquash.kNewPartitionName"))  -enable `checkBoxGrp -q -v1 exclWidget` 
				-tx "deformPartition"
				partitionNameWidget;
		
			updatePartitionNameWidget;
		}

		//	Update the control values to match the options.
		//
		eval (("squashSetup " + $tabLayout + " "+0+" "+$currentTabIndex));
	
		//	Turn off the wait cursor.
		//
		waitCursor -state 0;
		
		//	Deactivate the default UI template.
		//
		setUITemplate -popTemplate;
	}
}

//
//  Procedure Name:
//      squashOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
// ********* Change 'squash' in this proc to be the name of your command
proc squashOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "nonLinear";

	//	Build the option box actions.
	//
	string $callback = ("squashCallback");
	string $setup = ("squashSetup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	//  The value returned is the name of the layout to be used as
	//	the parent for the option box UI.
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	//	Any default option box behaviour based on the command name is set 
	//	up with this call.  For example, updating the 'Help' menu item with
	//	the name of the command.
	//
	setOptionBoxCommandName($commandName);

	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	//	Activate the default UI template so that the layout of this 
	//	option box is consistent with the layout of the rest of the 
	//	application.
	//
	//	Note: this option box example delays the creation of the UI
	//	until it's required.  Therefore this step is moved to the
	//	procedure where the UI is actually created. 
	//
	//setUITemplate -pushTemplate DefaultTemplate;

	//	STEP 4: Create option box contents.
	//	===================================
	//	
	//	This, of course, will vary from option box to option box.	
	
	//	Demonstrate the delaying of UI creation via tab layouts.
	//	Instead of creating all of the option box UI initially, only
	//	create that which is initially visible.  Wait, until the 
	//	other tabs are selected to create the remaining UI.
	//
	string $tabLayout = `tabLayout -scrollable 1`;

	//	Attach an action that will be invoked before a tab is selected.
	//	
	tabLayout -edit 
		-preSelectCommand ("createSquashTabUI " + $tabLayout)
		$tabLayout;

	//	Create just the immediate children of the tab layout so that
	//	the tabs appear.
	//
	columnLayout;
		setParent ..;
	columnLayout;
		setParent ..;
	
	//	Set the tab labels.
	//
	tabLayout -edit
		-tabLabelIndex 1 (uiRes("m_performSquash.kBasic"))
		-tabLabelIndex 2 (uiRes("m_performSquash.kAdvanced"))
		$tabLayout;

	//	Create the UI for the tab that is initially visible.
	//
	createSquashTabUI($tabLayout);

	//	Step 5: Deactivate the default UI template.
	//  ===========================================
	//
	//	Note: this option box example delays the creation of the UI
	//	until it's required.  Therefore this step is moved to the
	//	procedure where the UI is actually created.
	//
	//	See also Step 2. 
	//
	//setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//
	//	Provide more descriptive labels for the buttons.  This is not 
	//	necessary, but in some cases, for example, a button labelled 
	//	'Create' may be more meaningful to the user than one labelled
	//	'Apply'.
	//
	//  Disable those buttons that are not applicable to the option box.
	//
	//	Attach actions to those buttons that are applicable to the option
	//	box.  Note that the 'Close' button has a default action attached 
	//	to it that will hide the window.  If a a custom action is
	//	attached to the 'Close' button then be sure to call the 'hide the
	//	option box' procedure within the custom action so that the option
	//	box is hidden properly.

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performSquash.kCreate")) 
		-command ($callback + " " + $tabLayout + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $tabLayout + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $tabLayout + " " + 1 + " " + 0)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle (uiRes("m_performSquash.kCreateSquashDeformerOptions"));

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "Squash" );

	//	Step 9: Set the current values of the option box.
	//	=================================================
	//
	//	NOTE:  Can not do this here since we do not know what UI is
	//	currently visible.  This is moved to where the UI is created.
	//
	//eval (($setup + " " + $tabLayout + " " + 0));	
	
	//	Step 10: Show the option box.
	//	=============================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      optionBoxExample1Help
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string squashHelp()
{
	return 
	"  Command: squash - creates a squash\n" +
	"Selection: Deformable geometry.";	
}

proc string getCmd()
{
	string $cmd = "nonLinear -type squash ";

	$cmd += " -lowBound " + `optionVar -query squashMin`;
	$cmd += " -highBound " + `optionVar -query squashMax`;
	$cmd += " -startSmoothness " + `optionVar -query squashStartSmoothness`;
	$cmd += " -endSmoothness " + `optionVar -query squashEndSmoothness`;
	$cmd += " -maxExpandPos " + `optionVar -query squashMaxExpandPos`;
	$cmd += " -expand " + `optionVar -query squashExpand`;
	$cmd += " -factor " + `optionVar -query squashFactor`;

 	// Build a positioning flag if needed
	//
	string $positioning = `optionVar -query squashPositioning`;
	if ($positioning != "default") {
		$cmd += (" -" + $positioning);
	}
	
	int $exc=`optionVar -query squashExclusive`;
	if ($exc) {
		string $exn=`optionVar -query squashExclName`;
		if ($exn!="") {
			// make sure that we do not clash names with an existing
			// partition when the user requested a new partition, even
			// if the user may have entered a non-unique name
			//
			if ($exc == 1) 	$exn += "#";
			$cmd += (" -exclusive \"" + $exn+"\"");
		}
	}
	
	return $cmd;
}


//
//  Procedure Name:
//      performSquash
//
//  Description:
//		Perform the squash command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the squash command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performSquash(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command.
		//
		case 0:
			//	Retrieve the option settings
			//
			setOptionVars(false);

			//	Get the command.
			//
			$cmd = getCmd();

			//	Execute the command with the option settings.
			//
			evalEcho($cmd);

			break;

		//	Show the option box.
		//
		case 1:
			squashOptions;
			break;

		//	Return the command string.
		//
		case 2:
			//	Retrieve the option settings.
			//
			setOptionVars (false);

			//	Get the command.
			//
			$cmd = getCmd();
			break;
	}
	return $cmd;
}

