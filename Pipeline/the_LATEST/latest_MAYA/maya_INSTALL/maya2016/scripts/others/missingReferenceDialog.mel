// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc missingReferenceDialogDismissCmd( string $baseString )
{
	string $dismissString;
	if ( `optionVar -q missingReferenceDialogRememberSettingVar` )
	{
		$dismissString = "Always" + $baseString;
	}
	else
	{
		$dismissString = $baseString;
	}
	optionVar -remove missingReferenceDialogRememberSettingVar;
	
	layoutDialog -dismiss $dismissString;
}

global proc createMissingReferenceDialog( string $filename)
{
	// Get the layoutDialog's formLayout.
	//
	string $form = `setParent -q`;

	// Force the formLayout width since the layoutDialog
	// is not resizable.
	//
	formLayout -e -width 400 $form;
	
	string $msg = (uiRes("m_missingReferenceDialog.kFileNotFound"));
	string $text = `format -s $filename $msg`;
	string $abortText = (uiRes("m_missingReferenceDialog.kIgnoreReference"));
	string $skipText = (uiRes("m_missingReferenceDialog.kSkip"));
	string $browseText = (uiRes("m_missingReferenceDialog.kBrowse"));
	string $retryText = (uiRes("m_missingReferenceDialog.kRetry"));
	string $bakeText = (uiRes("m_missingReferenceDialog.kBakePathChanges"));
	string $rememberText = (uiRes("m_missingReferenceDialog.kRememberSettings"));

	optionVar -iv missingReferenceDialogRememberSettingVar 0;
	if ( !`optionVar -exists missingReferenceDialogBakeChanges` )
	{
		optionVar -iv missingReferenceDialogBakeChanges 0;
	}
	int $initialBakeChangesValue = `optionVar -q missingReferenceDialogBakeChanges`;
	
	string $t = `text -l $text`;
	string $abortButton = `button -l $abortText -c "missingReferenceDialogDismissCmd \"Abort\""`;
	string $bakeButton = `button -l $skipText -c "missingReferenceDialogDismissCmd \"Skip\""`;
	string $browseButton = `button -l $browseText -c "missingReferenceDialogDismissCmd \"Browse\""`;
	string $retryButton = `button -l $retryText -c "missingReferenceDialogDismissCmd \"Retry\""`;
	string $bakeCheckBox = `checkBox -l $bakeText -v $initialBakeChangesValue`;
	string $rememberCheckBox = `checkBox -l $rememberText -v false`;
	
	checkBox -e
		-onCommand	"optionVar -iv missingReferenceDialogBakeChanges 1"
		-offCommand	"optionVar -iv missingReferenceDialogBakeChanges 0"
		$bakeCheckBox;

	checkBox -e
		-onCommand	"optionVar -iv missingReferenceDialogRememberSettingVar 1"
		-offCommand	"optionVar -iv missingReferenceDialogRememberSettingVar 0"
		$rememberCheckBox;

	int $spacer = 5;
	int $top = 5;
	int $edge = 5;

	formLayout -edit
		-attachForm		$t					"top"		$top
		-attachForm		$t					"left"		$edge
		-attachNone		$t					"bottom"
		-attachForm		$t					"right"		$edge

		-attachControl	$abortButton		"top"		$spacer $t
		-attachForm		$abortButton		"left"		$edge 
		-attachNone		$abortButton		"bottom" 
		-attachPosition	$abortButton		"right"		$spacer 25

		-attachControl	$bakeButton			"top"		$spacer $t
		-attachPosition	$bakeButton			"left"		$spacer 25
		-attachNone		$bakeButton			"bottom"
		-attachPosition	$bakeButton			"right"		$spacer 50

		-attachControl	$browseButton		"top"		$spacer $t
		-attachPosition	$browseButton		"left"		$spacer 50
		-attachNone		$browseButton		"bottom"
		-attachPosition	$browseButton		"right"		$spacer 75
		
		-attachControl	$retryButton		"top"		$spacer $t
		-attachPosition	$retryButton	"left"		$spacer 75
		-attachNone		$retryButton		"bottom"
		-attachForm		$retryButton	"right"		$edge
		
		-attachControl	$bakeCheckBox		"top"		$spacer $retryButton
		-attachForm		$bakeCheckBox		"left"		$edge
		-attachNone		$bakeCheckBox		"bottom"
		-attachNone		$bakeCheckBox		"right"

		-attachControl	$rememberCheckBox	"top"		$spacer $bakeCheckBox
		-attachForm		$rememberCheckBox	"left"		$edge
		-attachForm		$rememberCheckBox	"bottom"	$spacer
		-attachNone		$rememberCheckBox	"right"
	$form;
}



global proc string missingReferenceDialog( string $filename )
{
	return `layoutDialog -title (uiRes("m_missingReferenceDialog.kRefFileNotFound")) -ui ("createMissingReferenceDialog(\"" + $filename + "\")")`;
}
