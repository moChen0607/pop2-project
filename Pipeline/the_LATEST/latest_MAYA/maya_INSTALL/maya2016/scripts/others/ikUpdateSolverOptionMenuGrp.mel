// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	File:		ikUpdateSolverOptionMenuGrp.mel
//
//
//	Description:
//
//		Procedure that updates a given optionMenuGrp field
//	with the list of available solvers. This list excludes
//	the "ikSplineSolver" which is a unique case that is
//	handled separately.
//

global proc ikUpdateSolverOptionMenuGrp( string $optMenuGrp )
{
	// FIREWALL: The $optMenuGrp must be an optionMenuGrp object.
	//
	if( !`optionMenuGrp -q -ex $optMenuGrp` )
	{
		return;
	}

	// Check for new ikSolver nodes in the system and add them to
	// the optionMenuGrp if necessary.
	//
	string $solverNodes[] = `ikSystem -q -ls`;
	string $solverItems[] = `optionMenuGrp -q -ils $optMenuGrp`;

	int $nSolverNodes = size($solverNodes);
	int $nSolverItems = size($solverItems);

	// Set the current menu parent (make sure we retrieve
	// the fully qualified name)
	//
	string $currentParent = `setParent -q`;
	string $currentMenuParent = `setParent -q -m`;
	setParent $optMenuGrp;
	string $menuPath = `setParent -q`;
	string $optMenuPath = $menuPath + "|OptionMenu";
	setParent -m $optMenuPath;

	string $nodeValue;
	string $menuItemValue;
	int $itemNum = $nSolverItems;
	int $i;
	for( $i = 0; $i < $nSolverNodes; $i++ )
	{
		$nodeValue = $solverNodes[$i];

		// Omit spline solvers
		//
		if( $nodeValue == "ikSplineSolver" ) continue;

		int $j;
		int $foundNode = 0;
		for( $j = 0; $j < $nSolverItems; $j++ )
		{
			$menuItemValue = ikSolverUnlocalize(`menuItem -q -l $solverItems[$j]`);
			if( $menuItemValue == $nodeValue )
			{
				$foundNode = 1;
			}
		}

		if( !$foundNode )
		{
			// Create new menu item
			//
			string $item = `menuItem -l $nodeValue ($nodeValue + $itemNum)`;
			$itemNum++;
		}
	}
	setParent $currentParent;
	setParent -m $currentMenuParent;
}
