// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


//
//
//  Creation Date:  October 1998
//
//  Description:
//      dynExecuteCollisionCommand executes the collision command 
//
//  Input Arguments():
//		string $collisionCmd -- the collision command string (except for the selection)
//
//  Return Value:
//      None.
//
//
//  ============== dynExecuteCollisionCommand ==============
//
//  SYNOPSIS
//		Execute the collision command.
//		The last item in the selection list is the collision, and all
//		other items collide with it.
//
global proc	dynExecuteCollisionCommand(string $collisionCmd)
{
	string $fullCmd;
	int $numParticleObjs;
	int $i;

	// Get the selection list.
	//
	string $selected[] = `ls -sl`;
	int    $selCount = size($selected);
	int    $connCount = $selCount - 1;

	// Make the last object in the list a collider.
	//
	string $collider = $selected[ $selCount-1 ];
	$collisionCmd = $collisionCmd + " " + $collider;
	evalEcho( $collisionCmd );

	// See if the very last object in the list was a particle shape.
	// If that's the case, and we got this far, then it must be a soft
	// object, because if it were anything else then the collision 
	// command would have failed and we would not be here.
	// In this case, get the geomtry which immediately precedes
	// the particle shape in the list.
	//
	if ((`particleExists $collider`) && ($selCount > 1))
	{
		$collider = $selected[ $selCount-2 ];
		$connCount = $selCount - 2;
	}

	// Now connect all the other objects in the list to it.
	//
    string $connectNames = "";
    for ($i = 0; $i < $connCount; $i++)
    {
		string $objName = $selected[$i];
        if (size($objName)>0)
        	$connectNames  = $connectNames + " " + $objName;
    }

    if (size($connectNames) > 0)
    {
		string $connectCmd =
			"connectDynamic -c " + $collider + " " + $connectNames;
        evalEcho($connectCmd);
    }
}

