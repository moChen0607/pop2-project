// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

global proc polyLaunchPaintReduceTool() 
//
// Description:
// A convenience procedure for launching the paint reduce weights tool.
//
{

	// Sometimes when the paint tool is launched from the Edit Polygons menu
	// the artAttrCreateMenuItems.mel file has not been sourced. Do it now
	// to make sure we execut artSetToolAndSelectAttr.
	//
	if (`whatIs artSetToolAndSelectAttr` == "Unknown" ) 
	{ 
		source artAttrCreateMenuItems.mel; 
	} 

	string $selectWarn = (uiRes("m_polyLaunchPaintReduceTool.kReducePaintNoNodeWarning"));

	// Check selection list validity
	//
	string $sel[] = `ls -sl`;
	if ( size( $sel ) == 0 ) {
		warning $selectWarn;
		return;
	}

	if ( size( $sel ) > 1 ) {
		warning((uiRes("m_polyLaunchPaintReduceTool.kReducePaintTooManyWarning")));
	}

	// Try to find a reduce node. Look downstream first. 
	//
	string $nodes[] = `listHistory -future true $sel[0]`;
	string $node = "";
	string $target = "";
	for ($node in $nodes) {
		if ( "polyReduce" == `nodeType $node` ) {
			$target = $node;
			break;
		}
	}

	// If no reduce node is found downstream, try upstream.
	//
	if ( "" == $target ) {
		$nodes = `listHistory -future false $sel[0]`;
		for ($node in $nodes) {
			if ( "polyReduce" == `nodeType $node` ) {
				$target = $node;
				break;
			}
		}
	}
	

	// If we still haven't found a reduce node, then the selection isn't
	// paintable.
	//
	if ( "" == $target ) {
		warning $selectWarn;
		return;
	}	

	artSetToolAndSelectAttr( "artAttrCtx", "polyReduce." + $target + ".vertexWeights" );

}
