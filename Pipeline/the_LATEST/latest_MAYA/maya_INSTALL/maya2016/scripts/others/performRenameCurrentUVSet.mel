// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


proc setOptionVars (int $forceFactorySettings)
{			   
	if ($forceFactorySettings || !`optionVar -exists polyRenameUVSet`)
		optionVar -stringValue polyRenameUVSet "";
}

global proc performRenameCurrentUVSetSetup (string $parent, 
										 int $forceFactorySettings)
{
	setOptionVars($forceFactorySettings);
	setParent $parent;

	string $sval;
	$sval = `optionVar -query polyRenameUVSet`;
	textFieldGrp -edit -tx $sval polyRenameUVSet;
}

global proc performRenameCurrentUVSetCallback (string $parent, int $doIt)
{
	setParent $parent;

	optionVar -stringValue polyRenameUVSet
		`textFieldGrp -query -tx polyRenameUVSet`;

	if ($doIt) {
		performRenameCurrentUVSet 0;
		addToRecentCommandQueue "performRenameCurrentUVSet 0" "RenameCurrentUVSet";
	}
}

proc polyRenameCurrentUVSetOptions()
{
	// Global template variables for form spacing
	global int $gOptionBoxTemplateFrameSpacing;
	
	string $commandName = "performRenameCurrentUVSet";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");
	   
	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate OptionBoxTemplate;
	waitCursor -state 1;

	// Form layout
	string $parent = `formLayout polyRenameCurrentUVSetOptions`;
	
		// Settings frame
		string $settingsFrame = 
		`frameLayout -label (uiRes("m_performRenameCurrentUVSet.kSettingsFrame"))`;

			columnLayout;
	
    			textFieldGrp -label (uiRes("m_performRenameCurrentUVSet.kNewUVSetName")) polyRenameUVSet;

			setParent ..; // columnLayout
			
		setParent $parent; // frameLayout;
		
	setParent ..; // formLayout;

	// Attach Description/Settings frames to form layout
	formLayout -e
		-af $settingsFrame "top" $gOptionBoxTemplateFrameSpacing
		-af $settingsFrame "left" $gOptionBoxTemplateFrameSpacing
		-af $settingsFrame "right" $gOptionBoxTemplateFrameSpacing
		-an $settingsFrame "bottom"
	$parent;

	setParent $parent;

	waitCursor -state 0;
	setUITemplate -popTemplate;
	   
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performRenameCurrentUVSet.kRenameCurrent"))
	       -command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;
			 
	setOptionBoxTitle (uiRes("m_performRenameCurrentUVSet.kRenameCurrentUVSetOptions"));

	setOptionBoxHelpTag( "RenameCurrentUVSet" );

	eval (($setup + " " + $parent + " " + 0));      
	showOptionBox();
}

global proc string performRenameCurrentUVSet (int $option)
{
	string $cmd="polyUVSet -rename -newUVSet ";
	switch ($option) 
	{
	  case 1: polyRenameCurrentUVSetOptions; 
		// Just the option box
		break;
		
	  default:
		setOptionVars(false);
		string $name = `optionVar -query polyRenameUVSet`;

		if (size($name))
		{
			$cmd += ("\"" + $name + "\"");
			if ($option == 0)
				evalEcho $cmd;
		}
		else
		{
			warning (uiRes("m_performRenameCurrentUVSet.kNoName"));
		}
		break;
	}
	return $cmd;
}


