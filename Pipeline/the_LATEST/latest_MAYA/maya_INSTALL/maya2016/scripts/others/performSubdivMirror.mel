// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  12 January, 1999
//
//
//  Description:
//		Option box for subdivision surfaces mirror geometry
//

//
//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//  Return Value:
//      None.
//
//  Note:
//     Commented out Type option for this rev
//
proc setOptionVars(int $forceFactorySettings)
{
	if ($forceFactorySettings ||
		!`optionVar -exists subdivMirrorX`) {
		optionVar -intValue subdivMirrorX 0;
	}

	if ($forceFactorySettings ||
		!`optionVar -exists subdivMirrorY`) {
		optionVar -intValue subdivMirrorY 0;
	}

	if ($forceFactorySettings ||
		!`optionVar -exists subdivMirrorZ`) {
		optionVar -intValue subdivMirrorZ 0;
	}

	if ($forceFactorySettings ||
		!`optionVar -exists subdivMirrorOrigObject`) {
		optionVar -intValue subdivMirrorOrigObject 3;
	}
	// Have a special var for the history; we'll leave it
	// of for now, but this is a back door for setting
	// it back on.
	if ($forceFactorySettings ||
		!`optionVar -exists subdivMirrorHistory`) {
		optionVar -intValue subdivMirrorHistory 0;
	}
}


//
//  Procedure Name:
//      subdivMirrorSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//	forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc subdivMirrorSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
    setOptionVars($forceFactorySettings);

	setParent $parent;

	//	Query the optionVar's and set the values into the controls.

	checkBoxGrp -edit
		-value1 `optionVar -query subdivMirrorX`
		-value2 `optionVar -query subdivMirrorY`
		-value3 `optionVar -query subdivMirrorZ`
		subdivMirror;

	int $origObject = `optionVar -query subdivMirrorOrigObject`;
	//	radioButtonGrp -edit -select $origObject
	//	subdivMirrorOrigObjectWidget;
}

//
//  Procedure Name:
//      subdivMirrorCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc subdivMirrorCallback(string $parent, int $doIt)
{
    setParent $parent;

	//	Set the optionVar's from the control values, and then
	//	perform the command.

	optionVar -intValue subdivMirrorX `checkBoxGrp -query -value1 subdivMirror`;
	optionVar -intValue subdivMirrorY `checkBoxGrp -query -value2 subdivMirror`;
	optionVar -intValue subdivMirrorZ `checkBoxGrp -query -value3 subdivMirror`;

//	optionVar -intValue subdivMirrorOrigObject
//		`radioButtonGrp -q -select subdivMirrorOrigObjectWidget`;


	if ($doIt) {
		performSubdivMirror 0; 
		addToRecentCommandQueue "performSubdivMirror 0" "Mirror";
	}
}

//
//  Procedure Name:
//      subdivMirrorOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc subdivMirrorOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "subdivMirror";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	//	STEP 1:  Get the option box.
	//	============================
	//
	string $layout = getOptionBox();
	setParent $layout;
	
	//	STEP 2:  Pass the command name to the option box.
	//	=================================================
	//
	setOptionBoxCommandName($commandName);
	
	//	STEP 3:  Activate the default UI template.
	//	==========================================
	//
	setUITemplate -pushTemplate DefaultTemplate;

	//	STEP 4: Create option box contents.
	//	===================================
	//	
	
	//	Turn on the wait cursor.
	//
	waitCursor -state 1;

	tabLayout -tabsVisible 0 -scrollable 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;
	
	//	Mirror axis
	//	
	checkBoxGrp -ncb 3 -label (uiRes("m_performSubdivMirror.kMirrorchkGrp")) 
		-label1 (uiRes("m_performSubdivMirror.kX"))  
		-label2 (uiRes("m_performSubdivMirror.kY"))  
		-label3 (uiRes("m_performSubdivMirror.kZ")) 
		subdivMirror;

	//	Original object
//	
//	radioButtonGrp 
//		-numberOfRadioButtons 3
//		-label "Original Object"
//		-l1 "Replace"
//		-l2 "Hide"
//		-l3 "Show"
//		subdivMirrorOrigObjectWidget;


	//	Turn off the wait cursor.
	//
	waitCursor -state 0;
	
	//	Step 5: Deactivate the default UI template.
	//	===========================================
	//
	setUITemplate -popTemplate;

	//	Step 6: Customize the buttons.  
	//	==============================
	//

	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performSubdivMirror.kMirrorButton")) 
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Step 7: Set the option box title.
	//	=================================
	//
	setOptionBoxTitle (uiRes("m_performSubdivMirror.kSubdivMirrorOptions"));

	//	Step 8: Customize the 'Help' menu item text.
	//	============================================
	//
	setOptionBoxHelpTag( "SubdivMirror" );

	//	Step 9: Set the current values of the option box.
	//	=================================================
	//
	eval (($setup + " " + $parent + " " + 0));	
	
	//	Step 10: Show the option box.
	//	=============================
	//
	showOptionBox();
}

//
//  Procedure Name:
//      subdivMirrorHelp
//
//  Description:
//		Return a short description about this command.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      string.
//
proc string subdivMirrorHelp()
{
	return ("  Command: mirror subdivision surface geometry. " +
			"Selection: Subdivision surfaces.");
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
proc string assembleCmd()
{
	setOptionVars(false);

	int $inX = `optionVar -query subdivMirrorX`;
	int $inY = `optionVar -query subdivMirrorY`;
	int $inZ = `optionVar -query subdivMirrorZ`;

	// Use our local var instead:
	// int $doHistory = `constructionHistory -q -tgl`;
	int $doHistory = `optionVar -query subdivMirrorHistory`;
	int $origObject = `optionVar -query subdivMirrorOrigObject`;

	string $version = "\"1\"";
    string $cmd = "doSubdivMirrorArgList";
	$cmd = $cmd + "( ";
	$cmd = $cmd + $version;
	$cmd = $cmd + ", { \"" ;
	$cmd = $cmd + $doHistory;
	$cmd = $cmd + "\",\"";
	$cmd = $cmd + $origObject;
	$cmd = $cmd + "\",\"";
	$cmd = $cmd + $inX;
	$cmd = $cmd + "\",\"";
	$cmd = $cmd + $inY;
	$cmd = $cmd + "\",\"";
	$cmd = $cmd + $inZ;
    $cmd = $cmd + "\"} )";

	return $cmd;
}

//
//  Procedure Name:
//      performSubdivMirror
//
//  Description:
//		Perform the performSubdivMirror command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the performSubdivMirror command with the current
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
global proc string performSubdivMirror(int $action)
{
	string $cmd = "";
	switch ($action) {
	  case 0:
		$cmd = `assembleCmd`;
		eval($cmd);
		break;

	  case 1:
		subdivMirrorOptions;
		break;

	  case 2:
		$cmd = `assembleCmd`;
		break;
	}
	return $cmd;
}
