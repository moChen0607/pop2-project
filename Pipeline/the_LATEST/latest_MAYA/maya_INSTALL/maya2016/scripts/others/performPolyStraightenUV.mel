// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  17 Sept 97
//
//
//  Procedure Name:
//      performPolyStraightenUV
//
//  Description:
//        Straighten the selected polygon vertices
//         
//  Input Arguments:
//        $option : Whether to set the options to default values.
//  Return Value:
//        command string iff $option==2
//

proc setOptionVars (int $forceFactorySettings)
{			   
	string $prefix = "polyStraightenUV";
	string $fltVars[]   = {"c", "pl", "bo"};
	float  $fltValues[] = { 0.,   1.,   0.};

	if ($forceFactorySettings || !`optionVar -exists polyStraightenUVUseGap`)
		optionVar -floatValue polyStraightenUVUseGap 1;

	if ($forceFactorySettings || !`optionVar -exists polyStraightenUVGap`)
		optionVar -floatValue polyStraightenUVGap 5;

	for ( $i = size($fltVars) ; $i-- ; )
	{
		$varName = ($prefix + $fltVars[$i]);
		if ($forceFactorySettings || !`optionVar -exists $varName`)
			optionVar -floatValue $varName $fltValues[$i];
	}
}

global proc performPolyStraightenUVSetup (string $parent, int $forceFactorySettings)
{
	string $prefix = "polyStraightenUV";
	string $fltVars[] ={"c", "pl", "bo"};

	setOptionVars($forceFactorySettings);
	setParent $parent;
	
	int $ival = `optionVar -query polyStraightenUVUseGap`;
	checkBoxGrp -edit -value1 $ival polyStraightenUVUseGap;

	intSliderGrp -edit -en $ival 
		-value `optionVar -query polyStraightenUVGap` polyStraightenUVGap;

	for ( $i = size($fltVars) ; $i-- ; )
	{
		$varName = ($prefix + $fltVars[$i]);
		if( $fltVars[$i] == "bo" ){
			floatSliderGrp -e -v (`optionVar -q $varName`) $varName; // Slider is NOT inverted
		} else {
			floatSliderGrp -e -v `optionVar -q $varName` $varName;
		}
	}
}

global proc performPolyStraightenUVCallback (string $parent, int $doIt)
{
	string $prefix = "polyStraightenUV";
	string $fltVars[] ={"c", "pl", "bo"};
	int $i;

	setParent $parent;

	optionVar -intValue polyStraightenUVGap
		`intSliderGrp -query -value polyStraightenUVGap`;

	optionVar -intValue polyStraightenUVUseGap
		`checkBoxGrp -query -value1 polyStraightenUVUseGap`;
       
	for ( $i = size($fltVars) ; $i-- ; )
	{
		$varName = ($prefix + $fltVars[$i]);
		if( $fltVars[$i] == "bo" ){
			optionVar -floatValue $varName (`floatSliderGrp -q -v $varName`); // Slider is NOT inverted
		} else {
			optionVar -floatValue $varName `floatSliderGrp -q -v $varName`;
		}
	}

	if ($doIt) {
		performPolyStraightenUV 0;
		addToRecentCommandQueue "performPolyStraightenUV 0" "PolyStraightenUV";
	}
}

proc polyStraightenUVOptions ()
{
	// Global template variables for form spacing
	global int $gOptionBoxTemplateDescriptionMarginWidth;
	global int $gOptionBoxTemplateFrameSpacing;
	
	string $commandName = "performPolyStraightenUV";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate OptionBoxTemplate;
	waitCursor -state 1;
	
	// Form layout
	string $parent = `formLayout polyUntangleUVOptions`;

		// Settings frame
		string $settingsFrame = 
		`frameLayout -label (uiRes("m_performPolyStraightenUV.kSettingsFrame"))`;
	
			columnLayout;

				string $grayCmd = 
					"{ int $tmp = `checkBoxGrp -q -v1 polyStraightenUVUseGap`;" +
					"  intSliderGrp -e -en $tmp polyStraightenUVGap;}";

				floatSliderGrp 
					-label (uiRes("m_performPolyStraightenUV.kCurvature")) 
					-min -1 
					-max 1 
					polyStraightenUVc;

				floatSliderGrp 
					-label (uiRes("m_performPolyStraightenUV.kPreserveLengthRatio")) 
					-min 0 
					-max 1 
					polyStraightenUVpl;

				checkBoxGrp 
					-label1 (uiRes("m_performPolyStraightenUV.kFillGapsInSelection")) 
					-cc $grayCmd 
					polyStraightenUVUseGap;

				intSliderGrp 
					-label (uiRes("m_performPolyStraightenUV.kUVGapTolerance")) 
					-min 0 
					-max 10 
					-fmx 1000 
					polyStraightenUVGap;

				floatSliderGrp 
					-label (uiRes("m_performPolyStraightenUV.kPreserveOriginalShape")) 
					-min 0 
					-max 1 
					polyStraightenUVbo;

			setParent ..; // columnLayout

		setParent $parent; // frameLayout
		
	setParent ..; // formLayout

	// Attach frame to form layout
	formLayout -e
		-af $settingsFrame "top" $gOptionBoxTemplateFrameSpacing
		-af $settingsFrame "left" $gOptionBoxTemplateFrameSpacing
		-af $settingsFrame "right" $gOptionBoxTemplateFrameSpacing
		-an $settingsFrame "bottom" 
	$parent;

	waitCursor -state 0;
	setUITemplate -popTemplate;
	   
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performPolyStraightenUV.kStraightenUVBorder"))
	       -command ($callback + " " + $parent + " " + 1)
		$applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	setOptionBoxTitle (uiRes("m_performPolyStraightenUV.kStraightenUVBorderOptions"));

	setOptionBoxHelpTag( "StraightenUVBorder" );

	eval (($setup + " " + $parent + " " + 0));      
	showOptionBox();
}

global proc string performPolyStraightenUV (int $option)
{       
	string $prefix = "polyStraightenUV";
	string $fltVars[] ={"c", "pl", "bo"};
	string $cmd="";

	switch ($option) 
	{
		case 0:
		setOptionVars(false);
		$cmd = "polyStraightenUVBorder";
		float $fval;

		for ( $i = size($fltVars) ; $i-- ; )
		{
			$varName = ($prefix + $fltVars[$i]);
			$fval = `optionVar -query $varName`;
			$cmd = $cmd + " -" + $fltVars[$i] + " " + $fval;
		}

		int $ival = `optionVar -query polyStraightenUVUseGap`;
		if ($ival)
			$ival = `optionVar -query polyStraightenUVGap`;
		else
			$ival = 0;
		$cmd =($cmd + " -gt " + $ival);

		evalEcho ("polyPerformAction \""+$cmd+"\" uv 0");
		break;

		case 1: polyStraightenUVOptions; break;
		case 2: $cmd = "performPolyStraightenUV 0"; break;
	}
	return $cmd;
}
