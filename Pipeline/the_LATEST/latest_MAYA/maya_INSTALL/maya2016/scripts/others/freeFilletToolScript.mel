// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      freeFilletToolScript
//
//  Description:
//	create wrapper around the freeFillet command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc freeFilletToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex freeFilletEuc` ) {
		optionVar -iv freeFilletEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex freeFilletLac` ) {
		optionVar -iv freeFilletLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q freeFilletEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q freeFilletLac` $goToTool;
	}
}

global proc freeFilletValues ( string $toolName ) 
{
	performFreeFormFillet 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc freeFilletProperties()
{
	scriptCtxCommonProperties;
}

proc createFreeFilletContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		freeFilletToolSetup( 0, "" );
		scriptCtx -i1 "filletFreeform_S.png"
		  -title (uiRes("m_freeFilletToolScript.kFreeformFilletTool"))
		  -bcn "freeFillet"

		  -exitUponCompletion `optionVar -q freeFilletEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("freeFilletToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_freeFilletToolScript.kSelectTwoCurves")) )
		  -setSelectionPrompt ((uiRes("m_freeFilletToolScript.kSelectCurve")) )
		  -setDoneSelectionPrompt ((uiRes("m_freeFilletToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q freeFilletLac`
		  -showManipulators true

		  -setSelectionCount 2
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		$tool;
	}
}

global proc string freeFilletToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolFreeFillet` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "freeFilletContext";
			createFreeFilletContext $tool;
		}
	}
	return scriptToolScript( "performFreeFormFillet", $setToTool, $tool );
}
