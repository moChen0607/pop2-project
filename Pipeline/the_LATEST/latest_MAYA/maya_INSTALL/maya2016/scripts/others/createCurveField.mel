// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//<doc>
//<name createCurveField>
//<owner "Autodesk">
//
//<synopsis>
//		createCurveField
//
//<returns>
//		None.	
//
//<description>
//	This command creates a volume axis field with a curve connected to it.
//	If a curve is selected when the field is created it is used, otherwise
//	a new curve is generated.
//
//<flags>
//	None.
//
//<examples>
//  createCurveField;
//</doc>


global proc createCurveField()
{
	string $curves[] = `ls -sl -dag -type nurbsCurve`;

	string $sel[] = `ls -sl`;
	string $objs[];	
	int	   $numObjs=0;
	int $i;
	int $numSel = size($sel);
	// remove the curves from the selection
	for( $i = 0; $i < $numSel; $i++ ){
		string $crvs[] = `ls -dag -type nurbsCurve $sel[$i]`;
		if( 0 == size( $crvs )){
			$objs[$numObjs] = $sel[$i];
			$numObjs++;
		}
	}
	if($numSel != $numObjs ){
		select $objs;
	}
	VolumeAxis;
	$sel = `ls -sl -dag -type volumeAxisField`;
	if( size($sel ) < 1 ){
		return;
	}
	string $field = $sel[0];
	
	string $curve;
	if( size($curves) > 0){
		$curve = $curves[0];
	} else {
		$curve = `curve -d 3 -p -4 0 0 -p -2 0 0 -p 0 0 0 -p 2 0 0 -p 4 0 0 -k 0 -k 0 -k 0 -k 1 -k 2 -k 2 -k 2 `;
	}
	setAttr ($field + ".volumeShape") 7;
	setAttr ($field + ".awayFromAxis") 0;
	setAttr ($field + ".alongAxis") 0.5;
	setAttr ($field + ".directionalSpeed") 0.0;
	connectAttr ($curve + ".worldSpace[0]") ($field + ".inputCurve");
}
