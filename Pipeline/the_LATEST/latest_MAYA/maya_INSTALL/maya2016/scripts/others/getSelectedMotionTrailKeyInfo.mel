// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

// Description: Returns true if a motion trail key is selected, false otherwise.
//				Only works on one key at a time.
//				the following data is added to $info:
// 				$info[0] = the name of object the keys are on
// 				$info[1] = the time of the selected key (if there is one)
// 				$info[2] = if the key is a breakdown or not
//
//
global proc int getSelectedMotionTrailKeyInfo(string $transform,
											  string $shape,
											  string $info[])
{
	int $numKeysFound = 0;

	string $matchStr = ($transform + ".point\\[[0-9]*\\]");
	string $sel[] = `ls -sl`;
	for( $s in $sel ) {
		if( size(`match $matchStr $s`) > 0 ) {
			$numKeysFound++;

			string $indexStr= `match "\\[[0-9]*\\]" $s`;
			int $index = int(`substring $indexStr 2 (size($indexStr)-1)`);
			float $times[] = `getAttr ($shape + ".keyframeTimes")`;
			string $object[] = `listConnections ($shape + ".transformToMove")`;
			$info[size($info)] = $object[0];
			$info[size($info)] = $times[$index];

			// Make sure it's a breakdown key for TX, TY and TZ
			int $foundCount = 0;
			float $breakdown[] = `keyframe -q -breakdown $object[0]`;
			for( $key in $breakdown ) {
				if( $key == $times[$index] ) {
					$foundCount++;
				}
			}
			$info[size($info)] = ($foundCount == 3);
			return 1;
		}
	}
	return 0;
}
