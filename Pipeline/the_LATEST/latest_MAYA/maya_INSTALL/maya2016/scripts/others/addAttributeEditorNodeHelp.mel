// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  24.Jan.2002
//
//<doc>
//<name addAttributeEditorNodeHelp>
//
//<synopsis>
//		addAttributeEditorNodeHelp(string $nodeType, string $helpCommand)
//
//<description>
//		The Attribute Editor's Help menu creates menu items
//		for node types that it is currently displaying. For custom
//		node types use this procedure to specify a help command
//		to be invoked when the custom node type menu item is
//		selected.
//
//<flags>
//		string $nodeType The node type.
//
//		string $helpCommand The command to display help for the node.
//			This command is invoked when the corresponding menu item is 
//			selected from the Attribute Editor Help menu. Specify an 
//			empty string to prevent the menu item for the node type
//			from being created.
//
//<returns>
//		Nothing.
//
//<examples>
//
//	//	Prevent a menu item in the Help menu from being
//	//	created for nodes of type "customNodeType1".
//	//
//	addAttributeEditorNodeHelp("customNodeType1", "");
//
//	//	Use the showHelp command to display a specific web page
//	//	when the "customNodeType2" Help menu item is selected.
//	//
//	addAttributeEditorNodeHelp("customNodeType2",
//		"showHelp -absolute \"http://www.alias.com\"");
//
//	//	Use the print command to inform user no help is available
//	//	for "customNodeType3".
//	//
//	addAttributeEditorNodeHelp("customNodeType3",
//		"print \"No help available yet for customNodeType3\\n\"");
//
//</doc>

global proc addAttributeEditorNodeHelp(
	string $nodeType,
	string $helpCommand)
{
	global string $gAttributeEditorNodeTypeArray[];
	global string $gAttributeEditorHelpCommandArray[];

	int $count, $index, $length;

	$length = size($gAttributeEditorNodeTypeArray);

	//	Determine if the argument node type is already in the
	//	array.
	//
	$count = stringArrayCount($nodeType, $gAttributeEditorNodeTypeArray);
	
	if (0 == $count) {
		//
		//	Not in array yet.
		//
		//	Add node type and help command.
		//
		$gAttributeEditorNodeTypeArray[$length] = $nodeType;
		$gAttributeEditorHelpCommandArray[$length] = $helpCommand;

	} else {
		//
		//	Already in array.
		//
		//	Find index of node in array.
		//
		for ($index = 0; $index < $length; $index++) {
			if ($nodeType == $gAttributeEditorNodeTypeArray[$index]) {
				break;
			}
		}

		//	Update help command.
		//
		$gAttributeEditorHelpCommandArray[$index] = $helpCommand;
	}
}

