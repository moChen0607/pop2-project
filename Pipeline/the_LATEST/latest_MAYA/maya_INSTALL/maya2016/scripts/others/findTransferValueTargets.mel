// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

proc int
checkContainerParents(string $container, string $containersToCheck[])
//
// Return true if the container is a descendent member of any of the
// containersToCheck.
//
{
	string $parentContainer = `container -q -findContainer $container`;
	while ($parentContainer != "") {
		if (stringArrayContains($parentContainer,$containersToCheck)) {
			return 1;
		}
		$parentContainer = `container -q -findContainer $parentContainer`;
	};
	return 0;
}

global proc string[] findTransferValueTargets()
//
// Examine the current selection list to see if it indicates we should 
// do the "transfer attribute values" between 2 containers on the
// selection list, or act on the sel list directly.
//
{
	string $sel[] = `ls -sl`;
	if (size($sel) > 2) {
		string $selCon[] = `ls -sl -containers`;
		if (size($selCon) == 2) {
			// if 2 containers are selected and the other selected
			// objects are members of those containers, then transfer values
			// on the containers
			//
			int $useContainers = 1;
			for ($selObj in $sel) {
				if (0 == `container -q -isContainer $selObj`) {
					if (! checkContainerParents($selObj,$selCon)) {
						$useContainers = 0;
						break;
					}
				}
			}
			if ($useContainers) {
				return $selCon;
			}
		}
	}
	return $sel;
}
