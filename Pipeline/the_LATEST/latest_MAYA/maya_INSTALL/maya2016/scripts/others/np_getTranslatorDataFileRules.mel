// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//      Description:
//              build array of translators' file rules.
//      Input:
//          the value true means it will return the default file rules list. The default value
//          is gotten by the command translator.
//          the value false means it will return the file rules list of the current workspace
//
//
global proc string[] np_getTranslatorDataFileRules(int $bUseDefaults)
{     
    string $results[];
    
    string $ignoreList[] = np_getTranslatorIgnoreList();
    string $primaryFileRuleList[] = np_getPrimaryProjectFileRules(true);
    string $secondaryFileRuleList[] = np_getSecondaryProjectFileRules(true);
    
    //add primary file rule list to ignoreList
    int $ingoreListSize = size($ignoreList);
    int $listSize = size($primaryFileRuleList);
    for($i = 0; $i < $listSize; $i += 3)
    {
        $ignoreList[$ingoreListSize++] = $primaryFileRuleList[$i+1];
    }
    
    //add secondary file rule list to ignoreList    
    $listSize = size($secondaryFileRuleList);
    for($i = 0; $i < $listSize; $i += 3)
    {
        $ignoreList[$ingoreListSize++] = $secondaryFileRuleList[$i+1];
    }    
    
    string $translatorsList[] = `translator -q -list`;
    
    $listSize = size($translatorsList);
    for($i = 0; $i < $listSize; $i++)
    {
        //See if this translator should be ignored
        if(stringArrayContains($translatorsList[$i] ,$ignoreList))
            continue;
         
        //test for the existance of a translator   
        if(!`translator -q -ld $translatorsList[$i]`)
            continue;
            
        $results[size($results)] = $translatorsList[$i];        
        
        if($bUseDefaults)
        {
            $results[size($results)] = `translator -q -dfr $translatorsList[$i]`;
        }
        else
        {
            $results[size($results)] = `workspace -q -fre $translatorsList[$i]`;
        }    
    }
    
    return $results;
}
