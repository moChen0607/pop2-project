// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//	General Interactive Batch render interface.
//

global proc mayaBatchRender()
{
	string $currRenderer = currentRenderer();
	string $cmd = "batchRender";
	if( $currRenderer == "mentalRay" ){
		// make sure mental ray is initialized
		miCreateDefaultNodes();

		if( `optionVar -exists "v_mrBatchRenderRenderMode"` ){
			if( `optionVar -query v_mrBatchRenderRenderMode` == 1 ){
				// Batch rendering with mrSA
				$cmd = $cmd + " -useStandalone";
				if( `optionVar -exists "v_mrBatchRenderPreRenderCommand"` ){
					string $preCmd = `optionVar -query v_mrBatchRenderPreRenderCommand`;
					if ($preCmd != "") {
						$cmd = $cmd + " -preRenderCommand \"" + $preCmd + "\"";
					}
				}
				if( `optionVar -exists "v_mrBatchRenderRenderCommand"` ){
					string $renderCmd = `optionVar -query v_mrBatchRenderRenderCommand`;
					$cmd = $cmd + " -melCommand \"" + $renderCmd + "\"";
				} else {
					error((uiRes("m_mayaBatchRender.kNoStandaloneCmd")));
				}

				string $extraOpts = " -renderCommandOptions \"";
				int $useExtraOpts = false;
				if( `optionVar -exists "v_mrBatchRenderRenderCommandOptions"` ){
					string $cmdOpts = `optionVar -query v_mrBatchRenderRenderCommandOptions`;
					if ($cmdOpts != "") {
						$extraOpts = $extraOpts + $cmdOpts;
						$useExtraOpts = true;
					}
				}
				// append include/lib dirs so that they are picked up last
				if( `optionVar -exists "v_mrBatchRenderRenderCommandOptionsInclude"` ){
					string $cmdOpts = `optionVar -query v_mrBatchRenderRenderCommandOptionsInclude`;
					if ($cmdOpts != "") {
						$extraOpts = $extraOpts + " -I \\\\\\\"" + $cmdOpts + "\\\\\\\"";
						$useExtraOpts = true;
					}
				}
				if( `optionVar -exists "v_mrBatchRenderRenderCommandOptionsLib"` ){
					string $cmdOpts = `optionVar -query v_mrBatchRenderRenderCommandOptionsLib`;
					if ($cmdOpts != "") {
						$extraOpts = $extraOpts + " -L \\\\\\\"" + $cmdOpts + "\\\\\\\"";
						$useExtraOpts = true;
					}
				}
				if ($useExtraOpts) {
					$extraOpts = $extraOpts + "\"";
					$cmd = $cmd + $extraOpts;
				}

				evalString $cmd;
				return;
			}
		}
	}


	// get renderer specific flags to 'Render' executable
	string $optsProc = `renderer -q -batchRenderOptionsStringProcedure $currRenderer`;
	string $cmdOpts = "";
	if ($optsProc != "") {
		$cmdOpts = `eval $optsProc`;
	}

	$cmd = $cmd + " -renderCommandOptions \"-interactive 1 " + $cmdOpts + "\" ";
	evalString $cmd;
}
