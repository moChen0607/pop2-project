// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  September 22, 2006
//
//  Description:
//      Helper script to centralize access to localized
//      plugin resources.   
//
//      This behaves in a similar fashion to languageResourcePath,
//      the main difference is that it has additional search locations
//      and it does not assume that resource files for plugins are 
//      will always be supplied. 
//
//      The procedure takes the name of a resource file and returns a
//      path to the language-appropriate version of the given file.
//		If the file does not exist an empty string will be returned.  
// 
//		The resulting path, if not empty, is guaranteed to point
//		to the localized version of the file.
//		
global proc string pluginLanguageResourcePath( string $file ) 
{
	string $path = "";
    int $isLocalized = `about -uiLanguageIsLocalized`;

    // In non-localized environment, return empty path
	if( !$isLocalized ) {
		return $path;
    }

    // Get MAYA_PLUG_IN_RESOURCE_PATH for the search path

    string $searchPath = `getenv("MAYA_PLUG_IN_RESOURCE_PATH")`;
    if (size($searchPath) > 0)
    {
        // Searchpath strings are separated with different 
        // characters depending on the platform. 
        string $pathSep; 
        if (`about -win`)
        {
           $pathSep = ";";
        }
        else
        {
           $pathSep = ":";
        }

        string $searchArray[] = stringToStringArray($searchPath, $pathSep);

        // Search for the file 
        // Return value is empty string if path is not found
		for ( $dir in $searchArray ) {
			$path = ($dir + "/" + $file); 
			
			// Did we find the pres resource in one of
			// the MAYA_PLUG_IN_RESOURCE_PATH folders?
			if( `filetest -r $path` ) {
				return $path;
			}
			// On MacOS, it might be that the pres resource is actually in
			// a folder with the lproj extension. Lets check for this case.
			else if (`about -macOS`) {
				$path = ($dir + ".lproj/" + $file); 
				if( `filetest -r $path` ) {
					return $path;
				}
			}
		}
    }

	return "";
}
