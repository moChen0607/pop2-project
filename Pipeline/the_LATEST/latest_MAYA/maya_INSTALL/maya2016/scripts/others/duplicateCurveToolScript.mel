// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      duplicateCurveToolScript
//
//  Description:
//	create wrapper around the duplicateCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc duplicateCurveToolSetup( int $forceFactorySettings,
									 string $goToTool )
{
	if( $forceFactorySettings || !`optionVar -ex duplicateCurveEuc` ) {
		optionVar -iv duplicateCurveEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex duplicateCurveLac` ) {
		optionVar -iv duplicateCurveLac 1;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q duplicateCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q duplicateCurveLac` $goToTool;
	}
}

global proc duplicateCurveValues ( string $toolName ) 
{
	performDuplicateCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc duplicateCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createDuplicateCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		duplicateCurveToolSetup( 0, "" );
		scriptCtx -i1 "duplicateCurve.png"
		  -title (uiRes("m_duplicateCurveToolScript.kDuplicateTool"))
		  -bcn "duplicateCurve"

		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("duplicateCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_duplicateCurveToolScript.kSelectSurface"))
		  -setSelectionPrompt (uiRes("m_duplicateCurveToolScript.kDuplicateSurface"))

		  -showManipulators true
		  -setSelectionCount 0
		  -setAutoToggleSelection true

		  -setAutoComplete `optionVar -q duplicateCurveLac`
		  -exitUponCompletion `optionVar -q duplicateCurveEuc`
		  -setAllowExcessCount true

		  -surfaceEdge true
		  -curveOnSurface true
		  -isoparm true
		  -polymeshEdge true
		$tool;
	}
}

global proc string duplicateCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolDuplCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "duplicateCurveContext";
			createDuplicateCurveContext $tool;
		}
	}
	return scriptToolScript( "performDuplicateCurve", $setToTool, $tool );
}
