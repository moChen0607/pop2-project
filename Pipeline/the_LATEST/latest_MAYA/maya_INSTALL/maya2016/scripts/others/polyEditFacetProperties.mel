// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//
//
//
//
//  Creation Date:  Thu Aug  7 21:44:35 MDT 1997
//
//  Description:
//              helps to create the controls for polyCreateFacet,
//              polyAppend and polySplit tools property sheet.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//

global proc polyEditFacetProperties (string $type, string $ctxCmd)
{
	// Global template variables for form spacing
	global int $gToolOptionBoxTemplateDescriptionMarginWidth;
	global int $gToolOptionBoxTemplateFrameSpacing;
	
	// Global template variables for field widths
	global int $gToolOptionBoxTemplateLabelWidth;
	global int $gToolOptionBoxTemplateControlAreaWidth;
	
	string $parent = `toolPropertyWindow -q -location`;
	
	setUITemplate -pushTemplate ToolOptionBoxTemplate;
	setParent $parent;

	string $title;
	if ($type == "polyAppend") {
		$title = (uiRes("m_polyEditFacetProperties.kAppendSettings"));
	} else if ($type == "polySplit") {
		$title = (uiRes("m_polyEditFacetProperties.kSplitSettings"));
	} else if ($type == "polyCreateFacet") {
		$title = (uiRes("m_polyEditFacetProperties.kCreateSettings"));
	}

	// Form layout
	$parent = `formLayout $type`;
	
		// Description frame
		string $descriptionFrame = 
		`frameLayout -label (uiRes("m_polyEditFacetProperties.kDescriptionFrame")) -mw $gToolOptionBoxTemplateDescriptionMarginWidth`;	
			
			if ($type == "polyAppend")
			{
				text -label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolDescApp")) polyCreatePolyToolLabel;
			}
			else if($type == "polyCreateFacet")
			{
				text -label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolDescCreate")) polyCreatePolyToolLabel;
			}
			else
			{
				text -label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolDescSplit")) polyCreatePolyToolLabel;
			}
			
		setParent $parent;
		
		// Settings frame
		string $settingsFrame = 
		`frameLayout -label (uiRes("m_polyEditFacetProperties.kSettingsFrame"))`;

			columnLayout;

				intFieldGrp 
					-label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolDivisions")) 
					-v1 1 
					-extraLabel (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolSubdExtra")) 
					-columnWidth3  $gToolOptionBoxTemplateLabelWidth
								   ($gToolOptionBoxTemplateControlAreaWidth / 3)
								   150
					subdSld;

				if ($type == "polyAppend") {
					// Angle
					floatSliderGrp 
						-field true 
						-label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolRotAngle")) 
						-min 0.0 
						-max 360.0 
						-v 0.0 
						angSld;
				}
				else if($type == "polySplit")
				{
					floatSliderGrp 
						-field true 
						-label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolSmooAngle")) 
						-min 0.0 
						-max 180.0 
						-v 0.0 
						smoothingAngleSld;

					checkBoxGrp 
						-label "" 
						-label1 (uiRes("m_polyEditFacetProperties.kSplitOnlyFromEdges")) 
						snapEdgeBox;

					// Magnet Snapping
					checkBoxGrp 
						-label "" 
						-label1 (uiRes("m_polyEditFacetProperties.kUseSnappingPointsAlongEdge")) 
						-columnWidth 2 ($gToolOptionBoxTemplateControlAreaWidth / 3 + 150)
						snapMagnetBox;
						
					intFieldGrp 
						-label (uiRes("m_polyEditFacetProperties.kNumberOfPoints")) 
						-extraLabel (uiRes("m_polyEditFacetProperties.kSnapToMidpoint")) 
						-columnWidth3  $gToolOptionBoxTemplateLabelWidth
									   ($gToolOptionBoxTemplateControlAreaWidth / 3)
									   150
						magnetSld;
						
					floatSliderGrp  
						-field true 
						-label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolSnapTol")) 
						-min 0. 
						-max 100 snapSld;

				}

				if($type != "polySplit")
				{
					checkBoxGrp -label1 (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolPlanar")) planarBox;

					checkBoxGrp -label1 (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolLimit")) limitPoints;

					intSliderGrp 
						-field true 
						-label (uiRes("m_polyEditFacetProperties.kPolyCreatePolyToolLimitTo")) 
						-min 3 
						-max 100 
						-enable (`checkBoxGrp -q -value1 limitPoints`) 
						maxPointsSlider;

					radioButtonGrp 
						-numberOfRadioButtons 3
						-label (uiRes("m_polyEditFacetProperties.kTextureSpace"))
						-label1 (uiRes("m_polyEditFacetProperties.kTextureSpaceNormalize"))
						-label2 (uiRes("m_polyEditFacetProperties.kTextureSpaceUnitize"))
						-label3 (uiRes("m_polyEditFacetProperties.kTextureSpaceNone"))
						-select 1 
						-vr 
						textureBox;
				}
				
			setParent ..; // columnLayout
	
		setParent $parent; // frameLayout
		
	setParent ..; // formLayout

	// Attach Description/Settings frames to form layout
	formLayout -e
		-af $descriptionFrame "top" $gToolOptionBoxTemplateFrameSpacing
		-af $descriptionFrame "left" $gToolOptionBoxTemplateFrameSpacing
		-af $descriptionFrame "right" $gToolOptionBoxTemplateFrameSpacing
		-an $descriptionFrame "bottom"

		-ac $settingsFrame "top" $gToolOptionBoxTemplateFrameSpacing $descriptionFrame
		-af $settingsFrame "left" $gToolOptionBoxTemplateFrameSpacing
		-af $settingsFrame "right" $gToolOptionBoxTemplateFrameSpacing
		-an $settingsFrame "bottom" 

	$parent;

	setUITemplate -popTemplate;
}
