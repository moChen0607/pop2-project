// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  20 Feb 2002
//
//  Description:
//      Make Collide 
//
global proc doMakeCollideFluid( int $version, string $args[] )
{
	if(( $version < 1 ) || ( size( $args ) < 1 )) {
		error(uiRes("m_doAppendHairCache.kIncorrectVersion"));
		return;
	}

	int $tess = $args[0];

	string $selected[] = `ls -sl`;
	// Handle the case where the xform nodes are selected.
	string $nurbs[]  = `listRelatives -ni -s -type "nurbsSurface" $selected`;
	string $meshs[]  = `listRelatives -ni -s -type "mesh" $selected`;
	string $fluids[] = `listRelatives -type "fluidShape" -type "fluidTexture3D" -type "fluidTexture2D" $selected`;

	// Handle the case where the shape nodes are selected.
	string $select;
	for( $select in $selected ) {
		if( `nodeType $select` == "nurbsSurface" ) $nurbs[size($nurbs)] = $select;
		if( `nodeType $select` == "mesh" ) $meshs[size($meshs)] = $select;
		if( `nodeType $select` == "fluidShape" ) $fluids[size($fluids)] = $select;	
	}

	if( size($nurbs) + size($meshs) == 0 ) {
		error ((uiRes("m_doMakeCollideFluid.kSelectNurbsOrMesh")));
		return;
	}

	if( size($fluids) == 0 ) {
		error((uiRes("m_doMakeCollideFluid.kSelectFluid")));
		return;
	}

	string $cmd;
	string $geometry;
	string $fluid;
	string $fmt = (uiRes("m_doMakeCollideFluid.kAlreadyColliding"));
	for($geometry in $nurbs) {
		for($fluid in $fluids) {			
			if(`collision -q $geometry $fluid`) {
				warning(`format -s $geometry -s $fluid $fmt`);
				continue;
			}
			$cmd = "createNode geoConnector;";
			string $geo = evalEcho($cmd);
			setAttr ($geo + ".tessellationFactor") $tess;
			$cmd = "connectAttr " + $geometry + ".message " + $geo + ".owner;"; 
			evalEcho($cmd);
			$cmd = "connectAttr " + $geometry + ".worldMatrix[0] " + $geo + ".worldMatrix;";
			evalEcho($cmd);
			$cmd = "connectAttr " + $geometry + ".local " + $geo + ".localGeometry;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".resilience " + $fluid + ".collisionResilience;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".friction " + $fluid + ".collisionFriction;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".sweptGeometry " + $fluid + ".collisionGeometry;";
			evalEcho($cmd);
			$cmd = "connectAttr time1.outTime " + $geo + ".currentTime;";			
			evalEcho($cmd);
		}
	}

	for($geometry in $meshs) {
		for($fluid in $fluids) {
			if(`collision -q $geometry $fluid`) {
				warning(`format -s $geometry -s $fluid $fmt`);
				continue;
			}
			$cmd = "createNode geoConnector;";
			string $geo = evalEcho($cmd);
			setAttr ($geo + ".tessellationFactor") $tess;
			$cmd = "connectAttr " + $geometry + ".message " + $geo + ".owner;";
			evalEcho($cmd);
			$cmd = "connectAttr " + $geometry + ".worldMatrix[0] " + $geo + ".worldMatrix;";
			evalEcho($cmd);
			$cmd = "connectAttr " + $geometry + ".outMesh " + $geo + ".localGeometry;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".resilience " + $fluid + ".collisionResilience;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".friction " + $fluid + ".collisionFriction;";
			evalEcho($cmd);
			$cmd = "connectAttr -na " + $geo + ".sweptGeometry " + $fluid + ".collisionGeometry;";
			evalEcho($cmd);
			$cmd = "connectAttr time1.outTime " + $geo + ".currentTime;";			
			evalEcho($cmd);
		}
	}
}

