// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  2005
//
//  Description: findTypeInHistory returns the node of the specified type that is
//  the closest traversal to the input object.
//		
//

global proc string findTypeInHistory(string $obj, string $type, int $future, int $past)
{
	if( $past && $future ){
		// In the case that the object type exists in both past and future
		// find the one that is fewer connections away.
		string $past[] = `listHistory -f 0 -bf -af $obj`;
		string $future[] = `listHistory -f 1 -bf -af $obj`;
		string $pastObjs[] = `ls -type $type $past`;
		string $futureObjs[] = `ls -type $type $future`;
		int $numPast = size( $pastObjs );
		int $numFuture = size( $futureObjs );
		if($numPast>0){
			if( $numFuture > 0) {
				int $i;
				int $min = size($future);
				if( size($past) < $min ){
					$min = size($past);
				}
				for( $i = 0; $i <  $min; $i++){
					if( $past[$i] == $pastObjs[0] ){
						return $pastObjs[0];
					}
					if( $future[$i] == $futureObjs[0] ){
						return $futureObjs[0];
					}
				}
			} else {
				return $pastObjs[0];
			}
		} else if( $numFuture > 0) {
			return $futureObjs[0];
		}
	} else {
		if( $past ){
			string $hist[] = `listHistory -f 0 -bf -af $obj`;
			string $objs[] = `ls -type $type $hist`;
			if( size($objs) > 0 ){
				return $objs[0];
			}
		}
		if( $future ){
			string $hist[] = `listHistory -f 1 -bf -af $obj`;
			string $objs[] = `ls -type $type $hist`;
			if( size($objs) > 0 ){
				return $objs[0];
			}
		}
	}
	return "";
}
