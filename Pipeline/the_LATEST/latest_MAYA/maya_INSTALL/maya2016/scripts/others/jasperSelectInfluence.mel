// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.


global proc jasperSelectInfluence(string $jasperCommand, string $influence)
// 
// Find all the selected skinCluster (not just the active one) 
// nodes that are influenced by $influence and connect the message
// attribute from the $influence transform to the .ptt attribute of the
// skinCluster node. 
//
{
	global string $CurrentSkinPaintInfluence;


	if (`treeView -q -exists skinClusterInflList`) {
			string $ann = `treeView -q -ann skinClusterInflList`;
		if ($ann == "shortNames") {
			$influence = influenceNameFromShortName($influence);
	    }
	}
	string $connections[];
	string $connection;

	string $buffer[];
	string $cmd = ($jasperCommand + " -q -dns `currentCtx`");
	int $size = `eval $cmd`;
	if ($size != 0)
	{
		// If selection mode is set to dependency node
		//
		$cmd = ($jasperCommand + " -q -dnn `currentCtx`");
		string $name = `eval $cmd`;
		tokenize( $name, "|", $buffer);
	}
			
	$CurrentSkinPaintInfluence = $influence;
//	print("Setting current influence to " + $influence + "\n");

	for( $i = 0; $i < $size; $i++ )
	{
		string $skinClusterName;
		$skinClusterName = $buffer[$i];
		if (size($skinClusterName) == 0)
			continue;

		// find if the skinCluster node is connected to the given 
		// influence
		//
		int $foundConnection = false;
		$connections = `listConnections ($skinClusterName+".matrix")`;
		for ($connection in $connections)
			if ($connection == $influence)
			{
				$foundConnection = true;
				break;
			}
		
		
		// Delete any existing connection to the .ptt attribute of the 
		// skinCluster
		//
		$connections = `listConnections $skinClusterName`;
		for( $connection in $connections ) 
			if ($connection != "")
			{
				if (isConnected(($connection+".message"),($skinClusterName+".ptt")))
				{
					disconnectAttr(($connection+".message"), ($skinClusterName+".ptt"));
					hilite -u $connection;
				}
			}

		// Connect $influence to the skinCluster node
		//
		if ($foundConnection && size($influence) != 0)
		{
			connectAttr ($influence+".message") ($skinClusterName+".ptt");
			hilite $influence;
//				print("Connecting node : "+$skinClusterName+ " to " + $influence+"\n");
		}
	}
}	
