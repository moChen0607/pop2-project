// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

{
	string $MayaScenes    = (uiRes("m_fileDialogFilterTypes.kMayaScenes"));
	string $MayaAscii     = (uiRes("m_fileDialogFilterTypes.kMayaAscii"));
	string $MayaBinary    = (uiRes("m_fileDialogFilterTypes.kMayaBinary"));
	string $ImageFiles    = (uiRes("m_fileDialogFilterTypes.kImageFiles"));
	string $PluginFiles   = (uiRes("m_fileDialogFilterTypes.kPluginFiles"));
	string $TemplateFiles = (uiRes("m_fileDialogFilterTypes.kTemplateFiles"));
    string $PixmapFiles   = (uiRes("m_fileDialogFilterTypes.kPixmapFiles"));
	string $AllFiles      = (uiRes("m_fileDialogFilterTypes.kAllFiles"));
	string $PSFiles       = (uiRes("m_fileDialogFilterTypes.kPhotoshopFiles"));
	string $CacheFiles    = (uiRes("m_fileDialogFilterTypes.kXMLCacheFiles"));
	string $EDLFiles	  = (uiRes("m_fileDialogFilterTypes.kEDLFiles"));
	string $MovieFiles    = (uiRes("m_fileDialogFilterTypes.kMovieFiles"));
	string $ToolSettings  = (uiRes("m_fileDialogFilterTypes.kFromToolSettings"));
	string $OfflineEditAscii = (uiRes("m_fileDialogFilterTypes.kOfflineEditAscii"));
	string $OfflineEditBinary = (uiRes("m_fileDialogFilterTypes.kOfflineEditBinary"));
	string $MelFiles    = (uiRes("m_fileDialogFilterTypes.kMelFiles"));
	string $EDLExportFiles	 = (uiRes("m_fileDialogFilterTypes.kEDLExportFiles"));
	string $RenderProxy	  = (uiRes("m_fileDialogFilterTypes.kRenderProxy"));
	string $LightProfile  = (uiRes("m_fileDialogFilterTypes.kLightProfile"));
	string $weightMap         = (uiRes("m_fileDialogFilterTypes.kWeightMap"));

	global string $FileDialogFilterTypes[22];
	$FileDialogFilterTypes[0] = ($AllFiles + " (*)");
	$FileDialogFilterTypes[1] = ($MayaScenes + " (*.ma *.mb)");
	$FileDialogFilterTypes[2] = ($MayaAscii + " (*.ma)");
	$FileDialogFilterTypes[3] = ($MayaBinary + " (*.mb)");
	$FileDialogFilterTypes[4] = ($ImageFiles + " (*.map *.pix *.als *.ALS *.jpeg *.JPEG *.jpg *.JPG *.pntg *.PNTG *.ps *.PS *.png *.PNG *.psd *.PSD *.pict *.PICT");
		$FileDialogFilterTypes[4] = ($FileDialogFilterTypes[4] + " *.ptx");
		$FileDialogFilterTypes[4] = ($FileDialogFilterTypes[4] + " *.qt *.QT *.qtif *.QTIF *.sgi *.SGI *.tga *.TGA *.tif *.TIF *.bmp *.BMP *.tiff *.TIFF *.iff *.IFF *.rgb *.RGB *.tdi *.TDI *.gif *.GIF *.exr *.EXR *.xpm *.XPM *.hdr *.HDR *.dds *.DDS)");
	$FileDialogFilterTypes[5] = ($PluginFiles + " (*.mll *.so *.py *.nll.dll *.pyc *.MLL *.SO *.PY *.PYC *.NLL.DLL *.bundle *.BUNDLE)");
	$FileDialogFilterTypes[6] = ($TemplateFiles + " (*.template)");
    $FileDialogFilterTypes[7] = ($PixmapFiles + " (*.bmp *.gif *.ico *.jpeg *.jpg *.mng *.pbm *.pgm *.png *.ppm *.svg *.tif *.tiff *.xbm *.xpm)");
	$FileDialogFilterTypes[8] = ($PSFiles + " (*.psd)");
	$FileDialogFilterTypes[9] = ($CacheFiles + " (*.xml)");
	$FileDialogFilterTypes[10] = ($EDLFiles + " (*.xml *.aaf)");
	$FileDialogFilterTypes[11] = ($MovieFiles + " (*.avi *.AVI *.mov *.MOV *.qt *.QT *.mpeg *.MPEG)");
	$FileDialogFilterTypes[12] = ($ToolSettings + " (*)");
	$FileDialogFilterTypes[13] = ($OfflineEditAscii + " (*.editMA)");
	$FileDialogFilterTypes[14] = ($OfflineEditBinary + " (*.editMB)");
	$FileDialogFilterTypes[15] = ($MelFiles + " (*.mel)");
	$FileDialogFilterTypes[16] = ($EDLExportFiles + " (*.xml)");
	$FileDialogFilterTypes[17] = ($RenderProxy + " (*.mi *.mi.gz)");
	$FileDialogFilterTypes[18] = ($LightProfile + " (*.ies *.ldt)");
	$FileDialogFilterTypes[19] = ($weightMap      + " (*.weightMap)");
}

global proc string fileTypeToFilterDescription(string $type)
{
	string $filterDesc = $type;

	if ( $type == "mayaBinary")
	{
		$filterDesc = (uiRes("m_fileDialogFilterTypes.kMayaBinary"));
	}
	else if ( $type == "mayaAscii")
	{
		$filterDesc = (uiRes("m_fileDialogFilterTypes.kMayaAscii"));
	}
	else if ( $type == uiRes("m_fileOptions.kBestGuess"))
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kAllFiles");
	}
	else if ( $type == "image")
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kImageFiles");
	}
	else if ( $type == "movie")
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kMovieFiles");
	}
	else if( $type == "edl" )
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kEDLFiles");
	}
	else if( $type == "editMA" ) 
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kOfflineEditAscii");
	}
	else if( $type == "editMB" ) 
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kOfflineEditBinary");
	}
	else if( $type == "MIRenderProxy" ) 
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kRenderProxy");
	}
	else if( $type == "ies" ) 
	{
		$filterDesc = uiRes("m_fileDialogFilterTypes.kLightProfile");
	}

	return $filterDesc;
}

global proc string filterDescriptionToFileType(string $filterDesc)
{
	string $buffer[];
	int $numSize;
	$numSize = `tokenize $filterDesc "(" $buffer`;
	$filterDesc = $buffer[0];
	if($numSize > 2){
		int $i=0;
		for($i=0;$i<$numSize-2;$i++){
			$filterDesc +="("+$buffer[$i+1];
		}
	}
	$filterDesc = strip($filterDesc);
	string $type = $filterDesc;

	if ($filterDesc == (uiRes("m_fileDialogFilterTypes.kMayaBinary")))
	{
		$type = "mayaBinary";
	}
	else if ($filterDesc == (uiRes("m_fileDialogFilterTypes.kMayaAscii")))
	{
		$type = "mayaAscii";
	}
	else if ($filterDesc == (uiRes("m_fileDialogFilterTypes.kAllFiles")))
	{
		$type = uiRes("m_fileOptions.kBestGuess");
	}
	else if ($filterDesc == (uiRes("m_fileDialogFilterTypes.kImageFiles")))
	{
		$type = "image";
	}
	else if ($filterDesc == (uiRes("m_fileDialogFilterTypes.kMovieFiles")))
	{
		$type = "movie";
	}
	else if(  ($filterDesc == (uiRes("m_fileDialogFilterTypes.kEDLFiles")))
			||($filterDesc == (uiRes("m_fileDialogFilterTypes.kEDLExportFiles"))))
	{
		$type = "edl";
	}
	else if($filterDesc == (uiRes("m_fileDialogFilterTypes.kOfflineEditAscii")))
	{
		$type = "editMA";
	}
	else if($filterDesc == (uiRes("m_fileDialogFilterTypes.kOfflineEditBinary")))
	{
		$type = "editMB";
	}
	else if($filterDesc == (uiRes("m_fileDialogFilterTypes.kRenderProxy")))
	{
		$type = "mentalRay";
	}
	else if($filterDesc == (uiRes("m_fileDialogFilterTypes.kLightProfile")))
	{
		$type = "ies";
	}

	return $type;
}

global proc string buildDefaultMayaOpenFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter  = $FileDialogFilterTypes[1]; $filter += ";;";
	$filter += $FileDialogFilterTypes[2]; $filter += ";;";
	$filter += $FileDialogFilterTypes[3]; $filter += ";;";
	$filter += $FileDialogFilterTypes[0];

	return $filter;
}

global proc string buildDefaultMayaSaveFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[2]; $filter += ";;";
	$filter += $FileDialogFilterTypes[3];

	return $filter;
}

global proc string buildDefaultReadWriteOfflineEditFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[13]; $filter += ";;";
	$filter += $FileDialogFilterTypes[14];

	return $filter;
}

global proc string buildReadableFileFilterList()
{
	global string $FileDialogFilterTypes[];
	string $fullFilterList;
	int $i, $len;
	string $trans[] = `translator -q -list`, $t;
	$len = size($trans);

    // Put "Maya Scenes (*.ma *.mb) first
    $fullFilterList = $FileDialogFilterTypes[1];

	for ($i=0; $i<$len; $i++)
	{
		$t = $trans[$i];
		if (`translator -q -readSupport $t`)
		{
			if ("mayaAscii" == $t)
			{
				$fullFilterList += ";;";
				$fullFilterList += $FileDialogFilterTypes[2];
			}
			else if ("mayaBinary" == $t)
			{
				$fullFilterList += ";;";
				$fullFilterList += $FileDialogFilterTypes[3];
			}
			else
			{
				string $filters = `translator -q -filter $t`;
				if ("" != $filters)
				{
					string $splitFilters[];
					tokenize $filters ";" $splitFilters;
					
					string $fileFilter = ($t + " ( ");
					for ($filter in $splitFilters)
					{
						$fileFilter += ($filter + " ");
					}
					$fileFilter += ")";

					$fullFilterList += ";;";
					$fullFilterList += $fileFilter;
				}
			}
		}
	}

	$fullFilterList += ";;";
	$fullFilterList += (uiRes("m_fileDialogFilterTypes.kAllFiles"));
	$fullFilterList += " (*)";

	return $fullFilterList;
}

global proc string buildWritableFileFilterList()
{
	global string $FileDialogFilterTypes[];
	string $fullFilterList;
	int $i, $len;
	string $trans[] = `translator -q -list`, $t;
	$len = size($trans);
    string $splitChar = "";
	for ($i=0; $i<$len; $i++)
	{
        if(size($splitChar) == 0 && size($fullFilterList) > 0){
            $splitChar = ";;";
        }
        
		$t = $trans[$i];
		if (`translator -q -writeSupport $t`)
		{
			if ("mayaAscii" == $t)
			{
				$fullFilterList += $splitChar + $FileDialogFilterTypes[2];
			}
			else if ("mayaBinary" == $t)
			{
				$fullFilterList += $splitChar + $FileDialogFilterTypes[3];
			}
			else
			{
				string $filters = `translator -q -filter $t`;
				if ("" != $filters)
				{
					string $splitFilters[];
					tokenize $filters ";" $splitFilters;
					
					string $fileFilter = ($t + " ( ");
					for ($filter in $splitFilters)
					{
						$fileFilter += ($filter + " ");
					}
					$fileFilter += ")";

					$fullFilterList += $splitChar + $fileFilter;
				}
			}
		}
	}

	return $fullFilterList;
}

global proc string buildWritableImageFileFilterList()
{
	python("import maya.app.general.createImageFormats as createImageFormats");
	python("formatManager = createImageFormats.ImageFormats()");
	string $formats[] =  python("formatManager.listFormats(createImageFormats.ImageDescriptor.kIsImage, createImageFormats.ImageDescriptor.filterFormat)");
	string $filter, $f;
	int $i, $len = size($formats);
	if ($len > 0)
	{
		$filter = $formats[0];
		for ($i=1; $i<$len; $i++)
		{
			$filter += ";;";
			$filter += $formats[$i];
		}
	}
	return $filter;
}

global proc string buildImageFileFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[4]; $filter += ";;";
	$filter += $FileDialogFilterTypes[0];

	return $filter;
}

global proc string buildPlugInFileFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[5]; $filter += ";;";
	$filter += $FileDialogFilterTypes[0];

	return $filter;
}

global proc string buildContainerTemplateFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[6]; $filter += ";;";
	$filter += $FileDialogFilterTypes[0];

	return $filter;
}

global proc string buildPixmapFileFilterList()
{
	global string $FileDialogFilterTypes[];

	string $filter;

	$filter += $FileDialogFilterTypes[7]; $filter += ";;";
	$filter += $FileDialogFilterTypes[0];

	return $filter;
}

global proc string[] fileBrowserActionSetup(string $type, int $fileMode) 
{
	global string $FileDialogFilterTypes[];
	string $actionOptions[4];
	//$actionOptions[0] == Localized label for the action button.
	//$actionOptions[1] == File filter to select at the start.  This is the first part of a filter description only.
	//						eg. to select the filter "Maya Files (*.ma *.mb) we would pass "Maya Files".
	//$actionOptions[2] == File filters list.  Each entry is separated by double semi-colons  eg. "All Files (*.*);;Maya Scenes (*.mb *.mb)"

	int $acceptMode = 0;
	if (0 == $fileMode)
	{
		$acceptMode = 1;
	}

	if ($acceptMode == 0)
	{
		$actionOptions[0] = (uiRes("m_projectViewer.kOpen"));
	}
	else
	{
		$actionOptions[0] = (uiRes("m_projectViewer.kSave"));
	}

	if ($type == "image")
	{
		if ($acceptMode == 0)
		{
			$actionOptions[2] = buildImageFileFilterList();
		}
		else
		{
			$actionOptions[2] = buildWritableImageFileFilterList();
		}
	}
	else if ($type == "movie")
	{
		string $filter = $FileDialogFilterTypes[11];
		if ($acceptMode == 0)
		{
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}

		$actionOptions[2] = $filter;
	}	
	else if ($type == "Photoshop")
	{
		string $filter = $FileDialogFilterTypes[8];
		if ($acceptMode == 0)
		{
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}

		$actionOptions[2] = $filter;
	}
	else if ($type == "xmlcache")
	{
		string $filter = $FileDialogFilterTypes[9];
		if ($acceptMode == 0)
		{
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}

		$actionOptions[2] = $filter;
	}
	else if ($type == "mel")
	{
		string $filter = $FileDialogFilterTypes[15];
		if ($acceptMode == 0)
		{
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}

		$actionOptions[2] = $filter;
	}
	else if($type == "edl")
	{
		string $filter; 
		
		if ($acceptMode == 0)
		{
			$filter = $FileDialogFilterTypes[10];
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}
		else
		{
			$filter = $FileDialogFilterTypes[16];		
		}
		
		$actionOptions[2] = $filter;
	}
	else if($type == "map")
	{
		if ($acceptMode != 0)
		{
			$actionOptions[2] = $FileDialogFilterTypes[12];
		}
		else
		{
			string $filter = $FileDialogFilterTypes[19];
			$filter += ";;";
			$filter += buildImageFileFilterList();
			$actionOptions[2] = $filter;
		}
	}
	else if ($type == "MIRenderProxy")
	{
		string $filter = $FileDialogFilterTypes[17];
		if ($acceptMode == 0)
		{
			$filter += ";;";
			$filter += $FileDialogFilterTypes[0];
		}

		$actionOptions[2] = $filter;
	}
	else if ($type == "ies")
	{
		string $filter = $FileDialogFilterTypes[18];
		$filter += ";;";
		$filter += $FileDialogFilterTypes[0];
		$actionOptions[2] = $filter;
	}
	else if ($acceptMode == 0)
	{
		$actionOptions[2] = buildReadableFileFilterList();
	}
	else
	{
		$actionOptions[2] = buildWritableFileFilterList();
	}


	if ( $type == "mayaBinary")
	{
        $actionOptions[1] = uiRes("m_fileDialogFilterTypes.kMayaBinary");
	}
	else if ( $type == "mayaAscii")
	{
        $actionOptions[1] = uiRes("m_fileDialogFilterTypes.kMayaAscii");
	}
	else if ( $type == uiRes("m_fileOptions.kBestGuess"))
	{
        $actionOptions[1] = uiRes("m_fileDialogFilterTypes.kAllFiles");
	}
	else if ( $type == "image")
	{
		string $type = uiRes("m_fileDialogFilterTypes.kImageFiles");

		if ($acceptMode != 0 && `optionVar -exists lastSaveImageAsFileType`) 
		{
			$type = `optionVar -q lastSaveImageAsFileType`;
		}

		$actionOptions[1] = $type;
	}
	else if ( $type == "movie")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kMovieFiles");;
	}
	else if ( $type == "Photoshop")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kPhotoshopFiles");;
	}
	else if ( $type == "xmlcache")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kXMLCacheFiles");;
	}
	else if ( $type == "mel")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kMelFiles");;
	}
	else if ( $type == "edl")
	{
	
		if ($acceptMode == 0)
		{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kEDLFiles");
	}
		else
		{
		 	$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kEDLExportFiles");
		}		
	}
	else if ( $type == "iff")
	{
		string $iffFilters = ("iff (*.iff);;");
		$iffFilters += (uiRes("m_fileDialogFilterTypes.kAllFiles"));
		$iffFilters += " (*)";
		$actionOptions[2] = $iffFilters;
		$actionOptions[1] = ("iff");
	}
	else if ( $type == "MIRenderProxy")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kRenderProxy");;
	}
	else if ( $type == "ies")
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kLightProfile");;
	}
	else if ("" != $type)
	{
		$actionOptions[1] = $type;
	}
	else
	{
		$actionOptions[1] = uiRes("m_fileDialogFilterTypes.kAllFiles");
	}

	return $actionOptions;
}
