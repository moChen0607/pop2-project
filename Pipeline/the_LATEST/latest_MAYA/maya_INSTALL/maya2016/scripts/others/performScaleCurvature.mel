// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  2003
//
//  Description:
//	     Option box for scaleCurvature menu.	
//


//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//
proc setOptionVars(int $forceFactorySettings)
{
	if($forceFactorySettings || !`optionVar -exists scaleCurvatureScaleFactor`)
	{
		optionVar -floatValue  scaleCurvatureScaleFactor 1.1;
	}

	if($forceFactorySettings || !`optionVar -exists scaleCurvatureMaxCurvature`)
	{
		optionVar -floatValue  scaleCurvatureMaxCurvature 0.8;
	}
}

//
//  Procedure Name:
//      performScaleCurvatureSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//		forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc performScaleCurvatureSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	floatSliderGrp -edit 
		-v `optionVar -query scaleCurvatureScaleFactor`
		scaleCurvatureScaleFactor;

	floatSliderGrp -edit 
		-v `optionVar -query scaleCurvatureMaxCurvature`
		scaleCurvatureMaxCurvature;

}

//
//  Procedure Name:
//      performScaleCurvatureCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//		doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc performScaleCurvatureCallback(string $parent, int $doIt)
{
    setParent $parent;

	optionVar -floatValue scaleCurvatureScaleFactor
		`floatSliderGrp -query -v scaleCurvatureScaleFactor`;

	optionVar -floatValue scaleCurvatureMaxCurvature
		`floatSliderGrp -query -v scaleCurvatureMaxCurvature`;

	if( $doIt ) {
		performScaleCurvature 0; 
	}
}


//
//  Procedure Name:
//      scaleCurvatureOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc scaleCurvatureOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "performScaleCurvature";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	string $layout = getOptionBox();
	setParent $layout;
	
	setUITemplate -pushTemplate DefaultTemplate;

	tabLayout -tv false -scr true;
	
	string $parent = `columnLayout -adjustableColumn 1`;

    floatSliderGrp
		-label (uiRes("m_performScaleCurvature.kScaleFactor"))
		-field 1
		-min 0 
		-max 2.0
		-fieldMinValue -100000.0
		-fieldMaxValue 100000.0
		-pre 3
		scaleCurvatureScaleFactor;

    floatSliderGrp
		-label (uiRes("m_performScaleCurvature.kMaxCurvature"))
		-field 1
		-min 0.0 
		-max 1.0
		-fieldMinValue -100000.0
		-fieldMaxValue 100000.0
		-pre 3
		scaleCurvatureMaxCurvature;


	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performScaleCurvature.kScaleCurvature"))
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Set the option box title.
	//
	setOptionBoxTitle (uiRes("m_performScaleCurvature.kScaleCurvatureOptions"));
	setOptionBoxCommandName($commandName);
	setOptionBoxHelpTag( "ScaleCurvature" );

	// Pop the UI template
	//
	setUITemplate -popTemplate;

	//	Set the current values of the option box.
	//
	eval( $setup + " " + $parent + " " + 0 );	
	
	//	Show the option box.
	//
	showOptionBox();
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc string assembleCmd()
{
	setOptionVars(false);

	string $cmd;

	string $cmd = ("modifySelectedCurves curvature " 
					+ `optionVar -query scaleCurvatureScaleFactor` +  " "
					+ `optionVar -query scaleCurvatureMaxCurvature` );

	return $cmd;
}

//
//  Procedure Name:
//      performScaleCurvature
//
//  Description:
//		Perform the command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the command with the current 
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performScaleCurvature(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command from option settings.
		//
		case 0:
			setOptionVars(false);
			$cmd = `assembleCmd`;
			eval($cmd);
			break;

		//	Show the option box.
		//
		case 1:
			scaleCurvatureOptions();
			break;

		//	Return the command string.
		//
		case 2:
			setOptionVars (false);
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

