// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  Jan, 2000
//
//	Procedure Name:
//		doNormalizeWeightsArgList
//
//	Description:
//		For smooth skinned objects, enable or disable weight normalization,
//		or normalize the weights.
//
//	Input Arguments:
//	$version: The version of this option box.  Used to know how to 
//	interpret the $args array.
//  
//	$args
//	Version 1
//		$args[0] : action: 	1 = disable normalization,
//							2 = enable normalization, (interactive)
//							3 = enable post normalization
//							4 = normalize weights on selected items
//							
//
//	Return Value:
//		None
//

global proc int doNormalizeWeightsArgList( string $version, string $args[] )
{
    string $currentSelection[] = `ls -selection`;
	int $mode = $args[0];

	int $foundSkin = 0;

	for ($sel in $currentSelection) {
		// find source and destination skin shapes
		//
		string $buff[];
		tokenize($sel,".",$buff);
		string $srcCluster = findRelatedSkinCluster($buff[0]);

		if ("" != $srcCluster) {
		
			string $cmd;
			$foundSkin = 1;

			if (1 == $mode) {
				// disable the normalize
				$cmd = ("setAttr "+$srcCluster+".normalizeWeights 0");
			} else if (2 == $mode) {
			    // interactive normalize
				$cmd = ("setAttr "+$srcCluster+".normalizeWeights 1");
			} else if (3 == $mode) {
			    // post normalize
				$cmd = ("setAttr "+$srcCluster+".normalizeWeights 2");				
			} else {
				// use the skin percent normalization
				$cmd = ("skinPercent -normalize true "+$srcCluster+";");
			}
			evalEcho($cmd);
		}
	}

	select -r $currentSelection;
	if (! $foundSkin) {
		error( (uiRes("m_doNormalizeWeightsArgList.kSelectSkin")));
	}

	return 1;
}
