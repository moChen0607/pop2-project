// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

// Description:  This procedure is called to set the hyper graph open
//      mode to the one user specified.
//
global proc setHyperGraphOpenMode(string $parent, string $type)
{
    setParent $parent;

    if (`radioButtonGrp -query -select useFirstExistingOneRadio`)
    {
        optionVar -intValue ($type+"OpenHyperGraphMode") 0;
    }
    else
    {
        optionVar -intValue ($type+"OpenHyperGraphMode") 1;
    }
}

// Description:  This procedure is called to reset the hyper graph
//      open mode to its default mode.
//
global proc resetHyperGraphOpenMode (string $parent, string $type)
{
    string $oldParent = `setParent -query`;

    setParent $parent;
    initializeOpenHyperGraphMode($type);
    int $mode = `optionVar -query ($type+"OpenHyperGraphMode")`;
    if ($mode == 0)
    {
        radioButtonGrp -edit -select 1 useFirstExistingOneRadio;
    }
    else if ($mode = 1)
    {
        radioButtonGrp -edit -select 1 createNewHyperGraphRadio;
    }

    setParent $oldParent;
}

// Description:  This procedure lets the user select what they want 
//      the "Hyper graph ..." menuItem's default behavior to be.
// 
//      Mode 0: default mode, use the first existing hyper graph
//              panel of the given type.
//      Mode 1: create a new hyper graph panel of the given type.
//
global proc hyperGraphOptions(string $type)
{
    string $layout = getOptionBox();
    setParent $layout;
    setUITemplate -pushTemplate DefaultTemplate;

    string $parent = `columnLayout -adjustableColumn 1`;
    
    radioButtonGrp -numberOfRadioButtons 1 
        -label (uiRes("m_hyperGraphOptions.kHyperGraphOption"))
        -label1 (uiRes("m_hyperGraphOptions.kCreateNew"))
        -changeCommand ("setHyperGraphOpenMode "+$parent+" "+$type)
        createNewHyperGraphRadio;

    radioButtonGrp -numberOfRadioButtons 1
        -label1 (uiRes("m_hyperGraphOptions.kFirstExistingOne"))
        -shareCollection createNewHyperGraphRadio
        -changeCommand ("setHyperGraphOpenMode "+$parent+" "+$type)
        useFirstExistingOneRadio;

    // Set the radio button when open the option box.
    //
    int $mode = 0;
    if (!`optionVar -exists ($type+"OpenHyperGraphMode")`)
    {
        initializeOpenHyperGraphMode($type);
    }
    $mode = `optionVar -query ($type+"OpenHyperGraphMode")`;

    switch ($mode)
    {
        case 1:
            radioButtonGrp -edit -select 1 createNewHyperGraphRadio;
            break;
        default:
            radioButtonGrp -edit -select 1 useFirstExistingOneRadio;
            break;        
    }

    setUITemplate -popTemplate;

    // Specify the standard buttons in the option box.
    //
    string $applyBtn = getOptionBoxApplyBtn();
    button -edit
        -command("setHyperGraphOpenMode "+$parent+" "+$type+"; hyperGraphWindow \"\" "+$type)
        $applyBtn;

    string $saveBtn = getOptionBoxSaveBtn();
    button -edit -command("setHyperGraphOpenMode "+$parent+" "+$type)
        $saveBtn;

    string $resetBtn = getOptionBoxResetBtn();
    button -edit -command ("resetHyperGraphOpenMode "+$parent+" "+$type)
        $resetBtn;

	string $title = (uiRes("m_hyperGraphOptions.kOptions"));
    setOptionBoxTitle `format -s $type $title`;
    setOptionBoxHelpTag("HyperGraphOptions");
    showOptionBox();    
}
