// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//////////////////////////////////////////////////////////////////////
//
//  Procedure Name: 
//		createNewOverride
//
//  Description:
//		This procedure is provided as a hook for customers to allow
//		you to redefine the behaviour of the "create new" button on
//		attributes.
//		
//  Input Arguments:
//		$dest - the name of the plug to which the newly created node is to 
//				be connected.
//		$force - determines whether or not existing connections should be
//				 overwritten by newly created ones
//
//  Return Value:
//		Return 1 if you want Maya to perform the default creation behaviour
//		for the plug.  Return 0 if you have chosen to implement a different 
//		behaviour for this connection and don't want the normal operation to 
//		be performed as well.
//
global proc int createNewOverride( string $dest, int $force )
{
	int $doNormalOperation = 1;

	string $destAttr[];
	int $numTokens = tokenize( $dest, ".", $destAttr ); 

	string $node = "";
	string $attr = "";

	if( $numTokens != 2 )
	{
		return $doNormalOperation;
	}
	else
	{ 
		$node = $destAttr[0];
		$attr = $destAttr[1];
	}

	if ("dx11Shader" == `nodeType $node` && `attributeExists ($attr + "_Type") $node`)
	{
		string $type = `getAttr ($node + "." + $attr + "_Type")`; 
		if ("texture" == $type)
		{
			$doNormalOperation = 0;

			string $cmd = "defaultNavigation -force true -connectToExisting -source %node -destination " + $dest;
			createRenderNodeCB -as2DTexture "" file $cmd; 
		} 
	}
	else if(  $attr == "contourContrast"
		 || $attr == "contourStore" )
	{
		$doNormalOperation = 0;
		string $navCmd = ( "defaultNavigation -connectToExisting -destination "
							+ $dest
							+ " -source %node" );
		createRenderNode -allWithMentalUp $navCmd "";
	}

	return $doNormalOperation;
}
