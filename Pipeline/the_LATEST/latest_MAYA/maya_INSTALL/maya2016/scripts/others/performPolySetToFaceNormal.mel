// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  Dec 3, 1998
//
//  Procedure Name:
//      performPolySetToFaceNormal
//
//  Description:
//      Perform an operation that set selected vertex or vertex-face normals
//		to their face normals
//         
//

proc setOptionVars (int $forceFactorySettings)
{			   
	if ($forceFactorySettings || !`optionVar -exists polySetToFaceNormalUserNormal`)
		optionVar -intValue polySetToFaceNormalUserNormal 0;
}

global proc performPolySetToFaceNormalSetup (string $parent, int $forceFactorySettings)
{
	setOptionVars($forceFactorySettings);
	setParent $parent;

	int $ival;

	$ival = `optionVar -q polySetToFaceNormalUserNormal`;
	$ival++;	// radioButtonGrp is 1 based
	radioButtonGrp -edit -select $ival polySetToFaceNormalUserNormal;	
}

global proc performPolySetToFaceNormalCallback (string $parent, int $doIt)
{
	setParent $parent;

	int $ival;
	
	$ival = `radioButtonGrp -query -select polySetToFaceNormalUserNormal`;
	$ival--;	// radioButtonGrp is 1 based
	optionVar -intValue polySetToFaceNormalUserNormal $ival;

	if ($doIt) {
		performPolySetToFaceNormal 0;
		addToRecentCommandQueue "performPolySetToFaceNormal 0" "PolySetToFaceNormal";
	}
}

proc polySetToFaceNormalOptions()
{
	// Global template variables for form spacing
	global int $gOptionBoxTemplateFrameSpacing;
	
	string $commandName = "performPolySetToFaceNormal";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");
	   
	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate OptionBoxTemplate;
	waitCursor -state 1;

	// Form layout
	string $parent = `formLayout polySetToFaceNormalOptions`;
	
		// Settings frame
		string $settingsFrame = 
		`frameLayout -label (uiRes("m_performPolySetToFaceNormal.kSettingsFrame"))`;

			columnLayout;
	
				radioButtonGrp 
					-numberOfRadioButtons 2
					-label 	(uiRes("m_performPolySetToFaceNormal.kCreateVertexFaceNormals"))
					-label1	(uiRes("m_performPolySetToFaceNormal.kUnlockVertexFaceNormals"))
					-label2	(uiRes("m_performPolySetToFaceNormal.kMatchFaceNormal"))
					-vertical
					polySetToFaceNormalUserNormal;

		setParent $parent;	// frameLayout
		
	setParent ..; // formLayout

	// Attach frame to form layout
	formLayout -edit
		-attachForm $settingsFrame "top" $gOptionBoxTemplateFrameSpacing
		-attachForm $settingsFrame "left" $gOptionBoxTemplateFrameSpacing
		-attachForm $settingsFrame "right" $gOptionBoxTemplateFrameSpacing
		-attachNone $settingsFrame "bottom"
	$parent;

	waitCursor -state 0;
	setUITemplate -popTemplate;
	   
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performPolySetToFaceNormal.kSetToFaceButton"))
	       -command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;
			 
	setOptionBoxTitle( (uiRes("m_performPolySetToFaceNormal.kSetToFaceNormalOptions")) );

	setOptionBoxHelpTag( "NormalsSetToFace" );

	eval (($setup + " " + $parent + " " + 0));      
	showOptionBox();
}

global proc string performPolySetToFaceNormal (int $option)
{
	string $cmd="";
	switch ($option) {

	  case 1: polySetToFaceNormalOptions; 
		// Just the option box
		break;
		
	  default:
		setOptionVars(false);

		int $val1 = `optionVar -q polySetToFaceNormalUserNormal`;

		if ($val1)
			$cmd = ("polySetToFaceNormal -setUserNormal ");
		else
			$cmd = ("polySetToFaceNormal ");

		if ($option == 0)
			evalEcho $cmd;
		break;
	}
	return $cmd;
}





