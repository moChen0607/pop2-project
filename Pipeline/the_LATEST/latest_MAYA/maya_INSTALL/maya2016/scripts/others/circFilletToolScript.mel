// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      circFilletToolScript
//
//  Description:
//	create wrapper around the circFillet command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc circFilletToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;
	if( $forceFactorySettings || !`optionVar -ex circFilletEuc` ) {
		optionVar -iv circFilletEuc 0;
	}
	if( $forceFactorySettings || !`optionVar -ex circFilletLac` ) {
		optionVar -iv circFilletLac $gStartWithAutoComplete;
	}
	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q circFilletEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q circFilletLac` $goToTool;
	}
}

global proc circFilletValues ( string $toolName ) 
{
	performCircularFillet 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc circFilletProperties()
{
	scriptCtxCommonProperties;
}

proc createCircFilletContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		circFilletToolSetup( 0, "" );
		scriptCtx -i1 "surfaceFillet.png"
		  -title (uiRes("m_circFilletToolScript.kCircularFilletTool"))
		  -bcn "circFillet"

		  -exitUponCompletion `optionVar -q circFilletEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("circFilletToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_circFilletToolScript.kSelectTwoSurfaces")) )
		  -setSelectionPrompt ((uiRes("m_circFilletToolScript.kSelectSecondSurface")) )
		  -setDoneSelectionPrompt ((uiRes("m_circFilletToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q circFilletLac`
		  -showManipulators true

		  -setSelectionCount 2
		  -isoparm true
		  -surfaceParameterPoint true
		$tool;
	}
}

global proc string circFilletToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolCircFillet` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "circFilletContext";
			createCircFilletContext $tool;
		}
	}
	return scriptToolScript( "performCircularFillet", $setToTool, $tool );
}
