// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  April, 2006
//
//	Procedure Name:
//		doBakeNonDefHistory
//
//	Description:
//		Delete non-deformer history.
//
//	Input Arguments:
//	$version: The version of this option box.  Used to know how to 
//	interpret the $args array.
//  
//	$args
//	Version 1
//		$args[0] : action: 	"pre" = preDeformers
//							"prePost" = prePostDeformers
//
//	Return Value:
//		None
//

proc string[] cachePointCountDifference(string $cacheFiles[], string $geom)
{
	string $result[];
	int $pointCount = 0;
	if (nodeType($geom) == "mesh") {
		int $pc[] = `polyEvaluate -vertex $geom`;
		$pointCount = $pc[0];
	} else if (nodeType($geom) == "nurbsSurface") {
		string $sel[] = `ls -sl`;
		select -r ($geom+".cv[*][*]");
		string $allPts[] = `ls -sl -flatten`;
		$pointCount = size($allPts);
		select -r $sel;
	}
	if ($pointCount > 0) {
		for ($cache in $cacheFiles) {
			int $cacheCount[] = `cacheFile -channelName $geom -q -pc $cache`;
			if (size($cacheCount) > 0) {
				if ($cacheCount[0] != $pointCount) {
					$result[size($result)] = $cache;
				}
			}
		}
	}
	return $result;
}

global proc doBakeNonDefHistory( string $version, string $args[] )
{
    string $currentSelection[] = `ls -selection`;
	string $mode = $args[0];

	if ($mode == "prePost") {
		string $topChangeGeoms[];
		string $topChangeCaches[];
		int $count = 0;

		for ($sel in $currentSelection) {
			string $geoms[] = getGeometriesToCache();
			for ($geom in $geoms) {
				string $cfs[] = findExistingCaches($geom);
				if (size($cfs) > 0) {
					string $diffs[] = cachePointCountDifference($cfs,$geom);
					if (size($diffs) > 0) {
						$topChangeGeoms[$count] = $geom;
						$topChangeCaches = stringArrayCatenate($topChangeCaches,$diffs);
						$count++;
					}
				}
			}
		}

		if ($count > 0) {
			string $geomsWithChange = (uiRes("m_doBakeNonDefHistory.kInvalidateDiskCache"));
			$geomsWithChange += "\n      ";
			for ($ii = 0; $ii < $count; $ii++) {
				$geomsWithChange += $topChangeGeoms[$ii];
				if ($ii != $count-1) {
					$geomsWithChange += ", ";
				}
			}
			string $aheadOnly = (uiRes("m_doBakeNonDefHistory.kPreOnly"));
			$geomsWithChange += ("\n"+$aheadOnly);
			string $preOnly = (uiRes("m_doBakeNonDefHistory.kDeletePre"));
			string $all = (uiRes("m_doBakeNonDefHistory.kDeleteAll"));
			string $cancel = (uiRes("m_doBakeNonDefHistory.kCancel"));
			string $confirm = `confirmDialog
				-title (uiRes("m_doBakeNonDefHistory.kWarningCache"))
				-message $geomsWithChange -button $preOnly -button $all
				-button $cancel
				-defaultButton $preOnly
				-cancelButton $cancel`;
			if ($confirm == $cancel) {
				return;
			} else if ($confirm == $preOnly) {
				$mode = "pre";
			} else {
				delete $topChangeCaches;
			}
		}
	}

	string $cmd = "bakePartialHistory ";

	if( $mode == "pre" ) {
		$cmd += "-preDeformers";
	} else {
		$cmd += "-prePostDeformers";
	}

	if (`optionVar -exists bakeNonDefHistorySmooth`) {
		int $smooth = `optionVar -q bakeNonDefHistorySmooth`;
		$cmd += (" -postSmooth "+$smooth);
	}
	
	eval($cmd);
}
