// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Creation Date:  4 April 1997
//
//  Procedure Name:
//      performPolySewEdge
//
//  Description:
//        sew the selected polygon border edges
//         
//  Input Arguments:
//        $option : Whether to set the options to default values.
//  Return Value:
//        command string iff $option==2
//

proc setOptionVars (int $forceFactorySettings)
{			   
	if ($forceFactorySettings || !`optionVar -exists polySewEdgeMap`)
		optionVar -intValue polySewEdgeMap 1;
	
	if ($forceFactorySettings || !`optionVar -exists polySewEdge`)
		optionVar -floatValue polySewEdge 0.0;

	if ($forceFactorySettings || !`optionVar -exists polySewEdgeWorldSpace`)
        optionVar -intValue polySewEdgeWorldSpace 1;
}

global proc performPolySewEdgeSetup (string $parent, int $forceFactorySettings)
{
	setOptionVars($forceFactorySettings);
	setParent $parent;
	
	int $iSM = `optionVar -query polySewEdgeMap`;
    checkBoxGrp -edit -value1 $iSM polySewEdgeMap;
	
	float $fval = `optionVar -query polySewEdge`;
	floatSliderGrp -edit -value $fval polySewEdge;
	
	int $iWS = `optionVar -query polySewEdgeWorldSpace`;
    checkBoxGrp -edit -value1 $iWS polySewEdgeWorldSpace;
}

global proc performPolySewEdgeCallback (string $parent, int $doIt)
{
	setParent $parent;
    optionVar -intValue polySewEdgeMap
        `checkBoxGrp -query -value1 polySewEdgeMap`;
	optionVar -floatValue polySewEdge 
		`floatSliderGrp -query -value polySewEdge`;
    optionVar -intValue polySewEdgeWorldSpace
        `checkBoxGrp -query -value1 polySewEdgeWorldSpace`;
	if ($doIt) {
		performPolySewEdge 0;
		addToRecentCommandQueue "performPolySewEdge 0" "PolySewEdge";
	}
}

proc polySewEdgeOptions ()
{
	string $commandName = "performPolySewEdge";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");
	   
	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;

    checkBoxGrp -label1 (uiRes("m_performPolySewEdge.kMergeUVs")) polySewEdgeMap;

	floatSliderGrp -label (uiRes("m_performPolySewEdge.kThreshold")) -min 0 -max 10 -fmx 10000000 polySewEdge;

    checkBoxGrp -label1 (uiRes("m_performPolySewEdge.kWorldSpaceCoords")) polySewEdgeWorldSpace;

	waitCursor -state 0;
	setUITemplate -popTemplate;
	   
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label (uiRes("m_performPolySewEdge.kSew"))
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	setOptionBoxTitle( (uiRes("m_performPolySewEdge.kOptionBoxTitle")) );
	
	setOptionBoxHelpTag( "MergeMultipleEdges" );

	eval (($setup + " " + $parent + " " + 0));      
	showOptionBox();
}

global proc string performPolySewEdge (int $option)
{
	string $cmd="";
	string $sel[];
	switch ($option) {
	case 0:
		setOptionVars(false);
		float $fval = `optionVar -query polySewEdge`;
        int $ivSM = `optionVar -query polySewEdgeMap`;
        int $ivWS = `optionVar -query polySewEdgeWorldSpace`;
		$cmd=("polySewEdge -t " + $fval + " -tx " + $ivSM + " -ws " + $ivWS);
		polyPerformAction $cmd e 0;
		break;
	case 1: polySewEdgeOptions; break;
	default:
		setOptionVars(false);
		float $fval = `optionVar -query polySewEdge`;
        int $ivSM = `optionVar -query polySewEdgeMap`;
        int $ivWS = `optionVar -query polySewEdgeWorldSpace`;
		$cmd=("polySewEdge -t " + $fval + " -tx " + $ivSM +" -ws " + $ivWS);
		$cmd = ("polyPerformAction \"" + $cmd + "\" e 0");
	}
	return $cmd;
}
