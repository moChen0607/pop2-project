// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//
//  Description:
//      Functions called from "performSubdivMirror".
//

//
//  Procedure Name:
//      doSubdivMirrorArgList
//
//  Description:
//		This is the actual function that gets called from "Subd Mirror" 
//      option box.
//
//  Input Arguments:
//	    $version: The version of this option box.  This is used to know how to 
//	              interpret the $args array.
//
//      history (args[0])
//      orig object (args[1])
//      x axis (args[2])
//      y axis (args[3])
//      z axis (args[4])
//
//  Return Value:
//      None.
//
//  Note:
//
global proc int doSubdivMirrorArgList( string $version, string $args[] )
{
	int $status = 0;

	if( size($args) < 5 ) {
		string $msg = (uiRes("m_doSubdivMirrorArgList.kWrongNumberOfArgs"));
		error($msg);
		$status = 1;
		return $status;
	}

	global int $gSelectSubdivSurface;

	// collapse all subd's
	string $list[] = `filterExpand -ex 1 -fp 1 -sm $gSelectSubdivSurface`; 
	int $len = size($list);

	if ($len == 0) {
		string $msg = (uiRes("m_doSubdivMirrorArgList.kSelectError"));
		error ($msg);
		$status = 1;
		return $status;
	}

	int $origObjectAction = $args[1]; 
	int $globalHist = $args[0];

	string $cmd = "subdMirror";

	switch ($origObjectAction) {
	  case 1:
		// replace original object, forces history off, but delete
		// will take care of that...
		$cmd = $cmd + " -ch off ";
		break;

	  case 2:
		// hide original object, forces history on
		$cmd = $cmd + " -ch on ";
		if( !$globalHist ) {
			string $msg = (uiRes("m_doSubdivMirrorArgList.kHistoryWarning"));
			warning($msg);
		}
		break;

	  case 3:
	  default:
		// show original object, respects global construction history
		$cmd = $cmd + " -ch " + $globalHist + " ";
		break;
	}

	$cmd += (" -xm " + $args[2] + " " );
	$cmd += (" -ym " + $args[3] + " " );
	$cmd += (" -zm " + $args[4] + " " );

	//  Convert each subdiv, one at a time so we can catch errors
	//
  	string $result[];
	string $selList = "";

	for($i = 0; $i < $len; $i++) {
		if ( (1 == `getAttr ($list[$i]+".intermediateObject")`) ||
			 (0 == `getAttr ($list[$i]+".visibility")`)) {
			continue;
		}
		$retVal = catch ($result = evalEcho ($cmd + $list[$i]));

		// if there's no error, do post work and add results 
		if ($retVal != 1){
			switch ($origObjectAction){
			  case 1:
				evalEcho ("delete "  + $list[$i]);
				break;
			  case 2:
				evalEcho ("hide " + $list[$i]);	  
				break;
			  default:
				break;
			}

			for ($s in $result) {
				$selList += ($s + " ");
			}
		}
	}

	if (size($selList) > 0){
  		$selList += ";";
		eval ("select -r " + $selList);
	}

	return $status;
}


