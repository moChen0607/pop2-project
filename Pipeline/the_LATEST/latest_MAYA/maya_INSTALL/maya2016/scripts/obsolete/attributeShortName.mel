// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
// Description:
//		Helper proc to return short name of attr, when attr
//		is in any of the following forms (note that the right-hand
//		column uses short name format for brevity; $longName in 
//		this case can be either shortName or longName...)
//		
//		child: 							translateX
//		child of compound: 				translate.translateX
//		mixed short+long: 				t.translateX
//		child of instanced multi: 		cdvm[0].cdvmp
//		child of non-instanced multi: 	cdvm.cdvmp
//		
global proc string attributeShortName( string $nodeName, string $longName ) {
	string $shortName;
	string $buffer[];

	tokenize $longName ".[" $buffer;
	for( $b in $buffer ) {
		// Don't want to attempt to find shortNames of multi-indices
		//
		if( substring($b, size($b), size($b)) == "]" ) {
			$shortName += ( "[" + $b );
			continue;
		}

		if( size( $shortName ) ) {
			$shortName += ".";
		}

		$shortName += `attributeQuery -node $nodeName -shortName $b`;			
	}

	if( !size( $shortName ) ) {
		// Hard-coded string OK for obsolete scripts
		string $fmt = "^1s is not an attribute of ^2s";
		error `format -s $longName -s $nodeName $fmt`;
	}


	return $shortName;
}
