// Copyright (C) 1997-2014 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
// Description :
//		To make a polygonal tetrahedron.
//		given the length of the edge.
//

proc float boundingSphereRadius( float $side )
{
	float $rad ;
	$rad = $side / sqrt(3.0) ;	
	return $rad ;
}
global proc int polyTetrahedron( float $side )
{
	float $r = boundingSphereRadius( $side ) ;

	float $vx[4] ;
	float $vy[4] ;
	float $vz[4] ;

	$vx[0] = $r ; 
	$vy[0] = 0.0 ; 
	$vz[0] = 0.0 ; 

	$vx[1] = -0.5 * $r ; 
	$vy[1] = 0.5 * sqrt(3.0) * $r ; 
	$vz[1] = 0.0 ; 

	$vx[2] = $vx[1] ; 
	$vy[2] = -1.0 * $vy[1]; 
	$vz[2] = 0.0 ; 

	$vx[3] = 0.0 ; 
	$vy[3] = 0.0; 
	$vz[3] = sqrt( $side* $side -$r * $r ) ;


	// create poly facets.
	//
	string $facet1[] ;
	$facet1 = `polyCreateFacet -ch 0 -p $vx[3] $vy[3] $vz[3] -p $vx[0] $vy[0] $vz[0] -p $vx[1] $vy[1] $vz[1]` ; 
	string $facet2[] ;
	$facet2 = `polyCreateFacet -ch 0 -p $vx[3] $vy[3] $vz[3] -p $vx[1] $vy[1] $vz[1] -p $vx[2] $vy[2] $vz[2]` ; 
	string $facet3[] ;
	$facet3 = `polyCreateFacet -ch 0 -p $vx[3] $vy[3] $vz[3] -p $vx[2] $vy[2] $vz[2] -p $vx[0] $vy[0] $vz[0]` ; 
	string $facet4[] ;
	$facet4 = `polyCreateFacet -ch 0 -p $vx[0] $vy[0] $vz[0] -p $vx[2] $vy[2] $vz[2] -p $vx[1] $vy[1] $vz[1]` ; 

	// unite.
	//
	string $tetrahedron[] ;
	$tetrahedron = `polyUnite -ch 0 $facet1[0] $facet2[0] $facet3[0] $facet4[0]` ;

	select -r $tetrahedron[0] ;
	return 0 ;
}

