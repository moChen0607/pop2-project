#!/bin/csh -f
#Tag 0x00C98a02
#*
#*+***********************************************************************
#*
#*  Module:
#*	shell wrapper to set environment variables before exec of real function
#*
#*-***********************************************************************
#*


set real_exec = `basename $0`.bin
set real_base = `basename $0`

#
#  Determine if launched from the desktop or a shell.  This will tell
#  us how to display error messages later.
#

set tty = 1

if ( -f /usr/bin/uname ) then
    set OSname = `/usr/bin/uname -s`
else
    set OSname = `/bin/uname -s`
endif

switch ("$OSname")
    case Linux:
    case Darwin:
        set lib = lib
        set lsFlags = '-l'
        breaksw

    case IRIX*:
        set lib = lib32
        set libn32 = 1
        set lsFlags = '-Hl'
        if ( -x /bin/tty ) then
            if ( "`tty`" !~ /dev/* ) then
                set tty = 0
            endif
        endif
		breaksw

    default:
        echo "${real_base} is not supported on $OSname."
        exit 1
        breaksw
endsw

#
#  If the MAYA_LOCATION is set, use it.  Otherwise, determine it.
#

if ( $?MAYA_LOCATION ) then
    if ( -x ${MAYA_LOCATION}/bin/${real_exec} ) then
	set real = ${MAYA_LOCATION}/bin/${real_exec}
    else
	unsetenv MAYA_LOCATION
    endif
endif


#
#  Need to determine where Maya resides based on how this shell was
#  invoked.  Determine full pathname, follow soft link, and extract
#  directory.  If the "real" executable is not in a "bin" directory,
#  someone's been screwing around with the distribution, so bail.
#

if ( ! $?MAYA_LOCATION ) then
    #
    #  Create the full pathname of the invoked binary.
    #

    if ( "$0" =~ /* ) then
	set me = "$0"
    else
	set me = `pwd`/$0
    endif


    #
    #  If it's a link, find the actual file.
    #

    while ( -l $me )
		set linkdirname = `dirname $me`
		set me = `/bin/ls $lsFlags $me | tr ' ' '\012' | tail -n 1`
		if ( "$me" !~ /* ) then
	    	set me = $linkdirname/$me
		endif
    end


    #
    #  Binary should be in $MAYA_LOCATION/bin.  Verify that.
    #

    set bindir = `dirname $me`
    if ( -d $bindir ) then
	set bindir = `cd $bindir; echo $cwd`
    endif
    set binfile = `basename $bindir`
    if ( "$binfile" =~ "bin" ) then
	setenv MAYA_LOCATION `dirname $bindir`
	if ( -x ${MAYA_LOCATION}/bin/${real_exec} ) then
	    set real = ${MAYA_LOCATION}/bin/${real_exec}
	else
	    unsetenv MAYA_LOCATION
	endif
    endif
endif

if ( $?MAYA_LOCATION ) then
    #
    #  Set environment, and launch the real executable.
    #

    if ( $OSname == "Darwin" ) then
        setenv	DYLD_LIBRARY_PATH	$MAYA_LOCATION/MacOS
    else
        set path = ($MAYA_LOCATION/bin $path)

        if ( $?libn32 && $?LD_LIBRARYN32_PATH ) then
            setenv	LD_LIBRARYN32_PATH	$MAYA_LOCATION/lib:$LD_LIBRARYN32_PATH
        else if ( $?LD_LIBRARY_PATH ) then
            setenv	LD_LIBRARY_PATH		$MAYA_LOCATION/lib:$LD_LIBRARY_PATH
        else
            setenv	LD_LIBRARY_PATH		$MAYA_LOCATION/lib
        endif
    endif
endif

#
#  This allows IMF to save SGI format image files with matte.

setenv WF_IMF_SGI_MATTE

#
#   101914: color correction needed for Cineon files.

setenv WF_IMF_CIN_CORRECTION both
setenv WF_IMF_CIN_WHITE_POINT 685


if ( $?MAYA_LOCATION ) then
    if ( -x $real ) then
	$real $argv:q
	exit $status
    endif
endif

if ( ! $?real ) set real = ${real_exec}

if ( $tty == 1 ) then
    echo The executable \(\`${real}\'\) cannot be found
else
    xconfirm -c -t "The executable \(\`${real}\'\) cannot be found" \
	    -icon warning >&/dev/null
endif
exit 1

#*+
#* ==========================================================================
#* The information  in  this  file is  provided for the  exclusive use of the
#* licensees of Alias Systems Corp. Such users have the right to use, modify,
#* and  incorporate this code  into  other  products  for purposes authorized
#* by the  Alias Systems Corp. license agreement, without fee.
#*
#* Alias Systems Corp. disclaims all warranties with regard to this software,
#* including all implied warranties  of  merchantability and  fitness.  In no
#* event  shall  Alias Systems Corp. be liable for any  special,  indirect or
#* consequential  damages  or  any  damages whatsoever resulting from loss of
#* use, data  or profits, whether in  an  action of  contract,  negligence or
#* other tortious  action,  arising out of or  in connection  with the use or
#* performance of this software.
#* ==========================================================================
#*-
