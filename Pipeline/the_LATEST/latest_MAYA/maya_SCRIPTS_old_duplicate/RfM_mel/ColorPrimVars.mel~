// Use this for assigning one color per selected object. For example,
// 1  Select the objects.
// 2  Execute this command, addConstColorPrimVar("mycolor", <<0,0,0>>,<<1,1,1>>);
global proc addConstColorPrimVar(string $shortname, vector $minval, vector $maxval) {
	string $sel[] = `ls -sl`;
	string $fullname = "rmancC" + $shortname;
	for($current in $sel) {
	    string $shapes[] = `listRelatives -s $current`;
	    string $shape = $shapes[0];
	    if(attributeExists($fullname, $shape) == 0) {
	        addAttr -ln $fullname -sn $fullname -at "float3" -usedAsColor -k 1 $shape;
			addAttr -ln "r" -at "float" -k 1 -parent $fullname $shape;
			addAttr -ln "g" -at "float" -k 1 -parent $fullname $shape;
			addAttr -ln "b" -at "float" -k 1 -parent $fullname $shape;
	        }
	
	    vector $rgb = rand($minval, $maxval);
		float $r = $rgb.x;
	    float $g = $rgb.y;
	    float $b = $rgb.z;
	    setAttr ($shape + "." + $fullname) -type "float3" $r $g $b;
	    }
	}
//-------------------------------------------------------------------------------
// Assigns a color to each vertex of a mesh.
global proc addVertexColorPrimVar(string $shortname, vector $minval, vector $maxval) {
	string $sel[] = `ls -sl`;
	string $fullname = "rmanvC" + $shortname;
	for($current in $sel) {
	    string $shapes[] = `listRelatives -s $current`;
	    string $shape = $shapes[0];
	    if(attributeExists($fullname, $shape) == 0) {
	        addAttr -ln $fullname -sn $fullname -dt vectorArray -k 1 $shape;
	        }
	    int     $numVerts[] = `polyEvaluate -vertex $shape`;
	    string	$dataStr = $numVerts[0] + " ";
	    float  	$r, $g, $b;
	    for($n = 0; $n < $numVerts[0]; $n++) {
	        vector $rgb = rand($minval, $maxval);
			float $r = $rgb.x;
	    	float $g = $rgb.y;
	    	float $b = $rgb.z;
	        $dataStr = $dataStr + $r + " " + $g + " " + $b + " ";
	        }
	    eval("setAttr " + $shape + "." + $fullname + " -type vectorArray " + $dataStr);
	    }
	}
//-------------------------------------------------------------------------------
// Assigns a color to each face of a mesh.
global proc addFaceColorPrimVar(string $shortname, vector $minval, vector $maxval) {
	string $sel[] = `ls -sl`;
	string $fullname = "rmanuC" + $shortname;
	for($current in $sel) {
	    string $shapes[] = `listRelatives -s $current`;
	    string $shape = $shapes[0];
	    if(attributeExists($fullname, $shape) == 0) {
	        addAttr -ln $fullname -sn $fullname -dt vectorArray -k 1 $shape;
	        }
	    int     $numFaces[] = `polyEvaluate -f $shape`;
	    string     $dataStr = $numFaces[0] + " ";
	    float    $r, $g, $b;
	    for($n = 0; $n < $numFaces[0]; $n++) {
	        vector $rgb = rand($minval, $maxval);
			float $r = $rgb.x;
	    	float $g = $rgb.y;
	    	float $b = $rgb.z;
	        $dataStr = $dataStr + $r + " " + $g + " " + $b + " ";
	        }
	    eval("setAttr " + $shape + "." + $fullname + " -type vectorArray " + $dataStr);
	    }
	}
