/*
See,
www.fundza.com/rms/bake_brickgeo/index.html
Malcolm Kesson
*/
global proc readGeoBrickmapRI()
{
// Get the name of the shape node
string $shapeNode = `rman ctxGetObject`;
string $parents[] = `listRelatives -parent $shapeNode`;
string $tformNode = $parents[0];

// The node may hava a number in its name that we can use 
// to set the random number generator
int	$nodeNumber = `match "[0-9]+" $shapeNode`;	
if($nodeNumber != "") {
	seed(int($nodeNumber));
	}

string $attr;
$attr = `rmanGetAttrName "rgbm_path"`;
$attr = `rmanGetFullSharedGeometricAttrName $shapeNode $attr`;
string $rgbm_path = `getAttr $attr`;

$attr = `rmanGetAttrName "rgbm_ignore"`;
$attr = `rmanGetFullSharedGeometricAttrName $shapeNode $attr`;
int $rgbm_ignore = `getAttr $attr`;

$attr = `rmanGetAttrName "rgbm_trace_visibility"`;
$attr = `rmanGetFullSharedGeometricAttrName $shapeNode $attr`;
int $rgbm_trace_visibility = `getAttr $attr`;

$attr = `rmanGetAttrName "rgbm_shadingrate"`;
$attr = `rmanGetFullSharedGeometricAttrName $shapeNode $attr`;
float $rgbm_shadingrate = `getAttr $attr`;

// This saves having to hide the proxy when baking
string $str = `getAttr renderManGlobals.rman__torattr___postWorldBeginScript`;	
if(startsWith($str, "RiAtmosphere")) {
	RiAttribute "visibility" "int camera" 0;
	RiAttribute "visibility" "int transmission" 0;
	RiAttribute "visibility" "int trace" 0;
	return;
	}

if($rgbm_ignore)
	return;
		
if($rgbm_shadingrate > 0) {
	RiShadingRate($rgbm_shadingrate);
	}
RiAttribute("visibility", "int trace", $rgbm_trace_visibility);
RiAttribute("visibility", "int specular", $rgbm_trace_visibility);
RiAttribute("visibility", "int diffuse", $rgbm_trace_visibility);
RiAttribute("visibility", "int transmission", $rgbm_trace_visibility);
RiGeometry("brickmap", "string filename", $rgbm_path);

// "switch off" the proxy
RiAttribute "visibility" "int camera" 0;
RiAttribute "visibility" "int transmission" 0;
RiAttribute "visibility" "int trace" 0;
}
